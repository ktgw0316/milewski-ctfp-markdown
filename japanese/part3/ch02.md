# 随伴 {#adjunctions}

数学では、あるものが別のものに似ているという言い方はいろいろある。最も厳密なのは等価性だ。互いを区別する方法がなければ、2つのものは等しい。想像できるあらゆる状況において、一方を他方の代わりにできる。たとえば、可換図式について話すときはいつも射の[等価性]{.keyword #equality} (equality) を使っていることに気が付いていただろうか？
それは、射が集合 (hom集合) を形成し、集合の要素は等価性を確認できるからだ。

しかし、等価性は強すぎることが多い。実際には等価ではないのに、あらゆる意図と目的に照らして同じであるという例はたくさんある。たとえば、ペアの型`(Bool, Char)`は`(Char, Bool)`と厳密に等しいわけではないが、含んでいる情報が同じなのは分かっている。この概念を最もうまく捉えたものは、2つの型の間の*同型射*――反転可能な射だ。これは射なので、構造を保存する。そして同型 (isomorphism) の "iso" は、もとの場所に帰り着く往復旅行の一部であることを意味する。どちら側から出発するかは関係ない。ペアにおいては、この同型射は`swap`と呼ばれる:

```haskell
swap :: (a,b) -> (b,a)
swap (a,b) = (b,a)
```

`swap`はそれ自身の逆だ。

## 随伴と単位/余単位ペア

圏が同型だと述べるときは、これを圏間の写像、すなわち関手によって表す。圏$\cat{C}$から圏$\cat{D}$への逆変換可能な関手$R$ ("right") が存在する場合に、$\cat{C}$と$\cat{D}$は同型だと言えるようにしたい。言い換えると、$\cat{D}$から$\cat{C}$に戻る別の関手$L$ ("left") が存在し、$R$と合成することで恒等関手$I$に等しくなる。合成は$R \circ L$と$L \circ R$の2種類があるので、恒等関手も$\cat{C}$内と$\cat{D}$内の2種類がある。

![](images/adj-1.jpg){width=50%}

しかし、ここがややこしいところだ: 2つの関手が*等しい*とは何を意味するのだろうか？
次の等価性は何を意味するだろう:


$$R \circ L = I_{\cat{D}}$$

あるいは、次のものは:

$$L \circ R = I_{\cat{C}}$$

関手の等価性を対象の等価性として定義するのは合理的に思える。2つの関手が同じ対象に作用するなら、同じ対象が生成されるはずだ。しかし、一般に、対象の等価性の概念は任意の圏においては存在しない。それは単に定義の一部ではない。(この「等価性とはなにか」といううさぎの穴を深く掘り下げると、ホモトピー型理論に辿り着く。)


関手は圏の圏における射*である*から、それらは等価性で比較可能なはずだ、と主張したくなるかもしれない。実際、対象が集合を形成するような小さい圏を扱う限り、集合の要素の等価性を使って対象を等価性比較できる。

ただし、覚えておいてほしい。$\Cat$は実際には$\cat{2}$-圏だ。$\cat{2}$-圏のhom集合には追加の構造がある――$\cat{1}$-射の間に作用する$\cat{2}$-射がある。$\Cat$では、$\cat{1}$-射は関手であり、$\cat{2}$-射は自然変換だ。だから、関手について述べるときに自然同型を等価性の代わりと考えるのは、より自然なのだ (この駄洒落は避けようがない！)。

したがって、圏の同型の代わりに[同値性]{.keyword #equivalence} (equivalence) という、より一般的な概念を考えるのが理にかなっている。2つの圏$\cat{C}$と$\cat{D}$が*同値* (equivalent) であるとは、それらの間を行き来する2つの関手があり、(どちらかの向きの) 合成が恒等関手と[自然同型]{.keyword #naturally_isomorphic} (naturally isomorphic) であることだ。言い換えると、合成$R \circ L$と恒等関手$I_{\cat{D}}$の間には双方向の自然変換があり、$L \circ R$と恒等関手$I_{\cat{C}}$の間にも別の双方向の自然変換がある。

随伴は同値性よりもさらに弱い。なぜなら、2つの関手の合成が恒等関手と[同型]{.keyword #isomorphic} (isomorphic) でなくてもよいからだ。その代わり、$I_{\cat{D}}$から$R \circ L$への[一方向の]{.keyword #one_way2}自然変換と、$L \circ R$から$I_{\cat{C}}$への別の一方向の自然変換が存在する必要がある。これら2つの自然変換の表記を以下に示す:

$$
\begin{gathered}
\eta \Colon I_{\cat{D}} \to R \circ L \\
\varepsilon \Colon L \circ R \to I_{\cat{C}}
\end{gathered}
$$

$\eta$は随伴の単位 (unit) と呼ばれ、$\varepsilon$は余単位 (counit) と呼ばれる。

これら2つの定義の非対称性に注目してほしい。一般には、残りの2つの写像は存在しない:

$$
\begin{gathered}
R \circ L \to I_{\cat{D}} \quad\quad\text{必須ではない} \\
I_{\cat{C}} \to L \circ R \quad\quad\text{必須ではない}
\end{gathered}
$$

この非対称性のため、関手$L$は関手$R$に対する[左随伴]{.keyword #left adjoint} (left adjoint) と呼ばれ、関手$R$は$L$に対する右随伴 (right adjoint) と呼ばれる。(当然、左と右に意味があるのは図を特定の向きに描いた場合だけだ)。

随伴は次のように略記される:

$$L \dashv R$$

随伴をよりよく理解するために、単位と余単位をさらに詳しく分析してみよう。

![](images/adj-unit.jpg){width=50%}

まずは単位から始めよう。これは自然変換なので、射の族だ。$\cat{D}$内の対象$d$について、$\eta$のコンポーネントは、$I d$ ($d$に等しい) と $(R \circ L) d$ (図中の$d'$) の間の射となる:

$$\eta_d \Colon d \to (R \circ L) d$$

合成$R \circ L$は、$\cat{D}$の自己関手であることに注意してほしい。

この等式は、$\cat{D}$内の任意の対象$d$を始域として選択でき、往復旅行する関手$R \circ L$を使って終域$d'$を選択できることを示している。そして、射$\eta_d$という矢が終域に向けて放たれる。

![](images/adj-counit.jpg){width=50%}

同様に、余単位$\varepsilon$のコンポーネントは次のように記述できる:

$$\varepsilon_{c} \Colon (L \circ R) c \to c$$

これは、$\cat{C}$内の任意の対象$c$を終域として選択でき、ラウンドトリップ関手$L \circ R$を使って始域$c' = (L \circ R) c$を選択できることを示している。そして、射$\varepsilon_{c}$という矢が始域から終域に向けて放たれる。

単位と余単位について別の見方をすると、単位は$\cat{D}$に恒等関手を挿入できる場所ならどこでも合成$R \circ L$を*導入* (introduce) でき、余単位は$\cat{C}$の恒等射で置き換えることで合成$L \circ R$を*除去* (eliminate) できる。これにより、導入した後で除去すれば何も変更されないことを保証するいくつかの「自明な」整合性条件が導かれる:

$$
\begin{gathered}
L = L \circ I_{\cat{D}} \to L \circ R \circ L \to I_{\cat{C}} \circ L = L \\
R = I_{\cat{D}} \circ R \to R \circ L \circ R \to R \circ I_{\cat{C}} = R
\end{gathered}
$$

これらは、次の図式を可換にするので、三角恒等式 (triangular identity) と呼ばれる:

![](images/triangles.png)

![](images/triangles-2.png)

これらは関手圏の図式だ。つまり、矢は自然変換であり、それらの合成は自然変換の水平合成だ。コンポーネントで表すと、これらの恒等式は次のようになる:

$$
\begin{gathered}
\varepsilon_{L d} \circ L \eta_d = id_{L d} \\
R \varepsilon_{c} \circ \eta_{R c} = id_{R c}
\end{gathered}
$$

Haskellでは単位と余単位を別の名前でよく見かける。単位は`return` (あるいは`Applicative`の定義では`pure`) として知られている:

```haskell
return :: d -> m d
```

また、余単位は`extract`として知られている:

```haskell
extract :: w c -> c
```

ここで、`m`は$R \circ L$に対応する (自己) 関手であり、`w`は$L \circ R$に対応する (自己) 関手だ。後で述べるように、これらはそれぞれモナドとコモナドの定義の一部だ。

自己関手をコンテナと見なすなら、単位 (すなわち`return`) は任意の型の値に対して既定の箱を生成する多相関数だ。余単位 (すなわち`extract`) はその逆を行う: コンテナから単一の値を取得または生成する。

後で述べるように、随伴関手の各ペアはモナドとコモナドを定義する。逆に、すべてのモナドやコモナドは随伴関手のペアに分解され得る――ただし、その分解は一意ではない。

Haskellではモナドをよく使うが、それらを随伴関手のペアに分解することはめったにない。その主な理由は、それらの関手によって通常は$\Hask$の外へ追い出されるからだ。

しかし、Haskellでは[自己関手]{.keyword #endofunctor}の随伴を定義できる。以下は`Data.Functor.Adjunction`から引用した定義の一部だ:

```haskell
class (Functor f, Representable u) =>
      Adjunction f u | f -> u, u -> f where
    unit :: a -> u (f a)
    counit :: f (u a) -> a
```

この定義には説明が必要だ。まず、これは多パラメーター型クラスを記述している――2つのパラメーターは`f`と`u`だ。その2つの型コンストラクターの間に`Adjunction`という関係を確立している。

バーティカルバーの後の追加条件は、関数の依存関係を指定している。たとえば、`f -> u`は`f`によって`u`が決定されることを意味する (`f`と`u`の関係は関数で、ここでは型コンストラクターについての関数だ)。逆に、`u -> f`は、`u`が分かれば`f`が一意に決まることを意味する。

なぜHaskellでは右随伴`u`が[表現可能]{.keyword #representable2}関手であるという条件を課せるのかについては、すぐ後で説明する。

## 随伴とhom集合

随伴の等価な定義として、hom集合の自然同型によるものがある。その定義はこれまで学んだ普遍的構成とうまく結びついている。ある一意な射がある構成を分解しているという話を聞いたら毎回、それはある集合からhom集合への写像だと見なすべきだ。それが「一意な射を選択する」ということの意味だ。

さらに言うと、分解は自然変換によって記述されることが多い。分解には可換図式が関わる――ある射は2つの射 (因子) の合成に等しい。自然変換は射を可換図式に写す。したがって、普遍的構成では、射から可換図式へ、そして一意な射へ向かう。最終的には、射から射への写像、あるいはあるhom集合から別の (通常は異なる圏の) hom集合への写像が得られる。もしこの写像が可逆で、すべてのhom集合に自然に拡張できるならば、随伴が存在する。

普遍的構成と随伴の主な違いは、後者がすべてのhom集合に対して大域的に定義されていることだ。たとえば、普遍的構成を使えば選択した2つの対象の積を定義できる。これはその圏内の他の対象のペアに対して積が存在しない場合でも同様だ。すぐ後で説明するように、対象の*任意のペア*の積が圏に存在する場合は、随伴によって定義することもできる。

![](images/adj-homsets.jpg){width=50%}

これがhom集合を使った随伴の別の定義だ。前と同じように、2つの関手$L \Colon \cat{D} \to \cat{C}$と$R \Colon \cat{C} \to \cat{D}$がある。任意の2つの対象として$\cat{D}$内の始対象$d$と$\cat{C}$内の終対象$c$を選択しよう$L$を使えば始対象$d$を$\cat{C}$に写せる。これで$\cat{C}$内の2つの対象$L d$と$c$が得られた。これらはhom集合を定義する:

$$\cat{C}(L d, c)$$

同様に、$R$を使えば終対象$c$を写せる。これで$\cat{D}$内の2つの対象$d$と$R c$が得られた。これらもhom集合を定義する:

$$\cat{D}(d, R c)$$

次のようなhom集合の同型が存在する場合に限って$L$は$R$に対する左随伴と呼ばれる:

$$\cat{C}(L d, c) \cong \cat{D}(d, R c)$$

これは$d$でも$c$でも自然だ。

自然性は、始対象$d$を$\cat{D}$間でスムーズに変化させられ、終対象$c$を$\cat{C}$間でスムーズに変化させられることを意味する。より正確には、$\cat{C}$から$\Set$への次の2つの (共変) 関手間に自然変換$\varphi$が存在する。対象に対するこれらの関手の作用は次のとおりだ:

$$
\begin{gathered}
c \to \cat{C}(L d, c) \\
c \to \cat{D}(d, R c)
\end{gathered}
$$

もう一方の自然変換$\psi$は次の (反変) 関手間に作用する:

$$
\begin{gathered}
d \to \cat{C}(L d, c) \\
d \to \cat{D}(d, R c)
\end{gathered}
$$

自然変換は両方とも可逆でなければならない。

随伴の2つの定義が等しいことは簡単に示せる。例として、単位変換の導出を、hom集合の同型から始めてみよう:

$$\cat{C}(L d, c) \cong \cat{D}(d, R c)$$

この同型は任意の対象$c$で成り立つので、$c = L d$でも成り立つ必要がある:

$$\cat{C}(L d, L d) \cong \cat{D}(d, (R \circ L) d)$$

左辺は少なくとも1つの射、つまり恒等射を必ず含むのが分かっている。自然変換は、この射を$\cat{D}(d, (R \circ L) d)$ の要素に写す。すなわち、恒等関手$I$を挿入すると、以下の圏内の射に写す:

$$\cat{D}(I d, (R \circ L) d)$$

$d$でパラメーター化された射の族が得られた。それらは関手$I$と関手$R \circ L$の間に自然変換を形成する (自然条件は容易に確認できる)。これはまさに単位$\eta$だ。

逆に、単位と余単位の存在から始めれば、hom集合間の変換を定義できる。例として、hom集合$\cat{C}(L d, c)$ 内の任意の射$f$を選択しよう。$f$に作用して$\cat{D}(d, R c)$ 内に射を生成する$\varphi$を定義したい。

選択肢はあまりない。試せる方法のひとつは$R$を使って$f$をリフトすることだ。これにより、$R (L d)$ から$R c$への射$R f$――$\cat{D}((R \circ L) d, R c)$ の要素である射が生成される。

$\varphi$のコンポーネントに必要なのは$d$から$R c$への射だ。これは問題ではない。なぜなら、$\eta_d$のコンポーネントを使えば$d$から $(R \circ L) d$を得られるからだ。すると、次の結果が得られる:

$$\varphi_f = R f \circ \eta_d$$

他の方向についても同様であり、$\psi$の導出も同様だ。

Haskellでの`Adjunction`の定義に戻ると、自然変換$\varphi$と$\psi$はそれぞれ (`a`と`b`についての) 多相関数`leftAdjunct`と`rightAdjunct`に置き換えられる。関手$L$と$R$は`f`と`u`と呼ばれる。

```haskell
class (Functor f, Representable u) =>
  Adjunction f u | f -> u, u -> f where
    leftAdjunct  :: (f a -> b) -> (a -> u b)
    rightAdjunct :: (a -> u b) -> (f a -> b)

```

`unit`/`counit`の構成と`leftAdjunct`/`rightAdjunct`の構成の同値性は、次の対応によって示される:

```haskell
  unit           = leftAdjunct id
  counit         = rightAdjunct id
  leftAdjunct f  = fmap f . unit
  rightAdjunct f = counit . fmap f

```

随伴について圏論での記述からHaskellのコードへの翻案をなぞるのは非常に有益だ。演習として大いに推奨したい。

以上で、Haskellで右随伴が自動的に[表現可能関手](#representable-functors)になる理由を説明する準備ができた。その理由は、第1近似としては、Haskellの型の圏を集合の圏として扱えるからだ。

右圏$\cat{D}$が$\Set$であるとき、右随伴$R$は$\cat{C}$から$\Set$への関手だ。そのような関手が表現可能なのは、hom関手$\cat{C}(rep, \_)$ が$R$に対して自然同型であるような関手$rep$が$\cat{C}$内にある場合だ。$\Set$から$\cat{C}$へのある関手$L$に対して$R$が右随伴である場合、そのような対象は常に存在する――それは$L$の下の単集合 $()$ の像だ:

$$rep = L ()$$

実際、随伴は次の2つのhom集合が自然同型だと教えてくれる:

$$\cat{C}(L (), c) \cong \Set((), R c)$$

与えられた$c$について、右辺は単集合 $()$ から$R c$への関数の集合だ。そのような関数が集合$R\ c$からそれぞれ1つの要素を選択することはすでに説明した。そのような関数の集合は、集合$R c$の同型だ。したがって、次が成り立つ:

$$\cat{C}(L (), -) \cong R$$

これは$R$が実際に表現可能であることを示している。

## 随伴に基づく積

これまでに、普遍的構成を用いた概念をいくつか紹介した。これらの概念の多くは、大域的に定義されている場合、随伴を使って表現する方が簡単だ。最も単純な非自明の例は積だ。[積の普遍的構成](#products-and-coproducts)の要点は、普遍的構成を通じて積に似た候補を分解できることだ。

より正確には、2つの対象$a$と$b$の積は、2つの射$fst$と$snd$を伴う対象 $(a \times b)$ (またはHaskell表記の`(a, b)`) であり、かつ2つの射$p \Colon c \to a$と$q \Colon c \to b$を伴う他の候補$c$に対して、$p$と$q$を$fst$と$snd$を通じて分解する一意な射$m \Colon c \to (a, b)$ が存在する。

[すでに](#products-and-coproducts)見たように、Haskellでは、2つの射影からこの射を生成する`factorizer`を実装できる。

```haskell
factorizer :: (c -> a) -> (c -> b) -> (c -> (a, b))
factorizer p q = \x -> (p x, q x)
```

分解条件が成立することは簡単に確認できる:

```haskell
fst . factorizer p q = p
snd . factorizer p q = q
```

射のペア`p`と`q`をとり、もう一つの射`m = factorizer p q`を生成する写像が存在する。

これをどのように変換すれば、随伴を定義するために必要な2つのhom集合間の写像にできるだろう？
秘訣は、$\Hask$の外に出て、射のペアを直積圏内の単一の射として扱うことだ。

直積圏とは何か思い出してほしい。2つの任意の圏$\cat{C}$と$\cat{D}$を取り上げよう。直積圏内の対象$\cat{C}\times{}\cat{D}$は、1つは$\cat{C}$から、もう1つは$\cat{D}$からの対象のペアだ。射は、1つは$\cat{C}$から、もう1つは$\cat{D}$からの射のペアだ。

ある圏$\cat{C}$に積を定義するには、直積圏$\cat{C}\times{}\cat{C}$から始める必要がある。$\cat{C}$内の射のペアは、直積圏$\cat{C}\times{}\cat{C}$内の単一の射だ。

![](images/adj-productcat.jpg){width=50%}

積を定義するために直積圏を使うのは、最初は少し紛らわしいかもしれない。しかし、これらはまったく異なる積だ。普遍的構成は直積圏を定義するのに必要ない。必要なのは対象のペアと射のペアという概念だけだ。

ただし、$\cat{C}$からの対象のペアは、$\cat{C}$の対象では*ない*。それは別の圏$\cat{C}\times{}\cat{C}$の対象だ。このペアは形式的に$\langle a, b \rangle$と書ける。ここで、$a$と$b$は$\cat{C}$の対象だ。一方、普遍的構成は、*同じ*圏$\cat{C}$内の対象$a\times{}b$ (またはHaskellでは`(a, b)`) を定義するために必要だ。この対象は、普遍的構成によって指定された方法でペア$\langle a, b \rangle$を表すことになっている。これは常に存在するわけではなく、たとえ一部の対象に対して存在していても、$\cat{C}$内の他の対象のペアに対しては存在しない場合もある。

さて、`factorizer`をhom集合の写像として見てみよう。1番目のhom集合は直積圏$\cat{C}\times{}\cat{C}$内にあり、2番目は$\cat{C}$内にある。$\cat{C}\times{}\cat{C}$内の一般的な射は、射のペア$\langle f, g \rangle$となる:

$$
\begin{gathered}
f \Colon c' \to a \\
g \Colon  c'' \to b
\end{gathered}
$$

ここで$c''$は$c'$と異なる可能性がある。しかし、積を定義するために関心があるのは、$\cat{C}\times{}\cat{C}$内で同じ始対象$c$を共有する特別な射のペア$p$と$q$だ。それは構わない: 随伴の定義では、左hom集合の始域は任意の対象ではない――左関手$L$が右圏内の対象に作用した結果だ。要求に合う関手を推測するのは簡単だ――それは$\cat{C}$から$\cat{C}\times{}\cat{C}$への対角関手 (diagonal functor) $\Delta$で、対象への作用は次のようになる:

$$\Delta\ c = \langle c, c \rangle$$

したがって、ここでの随伴の左hom集合はこうなる:

$$(\cat{C}\times{}\cat{C})(\Delta\ c, \langle a, b \rangle)$$

これは直積圏のhom集合だ。その要素は、`factorizer`の引数として知っている射のペアだ:

$$(c \to a) \to (c \to b) \ldots{}$$

右hom集合は$\cat{C}$にあり、始対象$c$から、ある関手$R$が$\cat{C}\times{}\cat{C}$内の終対象に作用した結果へと向かう。これこそがペア$\langle a, b \rangle$を積対象$a\times{}b$に写す関手だ。hom集合のこの要素は`factorizer`の*結果*として認識される:

$$\ldots{} \to (c \to (a, b))$$

![](images/adj-product.jpg){width=50%}

まだ完全な随伴は得られていない。まず`factorizer`が反転可能である必要がある――hom集合の間に*同型射*を構築しているからだ。`factorizer`の逆は、ある対象$c$から積対象$a\times{}b$への射$m$で始まる必要がある。言い換えると、$m$は次のものの要素でなければならない:

$$\cat{C}(c, a\times{}b)$$

逆factorizerは$m$を$\langle c, c \rangle$から$\langle a, b \rangle$へ向かう$\cat{C}\times{}\cat{C}$内の射$\langle p, q \rangle$に写す。言い換えると、次のものの要素である射に写す:

$$(\cat{C}\times{}\cat{C})(\Delta\ c, \langle a, b \rangle)$$

この写像が存在するならば、対角関手に対して右随伴が存在すると結論できる。この関手は積を定義する。

Haskellでは、$m$を$fst$と$snd$でそれぞれ合成することで、常に`factorizer`の逆を合成できる。

```haskell
p = fst . m
q = snd . m
```

積を定義する2つの方法の同値性の証明を完成するには、hom集合間の写像が$a$、$b$、$c$で自然であることも示す必要がある。これは熱心な読者のための練習として残しておこう。

ここまでで行ったことを要約しよう: 圏論的な積は対角関手の[右随伴]{.keyword #right adjoint}として大域的に定義できる:

$$(\cat{C} \times{} \cat{C})(\Delta\ c, \langle a, b \rangle) \cong \cat{C}(c, a\times{}b)$$

ここで、$a\times{}b$は、ペア$\langle a, b \rangle$に対する右随伴関手$Product$の作用の結果だ。$\cat{C}\times{}\cat{C}$からの関手はすべて双関手であるため、$Product$は双関手であることに注意してほしい。Haskellでは、$Product$双関手は単に`(,)`と書かれる。次の例のように、2つの型にこれを適用すれば直積型を得られる:

```haskell
(,) Int Bool ~ (Int, Bool)
```

## 随伴に基づく冪乗

冪乗$b^a$、すなわち関数オブジェクト$a \Rightarrow b$は、[普遍的構成](https://bartoszmilewski.com/2015/03/13/function-types/)を使って定義できる。この構成は、対象のすべてのペアに対して存在するなら、随伴と見なせる。繰り返すが、秘訣は言明に集中することだ:

> 他の任意の対象$z$のうち、射$g \Colon z\times{}a \to b$を伴うものについて、一意な射$h \Colon z \to (a \Rightarrow b)$ が存在する。

この言明によりhom集合間の写像が確立される。

この例では、同じ圏内の対象を扱っているので、随伴関手は2つとも自己関手だ。左 (自己) 関手$L$は、対象$z$に作用すると、$z\times{}a$を生成する。ある固定された$a$について積を求めるのに相当する関手だ。

右 (自己) 関手$R$は、$b$に作用すると、関数オブジェクト$a \Rightarrow b$ (すなわち$b^a$) を生成する。ここでも、$a$は固定されている。この2つの関手間の随伴は、よく次のように記述される:

$$-\times{}a \dashv (-)^a$$

この随伴の基礎となるhom集合の写像を極力理解しやすくするには、普遍的構成で用いた図式を描き直せばよい。

![](images/adj-expo.jpg){width=40%}

射$eval$はこの随伴の余単位に他ならないことに注目してほしい:
<!-- \footnote{See ch.9 on \hyperref[function-types]{universal
construction}.} -->

$$(a \Rightarrow b)\times{}a \to b$$

ここで:

$$(a \Rightarrow b)\times{}a = (L \circ R) b$$

前に述べたとおり、普遍的構成は同型を除いて一意な対象を定義する。それが積を "the" product、冪乗を "the" exponentialと書く理由だ。この特性は随伴にも及ぶ。つまり、ある関手に随伴があるなら、その随伴は同型を除いて一意だ。

## 課題

1. 次の2つの (反変) 関手間の変換$\psi$について、自然性の正方図式を導出せよ:
   $$
   \begin{gathered}
   a \to \cat{C}(L a, b) \\
   a \to \cat{D}(a, R b)
   \end{gathered}
   $$
2. 随伴の第2の定義におけるhom集合同型から始めて、余単位$\varepsilon$を導出せよ。
3. 随伴の2つの定義について、同値性の証明を完成せよ。
4. 余積が随伴によって定義できることを示せ。余積のfactorizerの定義から始めよ。
5. 余積が対角関手の左随伴であることを示せ。
6. 積と関数オブジェクトの間の随伴をHaskellで定義せよ。
