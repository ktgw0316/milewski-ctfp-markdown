# モナド：プログラマーの定義

プログラマーたちは、モナドにまつわる神話を発展させてきた。それはプログラミングにおける最も抽象的で難しい概念の1つだと考えられている。「理解した」人とそうでない人がいる。多くの人にとって、モナドの概念を理解した瞬間はあたかも神秘的な体験のようだ。モナドは非常に多様な構成の本質を抽象化しているので、日常生活でうまく比喩できるものがない。そして我々は暗闇の中で手探りするようになった。まるで盲人たちが象のさまざまな部分に触れて「ロープだ」「木の幹だ」「ブリトーだ！」と勝ち誇って叫ぶように[^britto]。

[^britto]: 訳注：もともとの「群盲象を評す」というインドの寓話にはブリトー（小麦粉の薄焼き生地で具材を包んだ料理）は出てこない。モナドがしばしばブリトーに例えられるのには、Brent Yorgeyがブログで「モナドの変な理解の仕方」の例として挙げたのに対し、きちんと理解している人たちが「ブリトーとしてもちゃんと説明できる」とふざけて乗ってきた、という経緯がある。参考：

    * <https://byorgey.wordpress.com/2009/01/12/abstraction-intuition-and-the-monad-tutorial-fallacy/>
    * <https://blog.plover.com/prog/burritos.html>

誤解を正させてもらおう。モナドを取り巻く神秘主義はすべて誤解に基づいている。モナドはとてもシンプルな概念だ。混乱を引き起こしているのは、モナドの応用先の多様さなのだ。

この記事のための調査の一環として、私はダクトテープ（別名ダックテープ）とその応用先について調べた。それを使ってできることのほんの一例を紹介しよう。

* ダクトをシーリングする
* アポロ13号に搭載された二酸化炭素除去装置を修理する
* いぼを治療する
* アップルのiPhone 4の通話切断問題を修正する
* プロムで着るドレスを作る
* 吊り橋を建設する

仮にあなたがダクトテープが何か知らず、このリストに基づいて理解しようとしているところを想像してほしい。幸運を祈る！

そういうわけで、「モナドは……のようなものだ」という常套句のコレクションにもう1つ追加しようと思う。モナドはダクトテープのようなものだ。応用は幅広く多様だが、原理はごく単純だ。モナドは物同士をくっつける。より正確には、物同士を合成する。

このことは、多くのプログラマー、特に命令型言語をバックグラウンドに持つプログラマーがモナドを理解するのに困難が伴う理由の一部を説明している。問題は、我々がプログラミングを関数合成の観点から考えるのに慣れていないことにある。これはもっともなことだ。関数から関数へ値を直接渡さずに、中間値に名前を付けるのは我々にとってよくあることだ。また、しばしばグルーコードの短い断片を、ヘルパー関数に抽象化せずにインライン化する。以下は、ベクトルの長さを求める関数をC言語で命令型スタイルで実装したものだ。

```c
double vlen(double *v) {
    double d = 0.0;
    int n;
    for (n = 0; n < 3; ++n)
        d += v[n]* v[n];
    return sqrt(d);
}
```

\noindent
これを、明示的な関数合成を用いた（様式化された）Haskell版と比較してほしい。

```haskell
vlen = sqrt . sum . fmap  (flip (^) 2)
```

\noindent
(ここでは、より謎めかせるために、指数演算子`(^)`の2番目の引数を`2`に設定して部分適用した^[訳注：普通は、ポイントフリー記法を使う場合でも `sqrt . sum . fmap (^ 2)` または`sqrt . sum . map (^ 2)` とする。]。)

Haskellのポイントフリースタイルが常に優れていると主張しているのではなく、プログラミングで行うことのすべての基礎に関数合成があると主張しているだけだ。そして実質的には関数を合成しているにもかかわらず、Haskellではわざわざ`do`記法と呼ばれる命令型構文をモナド的な合成のために提供している。その使い方については後で説明する。まずは、そもそもなぜモナド的な合成が必要なのか説明しよう。

## クライスリ圏

以前、通常の関数を装飾することで[Writerモナド](#writer_monad)（第4章）に到達したのだった。この特定の装飾は、戻り値を文字列と、あるいはもっと一般にはモノイドの要素とペアにすることによってなされた。いまではそのような装飾が関手だと気付ける。

```haskell
newtype Writer w a = Writer (a, w)

instance Functor (Writer w) where
    fmap f (Writer (a, w)) = Writer (f a, w)
```

\noindent
続いて、以下の形式の装飾された関数すなわちクライスリ射 (Kleisli arrow) を合成する方法を発見したのだった。

```haskell
a -> Writer w b
```

\noindent
ログの累積は、まさにこの合成の内部で実装されているのだった。

いまではクライスリ圏をより一般的に定義する準備が完了している。まずは圏$\cat{C}$と自己関手$m$から始める。対応するクライスリ圏$\cat{K}$は$\cat{C}$と同じ対象を持つが、射は異なる。$\cat{K}$内の2つの対象$a$と$b$の間の射は、もとの圏$\cat{C}$内の射
$$a \to m\ b$$
として実装される。注意すべきは、$\cat{K}$内のクライスリ射は$a$と$b$の間の射として扱い、$a$と$m\ b$の間の射としては扱わないことだ。

我々の例においては、ある決まったモノイド`w`について、$m$は`Writer w`に特化されていた。

クライスリ射は、それらに対し適切な合成を定義できるときだけ圏をなす。合成があって、その合成が結合的かつどの対象についても恒等射を持つ場合、関手$m$は[*モナド*]{.keyword #monad}\index{モナド} (monad) と呼ばれ、その結果形成される圏はクライスリ圏と呼ばれる。

Haskellでは、クライスリ合成はfish演算子`>=>`を用いて定義され、その恒等射は`return`と呼ばれる多相関数だ。クライスリ合成を使ったモナドの定義は次のとおりだ^[訳注：現在のHaskellでは`Monad`は`Applicative`の派生クラス。`Applicative`では`pure :: a -> f a`が定義されており、`Monad`で`return = pure`以外の定義をすると警告が表示される。]。

```haskell
class Monad m where
    (>=>) :: (a -> m b) -> (b -> m c) -> (a -> m c)
    return :: a -> m a
```

\noindent
モナドを定義する方法には同等のものがいくつもあり、前述のものがHaskellエコシステムでの主要な方法ではないことを覚えておいてほしい。この方法の概念の簡潔さと得られる直観は気に入っているが、プログラミングの際に便利な定義は他にもある。それらについてはすぐに話すつもりだ。

この定式化ではモナド則を非常に簡単に表現できる。Haskellではモナド則を強制できないが、等式による推論には使える。それらは単にクライスリ圏の標準的な合成律だ。

```haskell
(f >=> g) >=> h = f >=> (g >=> h) -- 結合性
return >=> f = f                  -- 左単位元
f >=> return = f                  -- 右単位元
```

\noindent
この種の定義はモナドが本当は何なのかも表している。つまり、モナドは装飾された関数を合成する方法なのだ。副作用や状態は関係ない。関係あるのは合成だ。後で見るように、装飾された関数はさまざまな作用や状態を表現するために使えるが、モナドはそのためのものではない。モナドとは、装飾された関数の一端を別の装飾された関数の一端につなぐような粘着力のあるダクトテープなのだ。

`Writer`の例に戻ろう。ログ生成関数（`Writer`関手に対するクライスリ射）たちは圏をなす。なぜなら、`Writer`はモナドだからだ^[監訳注：本書では簡潔性からか `<>` 演算子には言及がないが、現在のHaskellでは`Data.Semigroup.<>` を用いて`mappend = (<>)` と定義されているので、ここでのコードについては`s <> s'`のように書いたほうがよりストレートだろう。]。

```haskell
instance Monoid w => Monad (Writer w) where
    f >=> g = \a ->
        let Writer (b, s)  = f a
            Writer (c, s') = g b
        in Writer (c, s `mappend` s')
    return a = Writer (a, mempty)
```

\noindent
`Writer w`のモナド則は、`w`のモノイド則が満たされている限り満たされる（これもHaskellでは強制できない）。

`Writer`モナドについて、`tell`と呼ばれる便利なクライスリ射が定義されている。その唯一の目的は引数をログに追加することだ。

```haskell
tell :: w -> Writer w ()
tell s = Writer ((), s)
```

\noindent
これは後で他のモナド的な関数の構成要素として使うことになる。

## Fishの解剖

さまざまなモナドに対してfish演算子を実装するとすぐ、コードに重複がたくさんあり、簡単に括り出せることに気付くだろう。まず、2つの関数のクライスリ合成は1つの関数を返す必要があるので、その実装も型`a`の引数を1つ取るラムダから始められる。

```haskell
(>=>) :: (a -> m b) -> (b -> m c) -> (a -> m c)
f >=> g = \a -> ...
```

\noindent
この引数は`f`に渡す以外のことはできない。

```haskell
f >=> g = \a -> let mb = f a
                in ...
```

\noindent
ここで、型`m c`の結果を生成する必要がある。そのために自由に使えるのは型`m b`の対象と関数`g :: b -> m c`だ。この関数は*bind*と呼ばれ、通常は中置演算子として書かれる。

```haskell
(>>=) :: m a -> (a -> m b) -> m b
```

\noindent
どんなモナドについても、fish演算子を定義する代わりにbindを定義できる。実際、標準的なHaskellのモナドの定義ではbindが使われている。

```haskell
class Monad m where
    (>>=) :: m a -> (a -> m b) -> m b
    return :: a -> m a
```

\noindent
以下に`Writer`モナドのbindの定義を示す。

```haskell
(Writer (a, w)) >>= f = let Writer (b, w') = f a
                        in  Writer (b, w `mappend` w')
```

\noindent
これは確かにfish演算子の定義より短い。

`m`が関手であるという事実を利用すればbindをさらに詳しく分析できる。`fmap`を使えば`m a`の内容に関数`a -> m b`を適用できる。それによって`a`は`m b`に変換される。したがって、適用結果の型は`m (m b)`となる。これは欲しかったものそのものではない――必要なのは型`m b`の結果だ――が、惜しいところまでは行っている。`m`の2重適用を潰す、つまりフラット化する関数さえあればよい。そのような関数は`join`と呼ばれる。

```haskell
join :: m (m a) -> m a
```

\noindent
`join`を使ってbindを次のように書き直せる。

```haskell
ma >>= f = join (fmap f ma)
```

\noindent
これはモナドの定義の3番目の選択肢につながる。

```haskell
class Functor m => Monad m where
    join :: m (m a) -> m a
    return :: a -> m a
```

\noindent
ここでは`m`が`Functor`であることを明示的に要求した。これまでの2つのモナドの定義ではその必要はなかった^[監訳注：実際のHaskellでは「その必要はない」ことをコンパイラーに教える手段が（少なくともまだ）ないので、標準的なHaskellのモナドの定義でも`m`が`Functor`であることは（より正確には`Applicative`経由で）明示的に要求される。もっとも、定義は効率を考えなければこの章にあるとおりであるが。]。型構成子`m`は、fish演算子とbind演算子のどちらかをサポートするなら自動的に関手になるからだ。たとえば、`fmap`はbindと`return`で定義できる。

```haskell
fmap f ma = ma >>= \a -> return (f a)
```

\noindent
完全を期すため、`Writer`モナドの`join`を以下に示す^[訳注：`join`も`bind`を用いて`join m = m >>= id`と定義できる。これが上記の定義と一致していることを確認してみよう。]。

```haskell
join :: Monoid w => Writer w (Writer w a) -> Writer w a
join (Writer ((Writer (a, w')), w)) = Writer (a, w `mappend` w')
```

## `do`記法

モナドを使ってコードを書く方法の1つは、クライスリ射の使用――fish演算子によるそれらの合成だ。このプログラミング手法はポイントフリースタイルを一般化したものだ。ポイントフリーのコードはコンパクトで、実にエレガントなことが多い。しかし、一般には、理解するのが難しくほとんど意味不明だ。だから、ほとんどのプログラマーは関数の引数や中間結果の値に名前を付ける方を好む。

それはつまり、モナドを扱う場合にはfish演算子よりもbind演算子が好ましいことを意味する。Bindはモナド的な値を取ってモナド的な値を返す。プログラマーはそれらの値に名前を付けることもできる。しかし、これは改善とは言えない。本当にやりたいことは、あたかも通常の値を扱っているかのようなふりであって、それらの値をカプセル化しているモナド的コンテナーを扱いたいのではない。そのことこそ命令型コードの仕組みだ――グローバルなログの更新などの副作用は、ほとんど目に付かないところに隠されている。そしてHaskellが`do`記法でエミュレートするものでもある。

ここで疑問に思うだろう。一体なぜモナドを使うのか？　副作用を見えなくしたいなら、命令型言語を使い続ければよいのではないか？　その答えは、モナドなら副作用をはるかにうまくコントロールできるからだ。たとえば、`Writer`モナドのログは関数から関数へ渡され、グローバルに公開されることはない。ログを改竄したりデータ競合を引き起したりする可能性はない。また、モナド的なコードはプログラムの他の部分から明確に区分され隔離される。

`do`記法はモナド的な合成のための単なる糖衣構文だ。表面上は命令型コードによく似ているが、bindとラムダ式のシーケンスに直接変換できる。

一例として、前に`Writer`モナドでのクライスリ射の合成を説明するために使った例を取り上げる。現在の定義を使うと、それは次のように書き直せる。

```haskell
process :: String -> Writer String [String]
process = upCase >=> toWords
```

\noindent
この関数は、動作ログを生成しつつ、入力文字列内のすべての文字を大文字に変換して単語に分割する。

`do`記法なら以下のようになるだろう。

```haskell
process s = do
    upStr <- upCase s
    toWords upStr
```

\noindent
ここで、`upCase`は`Writer`を生成するにもかかわらず

```haskell
upCase :: String -> Writer String String
upCase s = Writer (map toUpper s, "upCase ")
```

\noindent
のように`upStr`は単なる`String`になっている。これは`do`ブロックがコンパイラーによって次のように脱糖されるためだ。

```haskell
process s =
    upCase s >>= \upStr ->
        toWords upStr
```

\noindent
`upCase`のモナド的な結果と、`String`を引数とするラムダとがbindされている。まさにこの引数の名前が`do`ブロックに書かれていた。次の行

```haskell
upStr <- upCase s
```

\noindent
は、`upStr`が*得る*のは`upCase s`の結果だ、と読める。

擬似的な命令型スタイルは`toWords`をインライン化するとさらに顕著になる。2つの関数呼び出しに置き換えてインライン化しよう。まず`tell`を呼び出して、文字列`"toWords"`をログに記録する。続いて`return`を呼び出して、文字列`upStr`を`words`によって分割した結果を返す。`words`は文字列を扱う通常の関数であることに注意してほしい。

```haskell
process s = do
    upStr <- upCase s
    tell "toWords "
    return (words upStr)
```

\noindent
ここで、doブロックの各行を脱糖すると、ネストされた新たなbindが導入される。

```haskell
process s =
    upCase s >>= \upStr ->
      tell "toWords " >>= \() ->
        return (words upStr)
```

\noindent
`tell`はunit値を生成するので、後続のラムダに渡す必要はないことに注意してほしい。モナド的な結果の内容を無視する（ただし、その作用――ここではログへの寄与――は無視しない）のはよくあることだ。そのため、そのような場合にbindを置き換える特別な演算子がある。

```haskell
(>>) :: m a -> m b -> m b
m >> k = m >>= (\_ -> k)
```

\noindent
実際に脱糖されたコードは次のようになる。

```haskell
process s =
    upCase s >>= \upStr ->
      tell "toWords " >>
        return (words upStr)
```

\noindent
一般に`do`ブロックは、左矢印によってそれ以降のコードで使える名前を導入するような行や、純粋に副作用を目的として実行される行（またはサブブロック）から構成される。Bind演算子は行間で暗黙的に使われる。ちなみに、Haskellでは`do`ブロックの書式［訳注：改行やインデント］を波括弧とセミコロンで置き換えられる。これが、モナドはセミコロンをオーバーロードする方法だ、と表現される理由になっている。

`do`記法を脱糖する際にラムダとbind演算子がネストされることで、`do`ブロックの残りを各行の結果に基づいて実行するのに影響を与えることに注目してほしい。この性質は、例外をシミュレートするなど、複雑な制御構造を導入するために使える。

興味深いことに、`do`記法と同等のものが命令型言語に、特にC++に応用されている。再開可能な関数 (resumable function) やコルーチン (coroutine) のことだ。C++の[futureがモナドをなす](https://bartoszmilewski.com/2014/02/26/c17-i-see-a-monad-in-your-future/)^[<https://bartoszmilewski.com/2014/02/26/c17-i-see-a-monad-in-your-future/>] ことは秘密ではない。それは継続モナドの一例だ。継続モナドについてはすぐ後で議論する。継続の問題点は、合成するのが非常に難しいことだ。Haskellでは`do`という表記法を使うことで、「こっちのハンドラーがそっちのハンドラーを呼ぶ」というスパゲッティを、逐次コードに非常によく似たものに変換している。再開可能な関数によって、同じ変換がC++でも可能になる。
また、同じメカニズムを適用すれば、[ネストされたループのスパゲッティ](https://bartoszmilewski.com/2014/04/21/getting-lazy-with-c/)^[<https://bartoszmilewski.com/2014/04/21/getting-lazy-with-c/>] をリスト内包表記または「ジェネレーター」に変えられる。これは本質的にリストモナドの`do`記法だ。モナドによる統一的な抽象化がなければ、これらの個々の問題は通常、その問題に特化した拡張を言語に提供することで対処されることになる。Haskellでは、すべてライブラリーを通じて対処される。
