# カン拡張

ここまでは、主に1つの圏または1対の圏を扱ってきた。しかし、それでは制約が強すぎる場合もある。

たとえば、圏$\cat{C}$で極限を定義するとき、添字圏 (index category) $\cat{I}$を導入し、錐の基礎となるパターンのテンプレートとした。錐の頂点のテンプレートとしては、別の自明な圏を導入した方が理にかなっていただろう。それなのに、代わりに$\cat{I}$から$\cat{C}$への定関手$\Delta_c$を使った。

この不自然さを正す時が来た。極限を3つの圏を使って定義しよう。初めに、添字圏$\cat{I}$から$\cat{C}$への関手$D$について考える。これは錐の底面を指す関手――図式関手 (diagram functor) だ。

`\begin{figure}[H] \centering`{=latex}
![](images/kan2.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
新しく追加された圏$\cat{1}$は、1つの対象（および1つの恒等射）を含む。$\cat{I}$からこの圏への関手は$K$だけしかない。対象はすべて$\cat{1}$内の唯一の対象に写され、射はすべて恒等射に写される。$\cat{1}$から$\cat{C}$への関手$F$はどれも、ここでの錐の潜在的な頂点を指す。

`\begin{figure}[H] \centering`{=latex}
![](images/kan15.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
錐は$F \circ K$から$D$への自然変換$\varepsilon$だ。$F \circ K$はもとの$\Delta_c$と全く同じことを行う。次の図式はこの変換を示している。

`\begin{figure}[H] \centering`{=latex}
![](images/kan3-e1492120491591.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
ここで、このような関手$F$のうち「最も優れた」ものを選択するような普遍性を定義できる。この$F$は$\cat{1}$を$\cat{C}$内の$D$の極限である対象に写し、$F \circ K$から$D$への自然変換$\varepsilon$がそれに対応する射影を提供する。この普遍関手は$K$に沿った$D$の右カン拡張 (right Kan extension) と呼ばれ、$\Ran_{K}D$で表される。

普遍性を定式化しよう。錐がもう1つあるとする――別の関手$F'$と、$F' \circ K$から$D$への自然変換$\varepsilon'$から構成された錐だ。

`\begin{figure}[H] \centering`{=latex}
![](images/kan31-e1492120512209.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
カン拡張$F = \Ran_{K}D$が存在するなら、$F'$からそれへの一意な自然変換$\sigma$が必ず存在し、$\varepsilon'$は$\varepsilon$を通じて次のように分解される：
$$\varepsilon' = \varepsilon\ .\ (\sigma \circ K)$$
ここで、$\sigma \circ K$は2つの自然変換の水平合成だ（片方は$K$における恒等自然変換だ）。この変換は$\varepsilon$によって垂直合成される。

`\begin{figure}[H] \centering`{=latex}
![](images/kan5.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
成分でみると、$\cat{I}$内の対象$i$に作用するとき、次が得られる：
$$\varepsilon'_i = \varepsilon_i \circ \sigma_{K i}$$
この例において、$\cat{1}$の単一の対象に対応する$\sigma$の成分は1つしかない。したがって、これは実際に、$F'$が定義する錐の頂点から$\Ran_{K}D$が定義する普遍錐の頂点へ向かう一意な射だ。その可換条件はまさに極限の定義で求められているものだ。

もっとも、重要なのは、自明圏 (trivial category) $\cat{1}$を任意の圏$\cat{A}$に自由に置き換えてよく、右カン拡張の定義は有効なままだということだ。

## 右カン拡張

関手$K \Colon \cat{I} \to \cat{A}$に沿った関手$D \Colon \cat{I} \to \cat{C}$の右カン拡張は関手$F \Colon \cat{A} \to \cat{C}$であり、$\Ran_{K}D$と表記され、自然変換
$$\varepsilon \Colon F \circ K \to D$$
を伴い、他のすべての関手$F' \Colon \cat{A} \to \cat{C}$と自然変換
$$\varepsilon' \Colon F' \circ K \to D$$
について、一意な自然変換
$$\sigma \Colon F' \to F$$
が存在し、$\varepsilon'$を分解する：
$$\varepsilon' = \varepsilon\ .\ (\sigma \circ K)$$
これはかなり長ったらしいが、図式ならすっきりと描ける：

`\begin{figure}[H] \centering`{=latex}
![](images/kan7.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
この図式はあることに気付くと興味深く見えてくる。それはカン拡張が、ある意味で「関手の積」の逆のように作用していることだ。$\Ran_{K}D$に対して$D/K$という表記を使う著者さえいる。実際、その表記なら、右カン拡張の余単位とも呼ばれる$\varepsilon$の定義は単純な簡約のように見える：
$$\varepsilon \Colon D/K \circ K \to D$$
カン拡張には別の解釈もある。関手$K$が圏$\cat{I}$を$\cat{A}$の中に埋め込むとする。最も単純なケースでは、$\cat{I}$は単に$\cat{A}$の部分圏になる。$\cat{I}$を$\cat{C}$に写す関手$D$がある。$D$を$\cat{A}$全体で定義された関手$F$に拡張できるだろうか？　理想的には、そのような拡張によって合成$F \circ K$が$D$と同型になる。言い換えると、$F$は$D$の域を$\cat{A}$に拡張する。しかし、本格的な同型は通常求めすぎで、半分で十分だ。つまり、$F \circ K$から$D$への片道の自然変換$\varepsilon$だけでよい。（逆方向は左カン拡張が指す。）


`\begin{figure}[H] \centering`{=latex}
![](images/kan6.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
当然、この埋め込みの描像は、極限のような例では破綻する。関手$K$が対象の単射ではなく、すなわちhom集合の忠実関手ではないからだ。その場合、カン拡張は失われた情報を推定するために最善を尽くす。

## 随伴としてのカン拡張

さて、右カン拡張が任意の$D$（および決まった$K$）に対して存在するとしよう。この場合、（$D$をダッシュで置き換えた）$\Ran_{K}-$は関手圏${[}\cat{I}, \cat{C}{]}$から関手圏${[}\cat{A}, \cat{C}{]}$への関手だ。この関手は前合成関手$- \circ K$に対する右随伴だと分かる。この前合成関手は${[}\cat{A}, \cat{C}{]}$内の関手を${[}\cat{I}, \cat{C}{]}$内の関手に写す。この随伴はこうなる：
$$[\cat{I}, \cat{C}](F' \circ K, D) \cong [\cat{A}, \cat{C}](F', \Ran_{K}D)$$
これは、$\varepsilon'$と呼んでいたすべての自然変換が、$\sigma$と呼んでいた一意な自然変換に対応しているという事実を言い換えたものだ。

`\begin{figure}[H] \centering`{=latex}
![](images/kan92.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
さらに、圏$\cat{I}$を$\cat{C}$と同じになるように選んだ場合は、恒等関手$I_{\cat{C}}$を$D$に置き換えられる。すると、次の恒等式が得られる：
$$[\cat{C}, \cat{C}](F' \circ K, I_{\cat{C}}) \cong [\cat{A}, \cat{C}](F', \Ran_{K}I_{\cat{C}})$$
これで、$\Ran_{K}I_{\cat{C}}$と同じになるような$F'$を選択できる。その場合、右辺は恒等自然変換を含み、それに対応して、左辺から次の自然変換が得られる：
$$\varepsilon \Colon \Ran_{K}I_{\cat{C}} \circ K \to I_{\cat{C}}$$
これは随伴の余単位によく似ている：
$$\Ran_{K}I_{\cat{C}} \dashv K$$
実際、関手$K$に沿った恒等関手の右カン拡張を使って$K$の左随伴を計算できる。そのためには、もう1つの条件が必要だ。すなわち、右カン拡張が関手$K$によって保存されなければならない。このカン拡張の保存が意味するのは、この関手のカン拡張を$K$に前合成したものを計算すると、もとのカン拡張を$K$に前合成したのと同じ結果が得られるということだ。ここでは、この条件は次のように単純化される：
$$K \circ \Ran_{K}I_{\cat{C}} \cong \Ran_{K}K$$
$K$による除算で表記すると、前述の随伴は次のように記述できる：
$$I/K \dashv K$$
これは随伴がある種の逆を表しているという直観を裏付ける。また、保存の条件は次のようになる：
$$K \circ I/K \cong K/K$$
ある関手自身に沿った右カン拡張$K/K$は余稠密モナド (codensity monad) と呼ばれる。

この随伴の式は重要な結果だ。これからすぐ見るように、エンド（コエンド）を使ってカン拡張を計算でき、すなわち右（左）随伴を見出す実用的な手段が得られるからだ。

## 左カン拡張

左カン拡張を与えるような双対構成が存在する。直観を得るために、余極限の定義から始めて、単元圏$\cat{1}$を使うようにそれを再構築しよう。余錐を作るには、関手$D \Colon \cat{I} \to \cat{C}$を使って底面を形成し、関手$F \Colon \cat{1} \to \cat{C}$を使って頂点を選択する。

`\begin{figure}[H] \centering`{=latex}
![](images/kan81.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
余錐の側面である単射はどれも、$D$から$F \circ K$への自然変換$\eta$の成分だ。

`\begin{figure}[H] \centering`{=latex}
![](images/kan10a.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
余極限は普遍余錐だ。したがって、他のすべての関手$F'$と自然変換
$$\eta' \Colon D \to F' \circ K$$

`\begin{figure}[H] \centering`{=latex}
![](images/kan10b.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
について、$F$から$F'$への一意な自然変換$\sigma$

`\begin{figure}[H] \centering`{=latex}
![](images/kan14.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
が存在する。ただし：
$$\eta' = (\sigma \circ K)\ .\ \eta$$
これは次の図式で表される：

`\begin{figure}[H] \centering`{=latex}
![](images/kan112.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
単元圏$\cat{1}$を$\cat{A}$に置き換えると、この定義は左カン拡張の定義へと自然に一般化される。左カン拡張は$\Lan_{K}D$と表記される。

`\begin{figure}[H] \centering`{=latex}
![](images/kan12.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
自然変換：
$$\eta \Colon D \to \Lan_{K}D \circ K$$
は左カン拡張の単位と呼ばれる。

先ほどと同様に、自然変換の間の1対1の対応関係：
$$\eta' = (\sigma \circ K)\ .\ \eta$$
を随伴によって作り直せる：
$$[\cat{A}, \cat{C}](\Lan_{K}D, F') \cong [\cat{I}, \cat{C}](D, F' \circ K)$$
言い換えると、$K$に前合成することで、左カン拡張は左随伴となり、右カン拡張は右随伴となる。

恒等関手の右カン拡張が$K$の左随伴を計算するのに使われるように、恒等関手の左カン拡張は$K$の右随伴だと分かる（$\eta$は随伴の単位だとする）：
$$K \dashv \Lan_{K}I_{\cat{C}}$$
2つの結果を組み合わせると、次が得られる：
$$\Ran_{K}I_{\cat{C}} \dashv K \dashv \Lan_{K}I_{\cat{C}}$$

## エンドとしてのカン拡張

カン拡張の真の力は、エンド（およびコエンド）を使って計算できるという事実に由来する。簡単のため行き先の圏$\cat{C}$が$\Set$である場合に注目するが、どの式も任意の圏に拡張できる。

カン拡張を使えば関手の作用をもとの域の外側に拡張できる、という考え方を再検討してみよう。$K$が$\cat{I}$を$\cat{A}$の中に埋め込むとする。関手$D$は$\cat{I}$を$\Set$に写す。$K$の像内の任意の対象$a$について$a = K i$であり、拡張された関手は$a$を$D i$に写すと言える。問題は、$K$の像の外側にある$\cat{A}$内の対象をどうするかだ。そのような対象はどれもたくさんの射を通じて$K$の像内のすべての対象と潜在的につながっている、と考えればよい。関手はそれらの射を保存しなければならない。対象$a$から$K$の像への射の全体は次のhom関手によって特徴づけられる：
$$\cat{A}(a, K -)$$

`\begin{figure}[H] \centering`{=latex}
![](images/kan13.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
このhom関手は2つの関手の合成であることに注意してほしい：
$$\cat{A}(a, K -) = \cat{A}(a, -) \circ K$$
右カン拡張は関手合成の右随伴だ：
$$[\cat{I}, \Set](F' \circ K, D) \cong [\cat{A}, \Set](F', \Ran_{K}D)$$
$F'$をhom関手に置き換えるとどうなるか見てみよう：
$$[\cat{I}, \Set](\cat{A}(a, -) \circ K, D) \cong [\cat{A}, \Set](\cat{A}(a, -), \Ran_{K}D)$$
そして、先ほどの合成で表すとこうなる：
$$[\cat{I}, \Set](\cat{A}(a, K -), D) \cong [\cat{A}, \Set](\cat{A}(a, -), \Ran_{K}D)$$
右辺は米田の補題を使って簡潔にできる：
$$[\cat{I}, \Set](\cat{A}(a, K -), D) \cong \Ran_{K}D a$$
ここで、自然変換の集合をエンドに書き直すと、右カン拡張に対する非常に便利な式が得られる：
$$\Ran_{K}D a \cong \int_i \Set(\cat{A}(a, K i), D i)$$
左カン拡張に対しても、コエンドによって同様の式が得られる：
$$\Lan_{K}D a = \int^i \cat{A}(K i, a)\times{}D i$$
確認のため、これが実際に関手合成に対する左随伴であることを証明しよう：
$$[\cat{A}, \Set](\Lan_{K}D, F') \cong [\cat{I}, \Set](D, F' \circ K)$$
前述の式で左辺を置換しよう：
$$[\cat{A}, \Set](\int^i \cat{A}(K i, -)\times{}D i, F')$$
これは自然変換の集合なので、エンドに書き直せる：
$$\int_a \Set(\int^i \cat{A}(K i, a)\times{}D i, F' a)$$
Hom関手の連続性を使って、コエンドをエンドに置き換えられる：
$$\int_a \int_i \Set(\cat{A}(K i, a)\times{}D i, F' a)$$
積と冪についての随伴が使える：
$$\int_a \int_i \Set(\cat{A}(K i, a),\ (F' a)^{D i})$$
この冪は、対応するhom集合と同型だ：
$$\int_a \int_i \Set(\cat{A}(K i, a),\ \Set(D i, F' a))$$
Fubiniの定理と呼ばれる定理より、2つのエンドを交換してよい：
$$\int_i \int_a \Set(\cat{A}(K i, a),\ \Set(D i, F' a))$$
内側のエンドは2つの関手間の自然変換の集合を表しているので、米田の補題を使える：
$$\int_i \Set(D i, F' (K i))$$
この自然変換の集合はまさに、証明しようとしていた随伴の右辺を形成する：
$$[\cat{I}, \Set](D, F' \circ K)$$
このようなエンド・コエンド・米田の補題を使った計算は、エンドの「微積分」としてはごく典型的なものだ。

## Haskellでのカン拡張

カン拡張のエンド・コエンドの式はHaskellに簡単に変換できる。右カン拡張から始めよう：
$$\Ran_{K}D a \cong \int_i \Set(\cat{A}(a, K i), D i)$$
エンドを全称量化子に、hom集合を関数型に置き換える：

```haskell
newtype Ran k d a = Ran (forall i. (a -> k i) -> d i)
```

\noindent
この定義を見ると、`Ran`には、関数を適用できる型`a`の値と、2つの関手`k`と`d`の間の自然変換が含まれている必要があることは明らかだ。たとえば、`k`がtree関手、`d`がリスト関手だとして、`Ran Tree [] String`が与えられたとする。これは、関数：

```haskell
f :: String -> Tree Int
```

\noindent
を渡したなら`Int`のリストが返ってくる、などのように動作する。右カン拡張はこの関数を使って木を生成し、再パッケージしてリストにする。たとえば、文字列から構文解析木を生成するパーサーを渡すと、その木の深さ優先探索順に対応するリストが得られる。

右カン拡張は、関手`d`を恒等関手に置き換えれば、任意の関手の左随伴を計算するために使える。これにより、関手`k`の左随伴は、次の型の多相関数の集合で表される：

```haskell
forall i. (a -> k i) -> i
```

\noindent
`k`がモノイドの圏からの忘却関手だとしよう。すると、全称量化子はすべてのモノイドに及ぶ。もちろんHaskellではモノイド則を表現できないが、以下は結果の自由関手の適切な近似になっている（忘却関手`k`は対象における恒等関手だ）：

```haskell
type Lst a = forall i. Monoid i => (a -> i) -> i
```

\noindent
期待どおり、自由モノイド、すなわちHaskellのリストが得られる：

```haskell
toLst :: [a] -> Lst a
toLst as = \f -> foldMap f as

fromLst :: Lst a -> [a]
fromLst f = f (\a -> [a])
```

\noindent
この左カン拡張はコエンドだ：
$$\Lan_{K}D a = \int^i \cat{A}(K i, a)\times{}D i$$
したがって、存在量化子へと翻訳される。記号的に表すとこうなる：

```haskell
Lan k d a = exists i. (k i -> a, d i)
```

\noindent
これはHaskellでは*GADT*を使うか、全称量化されたデータ構成子を使ってコード化できる：

```haskell
data Lan k d a = forall i. Lan (k i -> a) (d i)
```

\noindent
このデータ構造を解釈すると、不特定の数の`i`を含むコンテナーを取って`a`を1つ生成する関数を含むものだと言える。また、それらの`i`のコンテナーも持つ。`i`が何かは分からないので、このデータ構造で唯一できるのは、`i`のコンテナーを取得し、自然変換を使って関手`k`で定義されたコンテナーに再パックし、関数を呼んで`a`を得ることだけだ。たとえば、`d`が木で`k`がリストの場合は、木を直列化して得られるリストで関数を呼べば`a`が得られる。

左カン拡張は関手の右随伴を計算するために使える。積関手の右随伴が冪なのは知っているので、カン拡張を使って実装してみよう：

```haskell
type Exp a b = Lan ((,) a) I b
```

\noindent
これが実際に関数型と同型であることは、次の関数のペアによって分かる：

```haskell
toExp :: (a -> b) -> Exp a b
toExp f = Lan (f . fst) (I ())

fromExp :: Exp a b -> (a -> b)
fromExp (Lan f (I x)) = \a -> f (a, x)
```

\noindent
すでに一般的な場合について説明したとおり、次のような段階を経ることに注意してほしい：

#. まず、`x`のコンテナー（ここでは単なる自明な恒等コンテナー）と関数`f`を得た。
#. 次に、そのコンテナーを、恒等関手と関手のペアとの間の自然変換を使って再パックした。
#. 最後に、関数`f`を呼んだ。

## 自由関手

カン拡張の興味深い応用として自由関手 (free functor) の構成がある。これは以下のような実際的な問題の解となる：対象の写像である型構成子があるとする。その型構成子に基づいて関手を定義可能だろうか？　言い換えると、その型構成子を完全な自己関手へと拡張するような射の写像を定義できるだろうか？

鍵となる観察は、型構成子は離散圏を域とする関手として表現できる、というものだ。離散圏の射は恒等射だけだ。任意の圏$\cat{C}$について、単に恒等射以外の射を捨て去れば、常に離散圏$\cat{|C|}$を構成できる。したがって、$\cat{|C|}$から$\cat{C}$への関手$F$は、対象の単純な写像、すなわちHaskellで型構成子と呼ばれるものになる。また、標準関手 (canonical functor) $J$も存在し、$\cat{|C|}$を$\cat{C}$内へ注入する。それは対象についての（および恒等射についての）恒等関手だ。$F$の$J$に沿った左カン拡張は、存在するなら、$\cat{C}$から$\cat{C}$への関手だ：
$$\Lan_{J}F a = \int^i \cat{C}(J i, a)\times{}F i$$
これは$F$に基づく自由関手と呼ばれる。

Haskellでは、次のように書ける：

```haskell
data FreeF f a = forall i. FMap (i -> a) (f i)
```

\noindent
実際、すべての型構成子`f`について`FreeF f`は関手となる：

```haskell
instance Functor (FreeF f) where
  fmap g (FMap h fi) = FMap (g . h) fi
```

\noindent
ご覧のとおり、自由関手は、関数と引数の両方を記録することで関数の持ち上げを偽装する。それは、関数合成を記録することによって、持ち上げられた関数を累積する。関手則は自動的に満たされる。この構成は論文[Freer Monads, More Extensible Effects](http://okmij.org/ftp/Haskell/extensible/more.pdf)^[<http://okmij.org/ftp/Haskell/extensible/more.pdf>]で使われたものだ。

別の方法として、右カン拡張も同じ目的に使える：

```haskell
newtype FreeF f a = FreeF (forall i. (a -> i) -> f i)
```

\noindent
これが実際に関手であることは簡単に確認できる：

```haskell
instance Functor (FreeF f) where
    fmap g (FreeF r) = FreeF (\bi -> r (bi . g))
```
