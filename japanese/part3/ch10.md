# エンドとコエンド {#ends-and-coends}

圏内の射に当てはまる直観はたくさんあるが、誰もが同意できるのは、対象$a$から対象$b$への射があれば、2つの対象には何らかの「関係がある」というものだ。ある意味で、射はこの関係の証明だ。このことは任意の半順序集合を圏と見なしたものにおいては一目瞭然となる。そこでは射が［訳注：半順序］関係*そのものである*からだ。一般に、2対象間の同一の関係についてもその「証明」は複数あり得る。それらの証明は我々がhom集合と呼んでいた集合をなす。対象を変化させていくことで、対象のペアから「証明」の集合への写像が得られる。この写像は関手的だ――1番目の引数については反変で、2番目の引数については共変だ。このことは、その圏内の対象間に大域的な関係を確立していると見なせる。この関係は次のhom関手で表せる：
$$\cat{C}(-, =) \Colon \cat{C}^\mathit{op}\times{}\cat{C} \to \Set$$
一般に、このような関手はすべて、圏内の対象間の関係を確立するものとして解釈できる。関係には2つの異なる圏$\cat{C}$と$\cat{D}$が含まれる場合もある。その場合の関手は、そのような関係を表すことになり、次のようなシグネチャーを持ち、プロ関手 (profunctor) と呼ばれる：
$$p \Colon \cat{D}^\mathit{op}\times{}\cat{C} \to \Set$$
数学者はこれを$\cat{C}$から$\cat{D}$へのプロ関手（反転に注意）と呼んでいて、斜線付き矢印で表記する：
$$\cat{C} \nrightarrow \cat{D}$$
プロ関手は、$\cat{C}$の対象と$\cat{D}$の対象の間の[*証明を区別する関係*]{.keyword #proof-relevant_relation}\index{証明を区別する関係} (proof-relevant relation) と見なせる。ここで、集合の要素は関係の証明を記号化している。$p\ a\ b$が空の場合は常に、$a$と$b$の間に関係はない。関係は対称である必要はないことに注意してほしい。

もう1つの有用な直観は、自己関手はコンテナーであるという考え方の一般化だ。型が$p\ a\ b$のプロ関手の値は、型$a$の要素をキーとする$b$のコンテナーと見なせる。特に、homプロ関手の要素は$a$から$b$への関数だ。

Haskellでは、プロ関手は2引数の型構成子`p`として定義され、関数のペアを持ち上げる`dimap`と呼ばれるメソッドを備えている。そのペアのうち1番目の関数は「間違った」方向を向いている：

```haskell
class Profunctor p where
    dimap :: (c -> a) -> (b -> d) -> p a b -> p c d
```

\noindent
プロ関手の関手性は、`a`が`b`に関係しているという証明があれば、`c`が`d`に関係しているという証明も、`c`から`a`への射と`b`から`d`への別の射がある限り手に入ることを示している。あるいは、1番目の関数は新しいキーを古いキーに変換し、2番目の関数はコンテナーの内容を変更するとも見なせる。

1つの圏内で作用するプロ関手では、型$p\ a\ a$の対角要素からかなり多くの情報が取り出せる。$b$が$c$に関係していることは、$b \to a$と$a \to c$という射のペアがある限り証明できる。さらには、たった1つの射を使って非対角値に到達することもできる。たとえば、射$f \Colon a \to b$があれば、ペア$\langle f, \idarrow[b] \rangle$を持ち上げることで$p\ b\ b$から$p\ a\ b$へ向かえる：

```haskell
dimap f id pbb :: p a b
```

\noindent
あるいは、ペア$\langle \idarrow[a], f \rangle$を持ち上げることでも$p\ a\ a$から$p\ a\ b$へ向かえる：

```haskell
dimap id f paa :: p a b
```

## 対角自然変換

プロ関手は関手なので、それらの間の自然変換は通常の方法で定義できる。ただし、多くの場合は2つのプロ関手の対角要素間の写像を定義するだけで十分だ。このような変換は、対角要素を非対角要素に接続できる2つの方法を反映した可換条件を満たす場合、対角自然変換 (dinatural transformation) と呼ばれる。関手圏${[}\cat{C}^\mathit{op}\times{}\cat{C}, \Set{]}$の要素である2つのプロ関手$p$と$q$について、その間の対角自然変換は射の族：
$$\alpha_a \Colon p\ a\ a \to q\ a\ a$$
で、任意の$f \Colon a \to b$について次の図式が可換となるようなものだ：

`\begin{figure}[H] \centering`{=latex}
![](images/end.jpg){width=35%}
`\end{figure}`{=latex}

\noindent
これは厳密に自然性条件より弱いことに注意してほしい。$\alpha$が${[}\cat{C}^\mathit{op}\times{}\cat{C}, \Set{]}$内の自然変換だったなら、前記の図式は2つの自然性の正方図式と1つの関手性条件（プロ関手$q$が合成を保存すること）から構成できただろう：

`\begin{figure}[H] \centering`{=latex}
![](images/end-1.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
${[}\cat{C}^\mathit{op}\times{}\cat{C}, \Set{]}$内の自然変換$\alpha$についての成分は、対象のペアによって$\alpha_{a b}$のように添字付けされていることに注意してほしい。一方で、対角自然変換は1つの対象によって添字付けされる。対応するプロ関手の対角要素のみを写すからだ。

## エンド

いまや「代数」から圏論の「微積分」と見なせるものへ進む準備ができた。エンド（およびコエンド）の微積分は、古典的な微積分から、その概念だけでなく記法さえいくつか借用している。特に、コエンドは無限和あるいは積分として理解でき、エンドは無限積に類似している。ディラックのデルタ関数に似たものさえある。

エンドは極限を一般化したもので、関手がプロ関手に置き換えられている。錐の代わりに、くさび (wedge) がある。くさびの底面はプロ関手$p$の対角要素によって形成される。くさびの頂点は対象（ここでは、$\Set$値のプロ関手を想定しているため、集合）であり、側面は頂点を底面内の集合に写す関数の族だ。この族は、1つの多相関数――戻り値の型が多相である関数――と見なせる：
$$\alpha \Colon \forall a\ .\ \mathit{apex} \to p\ a\ a$$
錐とは違って、くさびには底面の頂点同士を接続する関数はない。しかし、すでに見たとおり、$\cat{C}$内の任意の射$f \Colon a \to b$について、$p\ a\ a$と$p\ b\ b$の両方を共通の集合$p\ a\ b$に接続できる。したがって、次の図式が可換だと主張できる：

`\begin{figure}[H] \centering`{=latex}
![](images/end-2.jpg)
`\end{figure}`{=latex}

\noindent
これは[*くさび条件*]{.keyword #wedge_condition}\index{くさび条件} (wedge condition) と呼ばれる。これは次のように書ける：
$$p\ \idarrow[a]\ f \circ \alpha_a = p\ f\ \idarrow[b] \circ \alpha_b$$
あるいは、Haskellの記法を使うと次のようになる：

```haskell
dimap id f . alpha = dimap f id . alpha
```

\noindent
ここで普遍的構成を使って進み、$p$のエンドを普遍くさび (universal wedge) として定義できる。これは$e$と関数$\pi$の族の組で、頂点$a$と族$\alpha$の組からなる他のすべてのくさびについて一意な関数$h \Colon a \to e$が存在し、すべての三角図式を可換にする。すなわち：
$$\pi_a \circ h = \alpha_a$$

`\begin{figure}[H] \centering`{=latex}
![](images/end-21.jpg)
`\end{figure}`{=latex}

\noindent
エンドの記号は、下付きの「積分変数」を添えた積分記号だ：
$$\int_c p\ c\ c$$
$\pi$の成分はエンドの射影写像 (projection map) と呼ばれる：
$$\pi_a \Colon \int_c p\ c\ c \to p\ a\ a$$
$\cat{C}$が離散圏（恒等射以外の射がない）の場合、エンドは圏$\cat{C}$全体にわたる$p$のすべての対角要素の大域的な積になる。後ほど説明するが、より一般には、この積とエンドの間には等化子を介した関係がある。

Haskellでは、エンドの式は全称量化子 (universal quantifier) に直接変換される：

```haskell
forall a. p a a
```

\noindent
厳密に言うと、これは$p$のすべての対角要素の積にすぎないが、くさび条件は[パラメトリシティ](https://bartoszmilewski.com/2017/04/11/profunctor-parametricity/)^[<https://bartoszmilewski.com/2017/04/11/profunctor-parametricity/>]によって自動的に満たされる。任意の関数$f \Colon a \to b$について、くさび条件は次のようになる：

```haskell
dimap f id . pi = dimap id f . pi
```

\noindent
あるいは、型注釈を付けると次のようになる：

<div class="sourceCode"><pre
class="sourceCode haskell"><code class="sourceCode haskell">dimap f id<sub>b</sub> . pi<sub>b</sub> = dimap id<sub>a</sub> f . pi<sub>a</sub></code></pre></div>
\begin{Shaded}
\begin{Highlighting}[]
dimap f id\textsubscript{b} . pi\textsubscript{b} = dimap id\textsubscript{a} f . pi\textsubscript{a}
\end{Highlighting}
\end{Shaded}

\noindent
この式の両辺の型は：

```haskell
Profunctor p => (forall c. p c c) -> p a b
```

\noindent
であり、`pi`は多相射影だ：

```haskell
pi :: Profunctor p => forall c. (forall a. p a a) -> p c c
pi e = e
```

\noindent
ここで、型推論によって自動的に`e`の正しい成分が選択される。

錐のすべての可換条件を1つの自然変換として表せたように、すべてのくさび条件も1つの対角自然変換にまとめられる。そのためには、定関手$\Delta_c$を、対象のすべてのペアを単一の対象$c$に写し射のすべてのペアをその対象の恒等射に写すような定プロ関手 (constant profunctor) へと一般化する必要がある。
くさびは、その関手からプロ関手$p$への対角自然変換だ。実際、$\Delta_c$がすべての射を単一の恒等関数に持ち上げることに気付けば、対角自然性の六角形はくさびのダイヤモンドへと収縮できる。

行き先の圏が$\Set$でなくてもその圏に対してエンドを定義できるが、これ以降では集合値プロ関手 ($\Set$-valued profunctor) とそのエンドのみを扱う。

## 等化子としてのエンド

エンドの定義における可換条件は等化子を用いて書ける。まず、2つの関数を定義しよう（ここでは数学的記法はあまりユーザーフレンドリーではないと思うので、Haskellの記法を用いる）。それらの関数はくさび条件の2つの収束分岐 (converging branch) に対応する：

```haskell
lambda :: Profunctor p => p a a -> (a -> b) -> p a b
lambda paa f = dimap id f paa

rho :: Profunctor p => p b b -> (a -> b) -> p a b
rho pbb f = dimap f id pbb
```

\noindent
どちらの関数もプロ関手`p`の対角要素を次の型の多相関数に写す：

```haskell
type ProdP p = forall a b. (a -> b) -> p a b
```

\noindent
これらの関数の型は異なる。しかし、`p`のすべての対角要素を集めて大きな直積型を1つ形成すれば、それらの型を単一化できる。

```haskell
newtype DiaProd p = DiaProd (forall a. p a a)
```

\noindent
関数`lambda`と`rho`はこの直積型からの写像を2つ導出する：

```haskell
lambdaP :: Profunctor p => DiaProd p -> ProdP p
lambdaP (DiaProd paa) = lambda paa

rhoP :: Profunctor p => DiaProd p -> ProdP p
rhoP (DiaProd pbb) = rho pbb
```

\noindent
`p`のエンドはこれら2つの関数の等化子だ。等化子は2つの関数が等しい最大の部分集合を選択することを思い出してほしい。ここでは、すべての対角要素たちの積の部分集合でくさびの図式を可換にするものが選択される。

## エンドとしての自然変換

エンドの最も重要な例は自然変換の集合だ。2つの関手$F$と$G$の間の自然変換は、$\cat{C}(F a, G a)$ という形式のhom集合から選択された射の族だ。もし自然性条件がなければ、自然変換の集合はこれらすべてのhom集合の積にすぎないだろう。実際、Haskellでは次のようになる：

```haskell
forall a. f a -> g a
```

\noindent
Haskellでこれが動作する理由は、自然性がパラメトリシティから導かれるためだ。ただし、Haskell以外では、このようなhom集合に渡るすべての対角要素が自然変換を生成するとは限らない。しかし、写像：
$$\langle a, b \rangle \mapsto \cat{C}(F a, G b)$$
がプロ関手であることに注目すれば、そのエンドを研究するのは意味がある。くさび条件は次のとおりだ：

`\begin{figure}[H] \centering`{=latex}
![](images/end1.jpg){width=40%}
`\end{figure}`{=latex}

\noindent
集合$\int_c \cat{C}(F c, G c)$ から要素を1つだけ選択しよう。この要素は2つの射影によって特定の変換の2つの成分に写される。それらを次のように呼ぼう：
$$
\begin{aligned}
\tau_a &\Colon F a \to G a \\
\tau_b &\Colon F b \to G b
\end{aligned}
$$
図式の左側の分岐では、hom関手を使って射のペア$\langle \idarrow[a], G f \rangle$を持ち上げる。このような持ち上げを前合成と後合成として一度に実装したのを思い出してほしい。$\tau_a$に作用するとき、持ち上げられたペアから次が得られる：
$$G f \circ \tau_a \circ \idarrow[a]$$
図式のもう一方の分岐からは次が得られる：
$$\idarrow[b] \circ \tau_b \circ F f$$
くさび条件が要求するこれらの等しさは$\tau$の自然性条件に他ならない。

## コエンド

予想どおり、エンドの双対はコエンドと呼ばれる。それはくさびの双対で構成され、余くさび (cowedge) と呼ばれる（cowedgeの発音はco-wedgeで、cow-edgeではない）。

![エッジの効いた牛 (An edgy cow)？](images/end-31.jpg "エッジの効いた牛 (An edgy cow)？"){width=25%}

\noindent
コエンドの記号は、上付きの「積分変数」を添えた積分記号だ：
$$\int^c p\ c\ c$$
エンドが積に関連しているのと同様に、コエンドは余積すなわち和に関連している（この観点でコエンドは和の極限である積分に似ている）。射影を持つのではなく、プロ関手の対角要素からコエンドに向かう入射を持つ。余くさび条件がなければ、プロ関手$p$のコエンドは$p\ a\ a$、$p\ b\ b$、$p\ c\ c$などのどれかだと言えただろう。あるいは、コエンドが単なる集合$p\ a\ a$になるような$a$が存在すると言えただろう。エンドの定義で使った全称量化子は、コエンドでは存在量化子 (existential quantifier) になる。

このため、擬似Haskellでは、コエンドを次のように定義する：

```haskell
exists a. p a a
```

\noindent
Haskellで存在量化子を実装する標準的な方法では、全称量化されたデータ構成子を用いる[^Coend-GADT]。したがって、次のように定義できる：

```haskell
data Coend p = forall a. Coend (p a a)
```

[^Coend-GADT]:
    監訳注：個人的な好みの範疇だが、存在量化のための全称量化子を使うのは混乱するので、後述のGADT構文を使って以下のようにするのがおすすめである：

    `data Coend p where`

    $\ \ $`Coend :: p a a -> Coend p`

\noindent
この背後にあるロジックは、型$p\ a\ a$の族のうちどれからでも、$a$の選択によらずにコエンドを構築できるというものだ。

エンドが等化子を使って定義できるのと同様に、コエンドは[*余等化子*]{.keyword #coequalizer}\index{余等化子} (coequalizer) を使って記述できる。すべての余くさび条件は、要約すると、可能なすべての関数$b \to a$について$p\ a\ b$の単一の巨大な余積を取ることだ。これはHaskellでは存在型 (existential type) として表現される：

```haskell
data SumP p = forall a b. SumP (b -> a) (p a b)
```

\noindent
この直和型を評価する方法は2つある。`dimap`を使って関数を持ち上げる方法と、プロ関手$p$に適用する方法だ：

```haskell
lambda, rho :: Profunctor p => SumP p -> DiagSum p
lambda (SumP f pab) = DiagSum (dimap f id pab)
rho    (SumP f pab) = DiagSum (dimap id f pab)
```

\noindent
ここで、`DiagSum`は$p$の対角要素の和だ：

```haskell
data DiagSum p = forall a. DiagSum (p a a)
```

\noindent
これら2つの関数の余等化子はコエンドだ。余等化子は`DiagSum p`から、`lambda`または`rho`を同じ引数に対して適用することで得られる値を同一視することで得られる。ここで、引数は関数$b \to a$と$p\ a\ b$の要素のペアだ。`lambda`と`rho`を適用すると、異なるかもしれない2つの値が生成され、それらの型は`DiagSum p`だ。コエンドでは、これらの2つの値が同一視され、余くさび条件が自動的に満たされる。

集合内の関連する要素を同一視するこのプロセスは形式的には、商を取る、と呼ばれる。商を定義するには[*同値関係*]{.keyword #equivalence_relation}\index{同値関係} (equivalence relation) $\sim$、つまり、反射律・対称律・推移律を満たすような関係が必要となる：
$$
\begin{aligned}
&a \sim a \\
&a \sim b\ \text{ならば}\ b \sim a \\
&a \sim b\ \text{かつ}\ b \sim c\ \text{ならば}\ a \sim c
\end{aligned}
$$
このような関係は集合を同値類 (equivalence class) に分割する。各同値類は相互に関連する要素たちで構成される。商集合は各同値類から1つの代表を選んで作られる。古典的な例は、以下の同値関係を持つ整数のペアの集合としての有理数の集合の定義だ：
$$(a, b) \sim (c, d)\ \text{iff}\ a * d = b * c,\ b \neq 0,\ d \neq 0$$
これが同値関係なのは簡単に確認できる。ペア $(a, b)$ は分数$\frac{a}{b}$と見なせて、分子と分母が公約数を持つものは同一視される。有理数はそのような分数の同値類だ。

極限と余極限についての以前の議論から、hom関手は連続的、すなわち極限を保存することを思い出してほしい。双対性より、反変hom関手は余極限を極限に変える。これらの特性はエンドとコエンドに一般化できる。これらは、それぞれ極限と余極限を一般化したものだ。特に、コエンドをエンドに変換するのに非常に便利な恒等式が得られる：
$$\Set(\int^x p\ x\ x, c) \cong \int_x \Set(p\ x\ x, c)$$
擬似Haskellで見てみよう：

<div class="sourceCode"><pre
class="sourceCode haskell"><code class="sourceCode haskell">(exists x. p x x) -> c ≅ forall x. p x x -> c</code></pre></div>
\begin{Shaded}
\begin{Highlighting}[]
(exists x. p x x) -> c \ensuremath{\cong} forall x. p x x -> c
\end{Highlighting}
\end{Shaded}

これは、存在型を取る関数はある多相関数と等価だと示している。これは完全に理にかなっている。そのような関数は、存在型として表される可能性のある型ならどれでも処理できるようになっている必要があるからだ。これは、直和型を受け入れる関数についての、すべての型に対応したハンドラーの組を持ったcase式として実装される、という原則と同じものだ。ここでは、直和型はコエンドに置き換えられ、ハンドラーの族はエンドすなわち多相関数になる。

## 忍者米田の補題

米田の補題に現れる自然変換の集合はエンドを使って表せて、結果としてこう定式化できる：
$$\int_z \Set(\cat{C}(a, z), F z) \cong F a$$
双対として次の式も存在する：
$$\int^z \cat{C}(z, a)\times{}F z \cong F a$$
この恒等式はディラックのデルタ関数の式（関数$\delta(a - z)$、というより$a = z$に無限大のピークを持つ分布）を強く連想させる。ここでは、hom関手がデルタ関数の役割を果たしている。

これら2つの恒等式を合わせて忍者米田の補題 (Ninja Yoneda lemma) と呼ぶことがある^[訳注：この名前は、圏論に造詣が深いオーストラリアの数学者たちをレンスター (Tom Leinster) が「忍者圏論家たち」と形容したことに由来する。ただし、忍者圏論家たち自身は単に米田の定理と呼んでいた。<https://mathoverflow.net/questions/20445/coend-computation/20451#20451>を参照のこと。なお、レンスターは『ベーシック圏論』の著者。]。

2番目の式を証明するには米田埋め込みからのある帰結を使う。つまり、2つの対象が同型となるのは、それらのhom関手が同型である場合に限られる。言い換えると、$a \cong b$となるのは、次の型の自然変換：
$$[\cat{C}, \Set](\cat{C}(a, -), \cat{C}(b, =))$$
があって同型射である場合だけだ。

まず、証明したい同型の左辺を、任意の対象$c$に向かうhom関手の内側に挿入する：
$$\Set(\int^z \cat{C}(z, a)\times{}F z, c)$$
連続性の議論を使うと、コエンドをエンドに置き換えられる：
$$\int_z \Set(\cat{C}(z, a)\times{}F z, c)$$
これで積と冪の間の随伴を利用できるようになった：
$$\int_z \Set(\cat{C}(z, a), c^{(F z)})$$
米田の補題を使って「積分を実行」し、次が得られる：
$$c^{(F a)}$$
(関手$c^{(F z)}$が$z$について反変なので、米田の補題の反変版を使っていることに注意してほしい。)
この冪対象は次のhom集合と同型だ：
$$\Set(F a, c)$$
最後に、米田埋め込みを利用して以下の同型に辿り着く：
$$\int^z \cat{C}(z, a)\times{}F z \cong F a$$

## プロ関手の合成

プロ関手が記述するのは関係――より正確には証明を区別する関係――だ、という考え方をさらに探求してみよう。つまり、集合$p\ a\ b$は、$a$が$b$に関連する証明の集合を表す。2つの関係$p$と$q$があるなら、それらを合成できる。$q\ b\ c$と$p\ c\ a$が両方とも空でないような中間対象$c$が存在するとき、$q$を$p$の後に合成したものを介して$a$は$b$に関連していると言うことにする。この新しい関係の証明はすべて、個々の関係の証明の任意のペアである。したがって、存在量化子はコエンドに対応し、2つの集合のデカルト積は「証明のペア」に対応するという理解に基づいて、次の方法でプロ関手の合成を定義できる：
$$(q \circ p)\ a\ b = \int^c p\ c\ a\times{}q\ b\ c$$
Haskellの`Data.Profunctor.Composition`における、プロ関手の同等の定義は以下のとおりだ。ただし、名前が多少変更されている：

```haskell
data Procompose q p a b where
  Procompose :: q a c -> p c b -> Procompose q p a b
```

\noindent
これは一般化代数的データ型 (generalized algebraic data type、略称GADT) の構文を使っており、自由型変数（ここでは`c`）が自動的に存在量化される。したがって、（非カリー化された）データ・コンストラクター`Procompose`は次と等価だ：

```haskell
exists c. (q a c, p c b)
```

\noindent
こうして定義された合成の単位元はhom関手だ――これは忍者米田の補題から直ちに帰結される。そこで、プロ関手が射の役割を果たす圏があるかを問うのは理にかなっている。ある、というのがその問いの答えだ。ただし、プロ関手の合成に関する結合律と恒等律は自然同型を除いてのみ満たされることに注意する必要がある。それらの結合律と恒等律が同型を除いて満たされる圏は、双圏 (bicategory) と呼ばれる（これは$\cat{2}$-圏より一般的である）。すなわち、双圏$\cat{Prof}$では、対象は圏であり、射はプロ関手であり、射同士の間の射 (別名2-セル) は自然変換だ。実際には、さらに先へも進める。プロ関手以外にも圏間の射としての通常の関手があるからだ。2種類の射を持つ圏は二重圏 (double category) と呼ばれる。

プロ関手はHaskellの`lens`ライブラリーと`arrow`ライブラリーで重要な役割を果たしている[^arrow][^profunctor-optics]。

[^arrow]:
    訳注：少なくとも標準ライブラリー（base）に含まれる`Control.Arrow`にはプロ関手についての陽な言及はない。しかしながら、arrowはプロ関手と関連が深く、（`Arrow`のメソッド`first :: p a b -> p (a, c) (b, c)` に対応する）strengthと呼ばれる演算を持つようなプロ関手たちの圏におけるモノイド対象となっている。参考：

    - Exequiel Rivas, Mauro Jaskelioff:
Notions of computation as monoids. J. Funct. Program. 27: e21 (2017)
    - Bart Jacobs, Chris Heunen, Ichiro Hasuo:
Categorical semantics for arrows. J. Funct. Program. 19(3-4): 403-438 (2009)

[^profunctor-optics]:
    監訳注：興味のある読者は、profunctor opticsというプロ関手を用いたレンズやその関連概念の抽象化についても調べるとよい。

    - Matthew Pickering, Jeremy Gibbons, and Nicolas Wu: Profunctor Optics: Modular Data Accessors, The Art, Science, and Engineering of Programming, 2017, Vol. 1, Issue 2, Article 7. <https://doi.org/10.22152/programming-journal.org/2017/1/7>
    - <https://hackage.haskell.org/package/profunctor-optics>
