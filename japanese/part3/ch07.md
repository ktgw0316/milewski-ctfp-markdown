# コモナド {#comonads}

これでモナドについてはカバーできたので、双対性の恩恵によって、射を逆にして反対圏で作業するだけでコモナドを無料で手に入れられる。

最も基本的なレベルでは、モナドはクライスリ射の合成についてのものであったのを思い出してほしい：

```haskell
a -> m b
```

\noindent
ここで`m`はモナドである関手だ。コモナドを文字`w`（逆さの`m`）で表すと、余クライスリ射 (co-Kleisli arrow) を次の型の射として定義できる：

```haskell
w a -> b
```

\noindent
余クライスリ射についてfish演算子に相当するものは次のように定義される：

```haskell
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
```

\noindent
余クライスリ射が圏をなすには`extract`と呼ばれる恒等余クライスリ射も必要となる：

```haskell
extract :: w a -> a
```

\noindent
これは`return`の双対だ。さらに、`extract`が左恒等射かつ右恒等射であることだけでなく、結合律も必須だ。すべてをまとめると、Haskellではコモナドを次のように定義できるように思われる：

```haskell
class Functor w => Comonad w where
    (=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
    extract :: w a -> a
```

\noindent
実用上は、後で説明するように、わずかに異なるプリミティブが使われる。

問題は、コモナドがプログラミングでどのように使われるかだ。

## コモナドでのプログラミング

モナドとコモナドを比べてみよう。モナドはコンテナーに値を入れる手段を`return`によって提供している。しかし、内部に格納された値にアクセスする手段は提供しない。もちろんモナドを実装するデータ構造は内容へのアクセスを提供する場合もあるかもしれないが、それはおまけと見なされる。モナドから値を抽出するための共通したインターフェイスはない。そして、内容を決して公開しないことを誇りにしている`IO`モナドという例を我々はすでに見た。

一方、コモナドは値をひとつ取り出す手段を提供している。しかし、値を入れる手段は提供しない。コモナドをコンテナーと見なすとしたら、それは中身がいつも事前に満たされていて、覗き見られるようになっているようなコンテナーだ。

クライスリ射は値を受け取り、文脈で装飾した結果を生成する。同様に、余クライスリ射は文脈全体と共に値を受け取り、結果を生成する。これは[*文脈付き計算*]{.keyword #contextual_computation}\index{文脈付き計算} (contextual computation) の実施例だ。

## 積コモナド

Readerモナドを覚えているだろうか？　それを導入した理由は、何らかの読み取り専用の環境`e`へのアクセスを必要とする計算を実装する問題に取り組むためだった。そのような計算は純粋関数として次の形で表せる：

```haskell
(a, e) -> b
```

\noindent
それらをクライスリ射に変えるのにはカリー化を使った：

```haskell
a -> (e -> b)
```

\noindent
しかし、この関数がすでに余クライスリ射の形をしていることに注目してほしい。引数をもっと便利な関手の形式：

```haskell
data Product e a = Prod e a deriving Functor
```

\noindent
に書き換えてみよう。合成演算子は簡単に定義できる。合成しようとしている射でも同じ環境を使えるようにすればよい：

```haskell
(=>=) ::
  (Product e a -> b) ->
  (Product e b -> c) ->
  (Product e a -> c)
f =>= g = \(Prod e a) -> let b = f (Prod e a)
                             c = g (Prod e b)
                         in c
```

\noindent
`extract`の実装は環境を単に無視する：

```haskell
extract (Prod e a) = a
```

\noindent
驚くまでもなく、積コモナドではreaderモナドと全く同じ計算が実行できる。ある意味で、環境をコモナドで実装するのはより自然だ――「文脈付き計算」の精神に則っている。一方、モナドは`do`記法という便利な糖衣構文を備えている。

readerモナドと積コモナドの関係はさらに深く、reader関手が積関手の右随伴であるという事実に関連している。しかし、一般的には、コモナドはモナドとは異なる計算の概念を扱う。さらなる例について後で説明する。

積コモナド`Product`は、組やレコードを含む任意の直積型へ簡単に一般化できる。

## 合成の分析

双対化のプロセスを続けることで、さらに進めてモナド的なbindとjoinを直接双対化することも可能かもしれない。その代わりに、それらを得るためにモナドで用いたプロセスを繰り返すこともできる。そこではfish演算子の解剖学を研究した。そのアプローチの方が啓発的に思える。

出発点は、合成演算子は`w a`を取り`c`を生成する余クライスリ射を生成しなければならないという認識だ。`c`を生成する唯一の方法は、第2の関数を型`w b`の引数に適用することだ：

```haskell
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
f =>= g = g ...
```

\noindent
だが、`g`に与えうる型`w b`の値はどうすれば生成できるだろうか？　型`w a`の引数と関数`f :: w a -> b`は自由に使える。答えはbindの双対を定義することだ。それはextendと呼ばれる：

```haskell
extend :: (w a -> b) -> w a -> w b
```

\noindent
`extend`を使えば合成を実装できる：

```haskell
f =>= g = g . extend f
```

\noindent
次は`extend`を解剖できるだろうか？　なぜ単に関数`w a -> b`を引数`w a`に適用しないのか、と言いたくなるかもしれない。しかし、結果として得られる`b`を`w b`に変換する方法がないことにすぐ気付くだろう。コモナドは値を持ち上げる手段を提供しないことを思い出してほしい。この時点で、モナドについての類似した構成では`fmap`を用いていた。ここで`fmap`が使えるとすれば、型`w (w a)`の何かが自由に使える場合だけだ。`w a`を`w (w a)`に変えられさえすればよい。そして、便利なことに、それはまさに`join`の双対であればよい。それは`duplicate`と呼ばれる：

```haskell
duplicate :: w a -> w (w a)
```

\noindent
したがって、モナドと同様に、コモナドにも3つの等価な定義がある。すなわち、余クライスリ射によるものと、`extend`によるものと、`duplicate`によるものだ。Haskellの`Control.Comonad`ライブラリーから直接引用した定義を以下に示す：

```haskell
class Functor w => Comonad w where
  extract :: w a -> a
  duplicate :: w a -> w (w a)
  duplicate = extend id
  extend :: (w a -> b) -> w a -> w b
  extend f = fmap f . duplicate
```

\noindent
`extend`のデフォルト実装が`duplicate`によって提供され、逆も同様だ。したがって、どちらか1つをオーバーライドするだけでよい。

これらの関数の背景にある直観は、一般にコモナドは型`a`の値で満たされたコンテナーと見なせるという考えに基づいている（積コモナドは値が1つしかない特殊なケースだ）。「現在の」値という概念があり、それは`extract`によって簡単にアクセスできる。余クライスリ射は現在の値に焦点を合わせて計算を実行するものの、周囲のすべての値にアクセスできる。コンウェイのライフゲームを考えてみてほしい。各セルには値（通常は単に`True`か`False`）が含まれる。ライフゲームに対応するコモナドは「現在の」セルに焦点が合っているようなセルのグリッドになるだろう。

では`duplicate`は何をするのだろう？　それはコモナド的なコンテナー`w a`を取ってコンテナーのコンテナー`w (w a)`を生成する。つまり、それぞれのコンテナーが`w a`内部の異なる`a`に焦点を合わせているという考え方だ。ライフゲームでは、グリッドのグリッドが得られる。外側のグリッドの各セルには、個別のセルに焦点を合わせた内側のグリッドが含まれている。

次に`extend`を見てみよう。それは、余クライスリ射と、複数の`a`で満たされたコモナド的コンテナー`w a`を取る。そして、すべての`a`に計算を適用して`b`に置き換える。結果は複数の`b`で満たされたコモナド的コンテナーとなる。これを実現するために`extend`は焦点を合わせる`a`を次々にシフトしていき、それぞれに余クライスリ射を適用する。ライフゲームでは、余クライスリ射は現在のセルの新しい状態を計算する。そのために文脈（すなわち最近傍セル）に注目する。`extend`のデフォルト実装はこの過程を示している。まず`duplicate`を呼び出して可能な焦点をすべて生成し、次にそのそれぞれに`f`を適用する。

## Streamコモナド

コンテナー内で焦点を合わせる要素を次々にシフトしていくこのプロセスを最もうまく説明できる例は無限ストリームだ。そういったストリームはリストにそっくりだが、空のコンストラクターがない点で異なる：

```haskell
data Stream a = Cons a (Stream a)
```

\noindent
これが`Functor`であるのは自明だ：

```haskell
instance Functor Stream where
    fmap f (Cons a as) = Cons (f a) (fmap f as)
```

\noindent
ストリームの焦点はその最初の要素なので、`extract`の実装は次のようになる：

```haskell
extract (Cons a _) = a
```

\noindent
`duplicate`は、それぞれ異なる要素に焦点を合わせた複数のストリームからなるストリームを1つ生成する：

```haskell
duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
```

\noindent
最初の要素はもとのストリーム、2番目の要素はもとのストリームのtail、3番目の要素はさらにそのtail、というように無限に続く。

完全なインスタンス宣言を以下に示す：

```haskell
instance Comonad Stream where
    extract (Cons a _) = a
    duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
```

\noindent
これは非常に関数的な観点でのストリームの捉え方だ。命令型言語なら、ストリームを1要素だけシフトするような`advance`メソッドから始めただろう。しかし、ここでの`duplicate`はシフトされたすべてのストリームを一気に生成する。Haskellの遅延評価がこれを可能にし、望ましいものにさえしている。もちろん、`Stream`を実用的なものにするために、`advance`に類似したものも実装しよう：

```haskell
tail :: Stream a -> Stream a
tail (Cons a as) = as
```

\noindent
ただし、これはコモナドのインターフェイスの一部では決してない。

デジタル信号処理の経験があればすぐ分かるとおり、ストリームについての余クライスリ射は単なるデジタルフィルターであり、`extend`はフィルターされたストリームを生成する。

簡単な例として、移動平均フィルターを実装してみよう。ストリームの`n`個の要素を合計する関数はこう書ける：

```haskell
sumS :: Num a => Int -> Stream a -> a
sumS n (Cons a as) = if n <= 0 then 0 else a + sumS (n - 1) as
```

\noindent
そして、ストリームの最初から`n`個の要素の平均を計算する関数はこう書ける：

```haskell
average :: Fractional a => Int -> Stream a -> a
average n stm = (sumS n stm) / (fromIntegral n)
```

\noindent
部分適用された`average n`は余クライスリ射なので、ストリーム全体に`extend`できる：

```haskell
movingAvg :: Fractional a => Int -> Stream a -> Stream a
movingAvg n = extend (average n)
```

\noindent
結果は移動平均のストリームとなる。

ストリームは単方向1次元コモナドの例だ。これは簡単に双方向にしたり多次元に拡張したりできる。

## 圏論から見たコモナド

圏論でコモナドを定義することは双対性についての簡単な練習問題になる。モナドと同様に、自己関手$T$から始めることにする。モナドを定義する2つの自然変換$\eta$と$\mu$は、コモナドでは単に反転される：
$$
\begin{aligned}
\varepsilon &\Colon T \to I \\
\delta &\Colon T \to T^2
\end{aligned}
$$
これらの変換の成分は、`extract`および`duplicate`に対応する。コモナド則は鏡に映ったモナド則だ。驚くまでもない。

そして、モナドは随伴から導出できる。双対性は随伴を反転させる。つまり、左随伴と右随伴が入れ替わる。また、合成$R \circ L$がモナドを定義するので、$L \circ R$はコモナドを定義するに違いない。随伴の余単位：
$$\varepsilon \Colon L \circ R \to I$$
は、コモナドの定義で見たものと同じだ。すなわち、成分で見ればHaskellにおける`extract`と同じだ。随伴の単位：
$$\eta \Colon I \to R \circ L$$
も、$L \circ R$の途中に$R \circ L$を挿入して$L \circ R \circ L \circ R$を作成するのに使える。$T$［訳注：$= L \circ R$］から$T^2$を作ると$\delta$が定義され、コモナドの定義が完成する。

モナドがモノイドであることもすでに見た。この双対について述べるにはコモノイドを使う必要があるだろうが、ではコモノイドとは何だろう？　モノイドとは単一対象圏であるという当初の定義を双対化しても興味深いものは何も得られない。すべての自己射を逆向きにしても、別のモノイドが得られるだけだ。しかし、思い出してほしい。モナドへのアプローチで用いた定義は、モノイドとはモノイダル圏における対象であるという、より一般化されたものだった。その構成は2つの射に基づいていた：
$$
\begin{aligned}
\mu &\Colon m \otimes m \to m \\
\eta &\Colon i \to m
\end{aligned}
$$
これらの射を反転させるとモノイダル圏におけるコモノイドができる：
$$
\begin{aligned}
\delta &\Colon m \to m \otimes m \\
\varepsilon &\Colon m \to i
\end{aligned}
$$
コモノイドの定義をHaskellで書くと：

```haskell
class Comonoid m where
  split   :: m -> (m, m)
  destroy :: m -> ()
```

\noindent
となるが、むしろ自明なものとなってしまう。明らかに`destroy`は引数を無視する：

```haskell
destroy _ = ()
```

\noindent
また、`split`は単に関数のペアだ：

```haskell
split x = (f x, g x)
```

\noindent
ここで、モノイドの単位律と双対関係にあるコモノイド則について考えてみよう：

```haskell
lambda . bimap destroy id . split = id
rho . bimap id destroy . split = id
```

\noindent
ここで、`lambda`は左単位律子で`rho`は右単位律子だ ([モノイダル圏](#monads-categorically)の定義を参照)。定義を代入すると：

```haskell
lambda (bimap destroy id (split x))
= lambda (bimap destroy id (f x, g x))
= lambda ((), g x)
= g x
```

\noindent
となり、`g = id`が示される。同様に、2番目の規則は`f = id`へと展開される。結論として：

```haskell
split x = (x, x)
```

\noindent
となり、Haskell（および一般に$\Set$圏）では、どの対象も自明なコモナドだと示される。

幸い、モノイダル圏のなかには、コモノイドを定義することがもっと興味深いものが他にある。そのひとつは自己関手の圏だ。そこで分かることとして、モナドが自己関手からなる圏におけるモノイドであるのと同様に：

> コモナドは自己関手からなる圏におけるコモノイドである。

## Storeコモナド

コモナドのもう1つの重要な例はstateモナドの双対だ。それはcostateコモナド、あるいはstoreコモナドと呼ばれる。

以前、冪を定義する随伴によってstateモナドが生成されるのを見た：
$$
\begin{aligned}
L z &= z\times{}s \\
R a &= s \Rightarrow a
\end{aligned}
$$
同じ随伴をcostateコモナドを定義するのに使うことにしよう。コモナドは合成$L \circ R$によって定義される：
$$L (R a) = (s \Rightarrow a)\times{}s$$
これをHaskellに変換するには、`Product`左関手と`Reader`右関手との間の随伴から始める。`Product`を`Reader`の後に合成するのは次の定義と等価だ：

```haskell
data Store s a = Store (s -> a) s
```

\noindent
対象`a`におけるこの随伴の余単位は次の射だ：
$$\varepsilon_a \Colon ((s \Rightarrow a)\times{}s) \to a$$
あるいは、Haskellの記法だとこうなる：

```haskell
counit (Prod (Reader f, s)) = f s
```

\noindent
これから`extract`を得られる：

```haskell
extract (Store f s) = f s
```

\noindent
随伴の単位：

```haskell
unit :: a -> Reader s (Product a, s)
unit a = Reader (\s -> Prod (a, s))
```

\noindent
は、部分適用されたデータ構成子として書き直せる：

```haskell
Store f :: s -> Store f s
```

\noindent
$\delta$、すなわち`duplicate`を以下の水平合成として構成しよう：
$$
\begin{aligned}
\delta &\Colon L \circ R \to L \circ R \circ L \circ R \\
\delta &= L \circ \eta \circ R
\end{aligned}
$$
一番左の$L$越しに$\eta$をくすねる必要がある。この$L$は`Product`関手だ。これは、ペアの左側の要素に$\eta$すなわち`Store f`を作用させることを意味する（これが`Product`の`fmap`が行うことだ）。すると、次の結果が得られる：

```haskell
duplicate (Store f s) = Store (Store f) s
```

\noindent
（$\delta$の式での$L$と$R$は恒等自然変換を表し、成分が恒等射であることを思い出してほしい）。

`Store`コモナドの完全な定義を以下に示す：

```haskell
instance Comonad (Store s) where
  extract (Store f s) = f s
  duplicate (Store f s) = Store (Store f) s
```

\noindent
`Store`の`Reader`の部分は、一般化されたコンテナーに型`s`の要素をキーにして`a`が格納されていると見なせる。たとえば、`s`が`Int`なら、`Reader Int a`は`a`の双方向無限ストリームだ。`Store`はこのコンテナーとキー型の値とをペアにする。また、たとえば、`Reader Int a`はある`Int`とペアになっている。この場合、`extract`はその整数を使って無限ストリームにインデックスを作成する。`Store`の2番目の要素は現在位置と見なせる。

この例についてさらに述べると、`duplicate`は`Int`でインデックスされた新しい無限ストリームを作成する。このストリームは要素としてストリームを含む。特に、現在位置にはもとのストリームが含まれている。しかし、他の（正負を問わない）`Int`をキーとして使うと、その新しいインデックスの位置へシフトされたストリームが得られる。

一般に、`duplicate`された`Store`に`extract`が作用すると、もとの`Store`が生成されることを確認できる（実際、コモナドの恒等律では`extract . duplicate = id`と規定されている）。

`Store`コモナドは`lens`ライブラリーの理論的基盤として重要だ[^lens]。概念としては、`Store s a`のようにコモナドを用いることは、型`s`をインデックスとしてデータ型`a`の特定の部分構造に（レンズのように）「焦点を合わせる」という考え方をカプセル化している^[監訳注：一般には部分構造に限定されない。]。特に、ある関数の型が：

```haskell
a -> Store s a
```

\noindent
なら、以下の関数のペアと等価だ：

```haskell
set :: a -> s -> a
get :: a -> s
```

[^lens]: 監訳注：興味のある読者はRussel O'Connorの "Functor is to Lens as Applicative is to Biplate: Introducing Multiplate" (<https://doi.org/10.48550/arXiv.1103.2841>) を参照のこと。この論文はWGP 11に採録されたが諸事情により論文集には含まれていない。参考：<https://r6.ca/blog/20110930T012533Z.html>

\noindent
`a`が直積型なら、`set`は、`a`内の型`s`のフィールドをセットしつつ変更版の`a`を返すように実装できる。同様に、`get`は`s`フィールドの値を`a`から読み取るように実装できる。これらの考え方については次章で詳しく説明する。

## 課題

#. `Store`コモナドを用いてコンウェイのライフゲームを実装せよ。ヒント：`s`の型は何がよいだろうか？
