# コモナド

モナドが使えるようになったので、双対性の恩恵を受けられ、矢を逆にして反対圏で作業するだけでコモナドを無料で手に入れられる。

最も基本的なレベルでは、モナドはKleisli矢を合成することであるのを思い出してほしい:

```haskell
a -> m b
```

ここで`m`はモナドである関手だ。コモナドを文字`w` (逆さの`m`) で表すと、余Kleisli矢 (co-Kleisli arrow) を次の型の射として定義できる:

```haskell
w a -> b
```

余Kleisli矢についてfish演算子に相当するものは次のように定義される:

```haskell
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
```

余Kleisli矢が圏を形成するには`extract`と呼ばれる恒等余Kleisli矢も必要だ:

```haskell
extract :: w a -> a
```

これは`return`の双対だ。さらに、左恒等射と右恒等射だけでなく結合則も必須だ。すべてをまとめると、Haskellではコモナドを次のように定義できる:

```haskell
class Functor w => Comonad w where
    (=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
    extract :: w a -> a
```

実際には、後で説明するように、わずかに異なるプリミティブが使われる。

問題は、コモナドがプログラミングで何の役に立つかだ。

## コモナドでのプログラミング

モナドとコモナドを比べてみよう。モナドはコンテナに値を入れる手段を`return`によって提供している。そして、内部に格納された値にはアクセスできない。もちろんモナドを実装するデータ構造は内容へのアクセスを提供してもよいが、それはボーナスと見なされる。モナドから値を抽出するための一般的なインターフェースはない。例として見た`IO`モナドは内容を決して公開しないことを誇りにしている。

一方、コモナドは単一の値を取り出す手段を提供している。そして、値を挿入する手段は提供しない。コモナドをコンテナと見なしたいなら、その中身はいつも事前に満たされていて、覗き見られるようになっている。

Kleisli矢は値を受け取り、文脈で装飾した結果を生成する。同様に、余Kleisli矢は文脈全体と共に値を受け取り、結果を生成する。これは[文脈付き計算]{.keyword #contextual_computation} (contextual computation) の実施例だ。

## 積コモナド

Readerモナドを覚えているだろうか？
それを導入した理由は、何らかの読み取り専用の環境`e`へのアクセスを必要とする計算を実装する問題に取り組むためだった。そのような計算は純粋関数として次の形で表せる:

```haskell
(a, e) -> b
```

それらをKleisli矢に変えるのにはカリー化を使った:

```haskell
a -> (e -> b)
```

しかし、これらの関数がすでに余Kleisli矢の形をしていることに注目してほしい。引数をもっと便利な関手の形式に書き換えてみよう:

```haskell
data Product e a = Prod e a deriving Functor
```

合成演算子は簡単に定義できる。合成しようとしている矢でも同じ環境を使えるようにすればよい:

```haskell
(=>=) :: (Product e a -> b) -> (Product e b -> c) -> (Product e a -> c)
f =>= g = \(Prod e a) -> let b = f (Prod e a)
                          c = g (Prod e b)
                      in c
```

`extract`の実装は環境を単に無視する:

```haskell
extract (Prod e a) = a
```

当然ながら、積コモナドはreaderモナドとまったく同じ計算を実行するのに使える。ある意味で、環境をコモナドで実装するのはより自然だ――「文脈に沿った計算」の精神に則っている。一方、モナドは`do`記法という便利な構文糖を備えている。

readerモナドと積コモナドの関係はさらに深く、reader関手が積関手の右随伴であるという事実と関係がある。しかし、一般的には、コモナドはモナドとは異なる計算の概念を扱う。さらなる例について後で説明する。

積コモナド`Product`は、タプルやレコードを含む任意の直積型へ簡単に一般化できる。

## 合成の分析

双対化のプロセスを続けることで、モナディックなbindとjoinを双対化できた。別の方法として、モナドで用いたプロセスを繰り返すこともできる。そこではfish演算子の解剖学を研究した。そのアプローチの方が啓発的に思える。

出発点は、合成演算子は`w a`をとり`c`を生成する余Kleisli矢を生成しなければならない、という認識だ。`c`を生成する唯一の方法は、2番目の関数を型`w b`の引数に適用することだ:

```haskell
(=>=) :: (w a -> b) -> (w b -> c) -> (w a -> c)
f =>= g = g ...
```

だが、`g`に与えうる型`w b`の値はどうすれば生成できるだろうか？
型`w a`の引数と関数`f :: w a -> b`は自由に使える。答えはbindの双対を定義することだ。それはextendと呼ばれる:

```haskell
extend :: (w a -> b) -> w a -> w b
```

`extend`を使えば合成を実装できる:

```haskell
f =>= g = g . extend f
```

次に、`extend`は分析できるだろうか？
なぜ単に関数`w a -> b`を引数`w a`に適用しないのか、と言いたくなるかもしれない。しかし、結果として得られる`b`を`w b`に変換する方法がないことにすぐ気付くだろう。コモナドには値をリフトする手段がないことを覚えておいてほしい。ここではモナドに類似した構成で`fmap`を使った。ここで`fmap`が使えるとすれば、型`w (w a)`の何かが自由に使える場合だけだ。`w a`を`w (w a)`に変えられさえすればよい。そして、便利なことに、それはまさに`join`の双対だ。それは`duplicate`と呼ばれる:

```haskell
duplicate :: w a -> w (w a)
```

したがって、モナドと同様に、コモナドにも3つの等価な定義がある。すなわち、余Kleisli矢の使用、`extend`、`duplicate`だ。Haskellの`Control.Comonad`ライブラリから直接引用した定義を以下に示す:

```haskell
class Functor w => Comonad w where
  extract :: w a -> a
  duplicate :: w a -> w (w a)
  duplicate = extend id
  extend :: (w a -> b) -> w a -> w b
  extend f = fmap f . duplicate
```

`extend`のデフォルト実装が`duplicate`によって提供され、逆も同様だ。したがって、どちらか1つをオーバーライドするだけでよい。

これらの関数の背景にある直観は、一般にコモナドは型`a`の値で満たされたコンテナと見なせるという考えに基づいている (積コモナドは値が1つしかない特殊なケースだ)。「現在の」値という概念があり、それは`extract`によって簡単にアクセスできる。余Kleisli矢が実行する計算は現在の値に焦点を合わせるが、周囲のすべての値にアクセスできる。コンウェイのライフゲームを考えてみてほしい。各セルには値が含まれる (通常は`True`か`False`だ) 。ライフゲームに対応するコモナドは「現在の」セルに焦点を合わせたセルのグリッドだ。

では`duplicate`は何をするのだろう？
それはコモナディックなコンテナ`w a`を取ってコンテナのコンテナ`w (w a)`を生成する。つまり、それぞれのコンテナが`w a`内部の異なる`a`に焦点を合わせているという考え方だ。ライフゲームでは、グリッドのグリッドが得られる。外側のグリッドの各セルには、個別のセルに焦点を合わせた内側のグリッドが含まれている。

次に`extend`を見てみよう。これは、余Kleisli矢と、複数の`a`で満たされた`w a`コモナディックなコンテナを取る。すべての`a`に計算を適用して`b`に置き換える。結果は複数の`b`で満たされたコモナディックなコンテナだ。そのために`extend`は、焦点を1つの`a`から別の`a`にシフトし、それぞれに余Kleisli矢を適用する。ライフゲームでは、余Kleisli矢は現在のセルの新しい状態を計算する。そのために文脈 (すなわち最近傍セル) に注目する。`extend`のデフォルト実装はこの過程を示している。まず`duplicate`を呼び出してすべての可能な焦点を生成し、次にそのそれぞれに`f`を適用する。

## Streamコモナド

このプロセスではコンテナのある要素から別の要素に焦点が移される。これを最もうまく説明できるのは無限ストリームの例だ。そのようなストリームはリストにそっくりだが、空のコンストラクタがない点が異なる:

```haskell
data Stream a = Cons a (Stream a)
```

これは次の`Functor`にすぎない:

```haskell
instance Functor Stream where
    fmap f (Cons a as) = Cons (f a) (fmap f as)
```

ストリームの焦点はその最初の要素なので、`extract`の実装は次のようになる:

```haskell
extract (Cons a _) = a
```

`duplicate`は、それぞれ異なる要素に焦点を合わせた複数のストリームからなるストリームを1つ生成する。

```haskell
duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
```

最初の要素は元のストリーム、2番目の要素は元のストリームの尾部、3番目の要素はさらにその尾部、というように無限に続く。

完全なインスタンスは次のとおりだ:

```haskell
instance Comonad Stream where
    extract (Cons a _) = a
    duplicate (Cons a as) = Cons (Cons a as) (duplicate as)
```

これは非常に関数的な観点でストリームを捉えている。おそらく命令型言語ではストリームを1要素だけシフトする`advance`メソッドから始めるだろう。しかし、ここでの`duplicate`はシフトされたすべてのストリームを一気に生成する。Haskellの遅延評価がこれを可能にし、望ましいものにさえしている。当然、`Stream`を実用的なものにするために、`advance`に類似したものも実装する:

```haskell
tail :: Stream a -> Stream a
tail (Cons a as) = as
```

ただし、これは決してコモナディックなインターフェイスの一部ではない。

デジタル信号処理の経験があればすぐ分かるとおり、ストリームに対する余Kleisli矢は単なるデジタルフィルタであり、`extend`はフィルターされたストリームを生成する。

簡単な例として、移動平均フィルターを実装してみよう。ストリームの`n`個の要素を合計する関数を次に示す:

```haskell
sumS :: Num a => Int -> Stream a -> a
sumS n (Cons a as) = if n <= 0 then 0 else a + sumS (n - 1) as
```

また、ストリームの最初の`n`要素の平均を計算する関数を次に示す:

```haskell
average :: Fractional a => Int -> Stream a -> a
average n stm = (sumS n stm) / (fromIntegral n)
```

部分適用された`average n`は余Kleisli矢なので、ストリーム全体にわたって`extend`できる:

```haskell
movingAvg :: Fractional a => Int -> Stream a -> Stream a
movingAvg n = extend (average n)
```

結果は移動平均のストリームとなる。

ストリームは単方向の1次元コモナドの例だ。これは簡単に双方向にしたり多次元に拡張したりできる。

## 圏論から見たコモナド

圏論でコモナドを定義するのは双対性の簡単な練習になる。モナドと同様に、自己関手`T`から始めよう。モナドを定義する2つの自然変換$\eta$と$\mu$は、コモナドに対しては単に反転される。

$$
\begin{align*}
\varepsilon &\Colon T \to I \\
\delta &\Colon T \to T^2
\end{align*}
$$

これらの変換のコンポーネントは、`extract`および`duplicate`に対応する。コモナド則は鏡に映ったモナド則だ。これは驚くことではない。

また、モナドは随伴から導出できる。双対性は随伴を反転させる: 左随伴と右随伴が入れ替わる。また、合成$R \circ L$がモナドを定義するので、$L \circ R$はコモナドを定義すべきだ。随伴の余単位は次のようになる:

$$\varepsilon \Colon L \circ R \to I$$

この$\varepsilon$は、コモナドの定義や、Haskellの`extract`のようなコンポーネントで見られるものと同じだ。随伴の単位:

$$\eta \Colon I \to R \circ L$$

も、$L \circ R$の途中に$R \circ L$を挿入して$L \circ R \circ L \circ R$を作成するのに使える。$T$から$T^2$を作ると$\delta$が定義され、コモナドの定義が完成する。

モナドがモノイドであることもすでに見た。この宣言の双対にはコモナドが必要だが、ではコモノイドとは何だろう?モノイドとは単一対象圏であるという当初の定義を双対化しても、興味深いものは何も得られない。すべての自己準同型を逆向きにすると、別のモノイドが得られる。しかし、思い出してほしい。モナドへのアプローチで用いた定義は、モノイドとはモノイダル圏の対象であるという、より一般化されたものだ。その構成は2つの射に基づいていた:

$$
\begin{align*}
\mu &\Colon m \otimes m \to m \\
\eta &\Colon i \to m
\end{align*}
$$

これらの射を逆にするとモノイダル圏でのコモノイドができる:

$$
\begin{align*}
\delta &\Colon m \to m \otimes m \\
\varepsilon &\Colon m \to i
\end{align*}
$$

コモノイドの定義はHaskellで書ける:

```haskell
class Comonoid m where
  split   :: m -> (m, m)
  destroy :: m -> ()
```

but it is rather trivial. Obviously `destroy` ignores its argument.

```haskell
destroy _ = ()
```

`split`は単に関数のペアだ:

```haskell
split x = (f x, g x)
```

ここで、モノイドの単位元則と双対関係にあるコモノイド則について考えてみよう。

```haskell
lambda . bimap destroy id . split = id
rho . bimap id destroy . split = id
```

ここで、`lambda`は左単位子で`rho`は右単位子だ ([モノイダル圏](#monad-categorically)の定義を参照)。これらの定義を挿入すると:

```haskell
lambda (bimap destroy id (split x))
= lambda (bimap destroy id (f x, g x))
= lambda ((), g x)
= g x
```

となり、`g = id`が示される。同様に、2番目の規則は`f = id`へと展開される。結論として:

```haskell
split x = (x, x)
```

となり、Haskell (および一般に$\Set$圏) では、すべての対象が自明にコモナドであることが示される。

幸い、コモノイドを定義するには他にもっと興味深いモノイダル圏がある。1つは自己関手からなる圏だ。そこで分かることとして、モナドが自己関手からなる圏におけるモノイドであるのと同様に:

> コモナドは自己関手からなる圏におけるコモノイドである。

## Storeコモナド

コモナドのもう1つの重要な例はstateモナドの双対だ。それはcostateコモナド、あるいはstoreコモナドと呼ばれる。

以前、冪乗を定義する随伴によってstateモナドが生成されるのを見た:

$$
\begin{align*}
L\ z &= z\times{}s \\
R\ a &= s \Rightarrow a
\end{align*}
$$

同じ随伴を使って、costateコモナドを定義しよう。コモナドは合成$L \circ R$によって定義される:

$$L\ (R\ a) = (s \Rightarrow a)\times{}s$$

これをHaskellに変換するには、左辺の`Prod`関手と右辺の`Reader`関手との間の随伴から始める。`Prod`の前に`Reader`を合成するのは次の定義と等価だ:

```haskell
data Store s a = Store (s -> a) s
```

対象`a`におけるこの随伴の余単位は射だ:

$$\varepsilon_a \Colon ((s \Rightarrow a)\times{}s) \to a$$

あるいは、Haskellの記法だと次のようになる:

```haskell
counit (Prod (Reader f, s)) = f s
```

これが`extract`となる:

```haskell
extract (Store f s) = f s
```

随伴の単位:

```haskell
unit :: a -> Reader s (Product a, s)
unit a = Reader (\s -> Prod (a, s))
```

は、部分適用されたデータコンストラクタとして書き直せる:

```haskell
Store f :: s -> Store f s
```

$\delta$、すなわち`duplicate`を水平合成として構成しよう:

$$
\begin{align*}
\delta &\Colon L \circ R \to L \circ R \circ L \circ R \\
\delta &= L \circ \eta \circ R
\end{align*}
$$

$\eta$は一番左の$L$を通過する必要がある。この$L$は`Prod`関手だ。これは、ペアの左側の要素に$\eta$すなわち`Store f`を作用させることを意味する (これが`Prod`の`fmap`が行うことだ)。すると、次の結果が得られる:

```haskell
duplicate (Store f s) = Store (Store f) s
```

($\delta$の式での$L$と$R$は恒等自然変換を表し、コンポーネントが恒等射なのを思い出してほしい)。

`Store`コモナドの完全な定義を以下に示す:

```haskell
instance Comonad (Store s) where
  extract (Store f s) = f s
  duplicate (Store f s) = Store (Store f) s
```

`Store`の`Reader`の部分は、型`s`の要素をキーとする`a`の総称コンテナと見なせる。たとえば、`s`が`Int`なら、`Reader Int a`は`a`の無限双方向ストリームだ。`Store`は、このコンテナとキー型の値とをペアにする。たとえば、`Reader Int a`は`Int`とペアになっている。この場合、`extract`はこの整数を使って無限ストリームにインデックスを作成する。`Store`の2番目の要素は現在位置と見なせる。

この例について更に述べると、`duplicate`は`Int`によって添字付けされた新しい無限ストリームを作成する。このストリームは要素としてストリームを含む。特に、現在位置には元のストリームが含まれている。しかし、他の (正または負の) `Int`をキーとして使うと、新しいインデックスに位置するシフトされたストリームが得られる。

一般に、`duplicate`された`Store`に`extract`が作用すると、元の`Store`が生成されることを確信できる (実際、コモナドの同一則では`extract . duplicate = id`と規定されている)。

`Store`コモナドは`Lens`ライブラリの理論的基盤として重要だ。概念としては、`Store s a`コモナドは、型`s`をインデックスとしてdate型`a`の特定のサブ構造に (レンズのように)「焦点を合わせる」という考え方をカプセル化している。特に、ある関数が次の型を取るなら:

```haskell
a -> Store s a
```

それは以下の関数のペアと等価だ:

```haskell
set :: a -> s -> a
get :: a -> s
```

`a`が積型である場合、`set`は、`a`内の型`s`のフィールドをセットする一方で、`a`の変更されたバージョンを返すように実装できる。同様に、`get`は、`s`フィールドの値を`a`から読み取るように実装できる。これらの考え方については次章で詳しく説明する。

## 課題

1. `Store`コモナドを使ってコンウェイのライフゲームを実装せよ。ヒント: `s`の型は何がよいだろうか？

