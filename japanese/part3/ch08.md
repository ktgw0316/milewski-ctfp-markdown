# $F$-代数

モノイドについて、集合や、単一対象圏や、モノイダル圏の対象としての定式化をすでに見た。この単純な概念からさらにどれだけの果汁を搾り取れるだろうか？

やってみよう。次の関数のペアを持つ集合$m$としてのモノイドの定義を取り上げる：
$$
\begin{aligned}
\mu &\Colon m\times{}m \to m \\
\eta &\Colon 1 \to m
\end{aligned}
$$
ここで、1は$\Set$の終対象――単元集合だ。1つ目の関数は乗算（要素のペアを取って積を返す）を定義し、2つ目の関数は$m$から単位元を選択する。これらのシグネチャーを持つ2つの関数ならどれでもモノイドになるわけではない。結合律と単位律という追加の条件を課す必要がある。しかし、それはしばらく忘れて「潜在的なモノイド」について考えてみよう。関数のペアは、2つの関数集合のデカルト積の要素だ。それらの集合が冪対象として表現できるのは知っている：
$$
\begin{aligned}
\mu &\in m^{m\times{}m} \\
\eta &\in m^1
\end{aligned}
$$
これら2つの集合のデカルト積は次のとおりだ：
$$m^{m\times{}m}\times{}m^1$$
高校代数（どのデカルト閉圏でも有効）を使えば次のように書き直せる：
$$m^{m\times{}m + 1}$$
記号 $+$ は$\Set$の余積を表す。これは関数のペアを次のような単一の関数――集合の要素――に置き換えたということだ：
$$m\times{}m + 1 \to m$$
この関数集合のどの要素も潜在的なモノイドだ。

この定式化の利点は、興味深い一般化を導くことだ。たとえば、この言語を使って群 (group) を記述するにはどうすればよいだろうか？　群は、モノイドのどの要素に対しても逆元を対応させる関数を追加したものだ。その関数は型$m \to m$の関数だ。例を挙げると、整数は群を形成し、二項演算として加算、単位元として0、逆として正負反転を持つ。群を定義するには次の3つの関数から始めてもよい：
$$
\begin{aligned}
m\times{}m \to m \\
m \to m \\
1 \to m
\end{aligned}
$$
前と同様に、この3つ組を結合して1つの関数集合にできる：
$$m\times{}m + m + 1 \to m$$
1つの二項演算子（加算）、1つの単項演算子（正負反転）、1つの零項演算子（恒等射――ここでは0）から始めた。そして、それらを1つの関数に結合した。このシグネチャーを持つすべての関数は、潜在的な群を定義する。

同様のことを続けられる。たとえば、環を定義するにはもう1つの二項演算子と1つの零項演算子を追加する、などだ。毎回、左辺がn乗（0乗――終対象――を含んでよい）の和で、右辺がその集合自体である関数型が得られる。

いまや一般化に夢中になってしまうだろう。まず、集合を対象に、関数を射に置き換えられる。n項演算子はn項積からの射として定義できる。これは有限積 (finite product) をサポートする圏が必要であることを意味する。零項演算子に対しては終対象が存在する必要がある。したがって、デカルト圏が必要だ^[訳注：n項演算子を表すだけならモノイダル圏で十分だが、ここではモノイダル積が圏論的な積であることを想定しており、その場合のモノイダル圏はデカルト圏と呼ばれる。]。これらの演算子を組み合わせるには冪が必要なので、必要なのはデカルト閉圏となる。最終的に、この代数的な悪ふざけを完成させるには余積が必要になる。

あるいは、式の導出方法を忘れて、最終成果物に集中してもよい。我々の射の左辺にある積の和は、自己関手を定義している。その代わりとして任意の自己関手$F$を選ぶとどうなるだろう？　その場合は圏に制約を課す必要はない。こうして得られたものは$F$-代数と呼ばれる。

$F$-代数は、1つの自己関手$F$、1つの対象$a$と、次の1つの射からなる3つ組だ：
$$F a \to a$$
ここでの対象はしばしば台 (carrier, underlying object) と呼ばれ、プログラミングの文脈ではキャリア*型*と呼ばれる。また、射は評価射 (evaluation function, structure map) と呼ばれることが多い。関手$F$が式を形成し、それを射が評価すると考えてほしい。

Haskellによる$F$-代数の定義を示す：

```haskell
type Algebra f a = f a -> a
```

\noindent
これは代数とその評価関数を同一視している。

モノイドの例では、問題の関手は次のようになる：

```haskell
data MonF a = MEmpty | MAppend a a
```

\noindent
これはHaskellで$1 + a \times{}a$を表したものだ（[代数的データ構造](#simple-algebraic-data-types)（第6章）について思い出してほしい）。

環は次の関手を使って定義される：

```haskell
data RingF a = RZero
        | ROne
        | RAdd a a
        | RMul a a
        | RNeg a
```

\noindent
これはHaskellで$1 + 1 + a \times{}a + a \times{}a + a$を表したものだ。

環の例としては整数の集合が挙げられる。`Integer`をキャリア型に選ぶと、評価関数を次のように定義できる：

```haskell
evalZ :: Algebra RingF Integer
evalZ RZero      = 0
evalZ ROne       = 1
evalZ (RAdd m n) = m + n
evalZ (RMul m n) = m * n
evalZ (RNeg n)   = -n
```

\noindent
同じ関手`RingF`に基づく$F$-代数は他にもたくさんある。たとえば、多項式も正方行列も環をなす。

ご覧のとおり、関手の役割は、評価に代数の評価子 (evaluator) を使えるような式を生成することだ。ここまでは、非常に単純な式しか見てこなかった。しかし、より複雑な、再帰を使って定義できる式に関心があることも多い。

## 再帰

任意の式木を生成する方法の1つは、関手の定義内の変数`a`を再帰で置き換えることだ。たとえば、環における任意の式は、木状のデータ構造によって生成される：

```haskell
data Expr = RZero
          | ROne
          | RAdd Expr Expr
          | RMul Expr Expr
          | RNeg Expr
```

\noindent
もとの環の評価子は再帰版に置き換えられる：

```haskell
evalZ :: Expr -> Integer
evalZ RZero        = 0
evalZ ROne         = 1
evalZ (RAdd e1 e2) = evalZ e1 + evalZ e2
evalZ (RMul e1 e2) = evalZ e1 * evalZ e2
evalZ (RNeg e)     = -(evalZ e)
```

\noindent
これはまだあまり実用的ではない。すべての整数を1の和で表現するのが強制されているからだ。もっとも、緊急時には役に立つ。

それにしても、$F$-代数の言葉で式木を記述するにはどうすればよいだろうか？　関手の定義において、いままさに行っている置換の結果で自由型変数を再帰的に置き換えるプロセスを、何らかの形で形式化する必要がある。これを段階的に行うことを考えてみよう。まず、深さ1の木を次のように定義する：

```haskell
type RingF1 a = RingF (RingF a)
```

\noindent
`RingF`の定義の穴を、`RingF a`によって生成された深さ0の木で埋めている。深さ2の木も同様にして得られる：

```haskell
type RingF2 a = RingF (RingF (RingF a))
```

\noindent
これは次のようにも書ける：

```haskell
type RingF2 a = RingF (RingF1 a)
```

\noindent
このプロセスを繰り返すことで、シンボリックな等式を書ける：

<div class="sourceCode"><pre
class="sourceCode haskell"><code class="sourceCode haskell">type RingF<sub>n+1</sub> a = RingF (RingF<sub>n</sub> a)</code></pre></div>
\begin{Shaded}
\begin{Highlighting}[]
type RingF\textsubscript{n+1} a = RingF (RingF\textsubscript{n} a)
\end{Highlighting}
\end{Shaded}

概念的には、このプロセスを無限に繰り返すことで`Expr`が得られる。`Expr`は`a`に依存しないことに注意してほしい。旅の出発点によらず、いつも同じ場所に辿り着く。これは任意の圏の任意の自己関手に常に当てはまるわけではないが、$\Set$圏ではうまくいく。

当然、これは大雑把な議論なので、後でより厳密に説明する。

自己関手を無限回適用すると、[*不動点*]{.keyword #fixed_point}\index{不動点} (fixed point) が得られる。これは次のように定義される対象のひとつだ：
$$\mathit{Fix}\ f = f\ (\mathit{Fix}\ f)$$
この定義の背景には、$\mathit{Fix}\ f$を得るために$f$を無限回適用しているので、さらにもう1回適用しても何も変わらない、という直観がある。Haskellでは、不動点の定義は次のようになる：

```haskell
newtype Fix f = Fix (f (Fix f))
```

\noindent
おそらく、次のように、定義されている型の名前がコンストラクターの名前と別ならば、もっと読みやすくなっただろう：

```haskell
newtype Fix f = In (f (Fix f))
```

\noindent
しかし、ここでは広く使われている表記に従うことにする。構成子`Fix`（あるいは好みによっては`In`）は関数と見なせる：

```haskell
Fix :: f (Fix f) -> Fix f
```

\noindent
また、関手適用を1層だけ剥がす関数もある：

```haskell
unFix :: Fix f -> f (Fix f)
unFix (Fix x) = x
```

\noindent
2つの関数は互いに逆だ。これらの関数は後で使うことになる。

## $F$-代数の圏

この本の中で最も古いトリックについて述べよう：何か新しい対象を構築する方法を思いついたら、それらが圏をなすかどうかを常に確認すべし。驚くまでもなく、任意の自己関手$F$上の代数は圏をなす。その圏の対象は代数だ――台対象$a$と射$F a \to a$からなるペアで、どちらももとの圏$\cat{C}$からのものだ。

この描像を完全なものにするには、$F$-代数の圏における射を定義する必要がある。射は、ある代数 $(a, f)$ を別の代数 $(b, g)$ に写さなければならない。これを、台を写す射$m$――もとの圏で$a$から$b$へ向かう射――として定義する。どの射でもよいわけではなく、2つの評価子と互換性がなければならない。（構造を保存するそのような射を[*準同型*]{.keyword #homomorphism}\index{準同型}と呼ぶ。）
$F$-代数の準同型を定義する方法は次のとおりだ。まず、$m$を次の写像へと持ち上げられることに注目してほしい：
$$F m \Colon F a \to F b$$
それから$g$を辿ると$b$に行き着く。同様に、$f$を使って$F a$から$a$に移ってから$m$を辿ってもよい。2つの経路が等しくなるようにしたい：
$$g \circ F m = m \circ f$$
`\begin{figure}[H] \centering`{=latex}
[![alg](images/alg.png){width=30%}](images/alg.png)
`\end{figure}`{=latex}

\noindent
これが本当に圏であることは簡単に確認できる（ヒント：$\cat{C}$の恒等射については問題なく、また、準同型の合成は準同型だ）。

$F$-代数の圏に始対象が存在するなら、それは[*始代数*]{.keyword #initial_algebra}\index{始代数} (initial algebra) と呼ばれる。この始代数の台を$i$と呼び、その評価子を$j \Colon F i \to i$と呼ぼう。始代数の評価子である$j$は同型射だと分かる。この結果はランベック (Lambek) の定理として知られている。その証明は始対象の定義に依存している。始対象の定義より、他の$F$-代数への一意な準同型$m$が存在する。$m$が準同型なので、次の図式が可換でなければならない：

`\begin{figure}[H] \centering`{=latex}
[![alg2](images/alg2.png){width=30%}](images/alg2.png)
`\end{figure}`{=latex}

\noindent
次に、台が$F i$である代数を構成しよう。そのような代数の評価子は$F (F i)$ から$F i$への射でなければならない。このような評価子は$j$を持ち上げるだけで簡単に構成できる。
$$F j \Colon F (F i) \to F i$$
$(i, j)$ は始代数なので、そこから $(F i, F j)$ への一意な準同型$m$が必要だ。次の図式が可換でなければならない：

`\begin{figure}[H] \centering`{=latex}
[![alg3a](images/alg3a.png){width=30%}](images/alg3a.png)
`\end{figure}`{=latex}

\noindent
しかし、次のような自明な可換図式もある（どちらの経路も同じだ！）：

`\begin{figure}[H] \centering`{=latex}
[![alg3](images/alg3.png){width=30%}](images/alg3.png)
`\end{figure}`{=latex}

\noindent
これは、$j$が代数の準同型であり、$(F i, F j)$ を $(i, j)$ に写すことを示していると解釈できる。これら2つの図式をつなぐと次のようになる：

`\begin{figure}[H] \centering`{=latex}
[![alg4](images/alg4.png){width=60%}](images/alg4.png)
`\end{figure}`{=latex}

\noindent
この図式は、同様に、$j \circ m$が代数の準同型であることを示していると解釈できる。この場合については2つの代数は同じだ。さらに、$(i, j)$ は始代数なので、それ自身から自身への準同型は1つしか存在できず、それは恒等射$\id_i$に他ならない。この恒等射が代数の準同型であることは分かっている。したがって、$j \circ m = \id_i$となる。この事実と左側の図式の可換性を用いて$m \circ j=\id_{Fi}$であることを証明できる。これは$m$が$j$の逆であることを示しているため、$j$は$F i$と$i$の間の同型射だと言える：
$$F i \cong i$$
しかし、これはまさに$i$が$F$の不動点だと言っている。以上が最初の大雑把な議論の背景にある形式的証明だ。

Haskellに戻ろう。$i$は`Fix f`で、$j$はコンストラクター`Fix`、その逆は`unFix`だと理解できる。ランベックの定理における同型からは、始代数を得るには関手$f$を取って引数$a$を`Fix f`に置き換えればよいことが分かる^[監訳注：$\bot$があるHaskellでは`Fix f`が始代数の台に対応しているとは限らない。詳細は第24章6節を参照。]。また、不動点が$a$によらない理由も分かる。

## 自然数

自然数も$F$-代数として定義できる。出発点は次のような射のペアだ：
$$
\begin{aligned}
zero &\Colon 1 \to N \\
succ &\Colon N \to N
\end{aligned}
$$
1つ目は0を選択し、2つ目はすべての数をその次の数に写す。前と同じように、この2つは1つにまとめられる：
$$1 + N \to N$$
左辺は関手を定義し、Haskellでは次のように書ける：

```haskell
data NatF a = ZeroF | SuccF a
```

\noindent
この関手の不動点（この関手が生成する始代数）は、Haskellでは次のように表現できる：

```haskell
data Nat = Zero | Succ Nat
```

\noindent
自然数は、0か、ある数の次の数かのどちらかだ。これは自然数のペアノ表現として知られている。

## Catamorphism

始代数の条件をHaskellの表記で書き直してみよう。始代数を`Fix f`と呼ぶことにする。その評価子はコンストラクター`Fix`だ。始代数から他の任意の代数への一意な射`m`が同じ関手上に存在する。台が`a`であり評価子が`alg`である代数を考えてみよう。

`\begin{figure}[H] \centering`{=latex}
[![alg5](images/alg5.png){width=40%}](images/alg5.png)
`\end{figure}`{=latex}

\noindent
ところで、`m`が何なのかに注目してほしい。これは不動点の評価子であり、再帰的な式木全体の評価子だ。これを実装するための汎用的な方法を探してみよう。

ランベックの定理はコンストラクター`Fix`が同型射であることを示している。その逆は`unFix`と呼ばれる。したがって、この図式で矢印の1つを反転して次のようにできる：

`\begin{figure}[H] \centering`{=latex}
[![alg6](images/alg6.png){width=40%}](images/alg6.png)
`\end{figure}`{=latex}

\noindent
この図式の可換条件を書こう：

```haskell
m = alg . fmap m . unFix
```

\noindent
この等式は`m`の再帰的な定義として解釈できる。再帰は関手`f`を使って作成されたすべての有限木に対して停止する。そのことは、`fmap m`が関手`f`の最上層の下層で動作することに着目すれば分かる。言い換えると、それはもとの木の子に対して機能する。子は常にもとの木より1レベル浅くなる。

`Fix f`を使って構築された木に`m`を適用するとどうなるか述べよう。まず、`unFix`の作用によってコンストラクターが剥がれ、木の最上位があらわになる。次に、最上位ノードのすべての子に`m`を適用する。それにより、型`a`の結果が生成される。最後に、非再帰的評価子`alg`を適用することで、これらの結果を結合する。重要な点は、評価子`alg`が単純な非再帰関数であることだ。

これは任意の代数`alg`に対して行えるので、代数をパラメーターに取って`m`と呼ばれる関数を返す高階関数を定義するのは筋が通っている。この高階関数はcatamorphismと呼ばれる：

```haskell
cata :: Functor f => (f a -> a) -> Fix f -> a
cata alg = alg . fmap (cata alg) . unFix
```

\noindent
例を見てみよう。自然数を定義する関手を考える：

```haskell
data NatF a = ZeroF | SuccF a
```

\noindent
キャリア型として`(Int, Int)`を選択し、代数を次のように定義する：

```haskell
fib :: NatF (Int, Int) -> (Int, Int)
fib ZeroF = (1, 1)
fib (SuccF (m, n)) = (n, m + n)
```

\noindent
この代数のcatamorphismである`cata fib`でフィボナッチ数が計算されることは簡単に納得できる。

一般に、`NatF`の代数は漸化式を定義する。つまり、現在の要素の値を前の要素によって表す。そして、catamorphismは数列のn番目の要素の値を求める。

## fold

型`e`のリストは次の関手の始代数だ：

```haskell
data ListF e a = NilF | ConsF e a
```

\noindent
実際、再帰の結果（`List e`と呼ぶことにする）で変数`a`を置き換えると、次のようになる：

```haskell
data List e = Nil | Cons e (List e)
```

\noindent
リスト関手の代数は、特定のキャリア型を選択し、リストの2つのコンストラクターについてパターンマッチングを行う関数を定義する。その関数の`NilF`に対する値は空リストを評価する方法を表し、`ConsF`に対する値は現在の要素をそれ以前の累積値と組み合わせる方法を表す。

たとえば、以下はリストの長さを計算するために使える代数だ（キャリア型は`Int`だ）：

```haskell
lenAlg :: ListF e Int -> Int
lenAlg (ConsF e n) = n + 1
lenAlg NilF = 0
```

\noindent
実際、結果として得られる`cata lenAlg`というcatamorphismによってリストの長さを計算できる。評価関数は、(1) リストの要素と累積値を受け取って新しい累積値を返す関数と、(2) 開始値（ここでは0）とを組み合わせたものであることに注目してほしい。開始値の型と累積値の型はキャリア型によって与えられる。

これを従来のHaskellの定義と比較してみよう：

```haskell
length = foldr (\e n -> n + 1) 0
```

\noindent
`foldr`の2つの引数は代数の2つの成分そのものだ。

別の例を見てみよう：

```haskell
sumAlg :: ListF Double Double -> Double
sumAlg (ConsF e s) = e + s
sumAlg NilF = 0.0
```

\noindent
再び、これを次のものと比較する：

```haskell
sum = foldr (\e s -> e + s) 0.0
```

\noindent
ご覧のとおり、`foldr`はリストに対してcatamorphismを特殊化して便利にしたものにすぎない^[訳注：翻訳時点での事実上標準のHaskell処理系GHCの標準ライブラリー（base）における`foldr`の型は`foldr :: Foldable t => (a -> b -> b) -> b -> t a -> b`である。本文中ではリストに対する`foldr`について言及している。]。

## 余代数

いつものように、F-余代数という双対構成があり、射が逆向きになっている：
$$a \to F a$$
任意の関手についての余代数も、その余代数的構造を保存する準同型を伴った圏をなす。その圏の終対象 $(t, u)$ は終余代数 (terminal / final coalgebra) と呼ばれる。他のすべての代数 $(a, f)$ に対し、次の図式を可換にする一意な準同型`m`が存在する：

`\begin{figure}[H] \centering`{=latex}
[![alg7](images/alg7.png){width=40%}](images/alg7.png)
`\end{figure}`{=latex}

\noindent
終余代数は、射$u \Colon t \to F t$が同型射である（余代数に関するランベックの定理）という意味で関手の不動点である：
$$F t \cong t$$
終余代数は、プログラミングでは通常、（無限でもよい）データ構造または遷移系を生成するためのレシピとして解釈される。

始代数の評価にcatamorphismが使えるのと同様に、終余代数の余評価にはanamorphismが使える：

```haskell
ana :: Functor f => (a -> f a) -> a -> Fix f
ana coalg = Fix . fmap (ana coalg) . coalg
```

\noindent
余代数の正統な例は、その不動点が型`e`の要素の無限ストリームであるような関手に基づく。これがその関手だ：

```haskell
data StreamF e a = StreamF e a
  deriving Functor
```

\noindent
そして、これがその不動点だ：

```haskell
data Stream e = Stream e (Stream e)
```

\noindent
`StreamF e`の余代数は、型`a`のシードを取り、ある要素と次のシードからなるペアを生成する関数だ（ペアを`StreamF`という飾った名前で呼んでいる）。

無限数列を生成する余代数の簡単な例はすぐ生成できる。たとえば、2乗のリストや逆数のリストなどだ。

もっと興味深い例としては、素数のリストを生成する余代数がある。無限リストを台として使うのが秘訣だ。最初のシードはリスト`[2..]`になる。次のシードはこのリストのtailから2の倍数をすべて除いたものになる。これは奇数のリストであり、3から始まる。次の段階では、このリストのtailを取って3の倍数をすべて除く。エラトステネスの篩を作っているのに気付いただろう[^sieve]。この余代数は次の関数で実装される：

```haskell
era :: [Int] -> StreamF Int [Int]
era (p : ns) = StreamF p (filter (notdiv p) ns)
    where notdiv p n = n `mod` p /= 0
```

[^sieve]: 監訳注：計算量の観点からは関数`era`は本来のエラトステネスの篩と大きく異なる。より詳しくは、`era`は$n$以下の素数を求めるのに$\mathcal{O}(n^2 / (\log{}n)^2)$時間かかるが、エラトステネスの篩は$\mathcal{O}(n\log{}\log{}n)$時間である。

    参考：Melissa E. O'Neill: *The Genuine Sieve of Eratosthenes*. J. Funct. Program. 19(1): 95-106 (2009) <https://doi.org/10.1017/S0956796808007004>

\noindent
この余代数のanamorphismは素数のリストを生成する：

```haskell
primes = ana era [2..]
```

\noindent
ストリームは無限リストなので、Haskellのリストに変換できるはずだ。そのためには、同じ関手`StreamF`を使って代数を形成したうえでcatamorphismを実行すればよい。たとえば、次のcatamorphismはストリームをリストに変換する：

```haskell
toListC :: Fix (StreamF e) -> [e]
toListC = cata al
   where al :: StreamF e [e] -> [e]
         al (StreamF e a) = e : a
```

\noindent
ここでは、同じ不動点が同じ自己関手の始代数でもあり終余代数でもある[^toListC]。これは任意の圏で常に成り立つわけではない。一般に、自己関手には不動点が多数あり得る（不動点がないこともある）。始代数はいわゆる最小不動点で、終余代数は最大不動点だ。ただし、Haskellでは両方とも同じ式で定義されて一致する。

[^toListC]: 監訳注：これは結構微妙なところである。

    たとえば、$\bot$があるが`seq`のないHaskellを考えたときに、関手$a \times -$は始代数を持たない。では、`StreamF a`が始代数をもたないのかというとそうではなく、実は`StreamF a e`は圏論的な積$a \times e$ではないので、上記の議論はあてはまらず始代数を持つ。

    酒井政裕氏の修士論文「非正格関数に対して適用可能な融合変換」では、関手$F$が始代数を持つための条件は$fmap f \bot = \bot$であることを示している。

リストのanamorphismはunfoldと呼ばれる。有限リストを作成するには、ペアについての`Maybe`を生成するように関手を改変する：

```haskell
unfoldr :: (b -> Maybe (a, b)) -> b -> [a]
```

\noindent
`Nothing`の値でリストの生成が終了する。

余代数の面白い例として、レンズに関するものが挙げられる。レンズはゲッターとセッターのペアとして表せる：

```haskell
set :: a -> s -> a
get :: a -> s
```

\noindent
ここで、通常、`a`は型`s`のフィールドを持つ直積データ型だ^[監訳注：Haskellの`lens`の典型的用法についてはそうかもしれないが、一般のレンズについてはそのようなことはない。]。ゲッターはそのフィールドの値を取得し、セッターはそのフィールドを新しい値に置き換える。これら2つの関数は1つにまとめられる：

```haskell
a -> (s, s -> a)
```

\noindent
この関数はさらに次のように書き直せる：

```haskell
a -> Store s a
```

\noindent
ここで次のような関手を定義した：

```haskell
data Store s a = Store (s -> a) s
```

\noindent
これは積の和から構成される単純な代数的関手ではないことに注意してほしい。これには冪$a^s$が含まれる。

レンズはこの関手の余代数であり、キャリア型は`a`だ。`Store s`がコモナドでもあることは以前にも見た。振る舞いの良い (well-behaved) レンズは、コモナドの構造と適合する余代数に対応することが分かる。これについては次章で説明する。

## 課題

#. 1変数多項式の環について評価関数を実装せよ。多項式は$x$の冪の係数のリストとして表せる。たとえば、$4x^2-1$は、（0乗項から始めて）`[-1, 0, 4]`と表される。
#. 前の構成を、$x^2y-3y^3z$のような、独立変数を多数含む多項式に一般化せよ。
#. $2\times{}2$行列の環について代数を実装せよ。
#. 自然数の2乗のリストを生成するようなanamorphismを持つ余代数を定義せよ。
#. `unfoldr`を使って、最初の$n$個の素数のリストを生成せよ。
