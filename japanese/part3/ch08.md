# F-代数

モノイドについて、集合や、単一対象圏や、モノイダル圏の対象としての定式化を見てきた。この単純な概念からどれだけの果汁をさらに搾り取れるだろうか？

やってみよう。次の関数のペアを持つ集合$m$としてのモノイドの定義を取り上げる：

$$
\begin{aligned}
\mu &\Colon m\times{}m \to m \\
\eta &\Colon 1 \to m
\end{aligned}
$$

ここで、1は$\Set$の終対象――単集合だ。最初の関数は乗算（要素のペアを取って積を返す）を定義し、2番目の関数は$m$から単位元を選択する。これらのシグネチャーを持つ2つの関数ならどれでもモノイドになるわけではない。結合律と単位律という追加の条件を課す必要がある。しかし、それはしばらく忘れて「潜在的なモノイド」について考えてみよう。関数のペアは、2つの関数集合のカルテシアン積の要素だ。それらの集合が冪対象として表現できるのは知っている：

$$
\begin{aligned}
\mu &\in m^{m\times{}m} \\
\eta &\in m^1
\end{aligned}
$$

これら2つの集合のカルテシアン積は次のとおりだ：

$$m^{m\times{}m}\times{}m^1$$

高校代数（すべてのカルテシアン閉圏で機能する）を少し使えば次のように書き直せる：

$$m^{m\times{}m + 1}$$

記号 $+$ は$\Set$の余積を表す。ここで、関数のペアを次のような単一の関数――集合の要素――に置き換える：

$$m\times{}m + 1 \to m$$

この関数集合のどの要素も潜在的なモノイドだ。

この定式化の利点は、興味深い一般化を導くことだ。たとえば、この言語を使って群 (group) を記述するにはどうすればよいだろうか？
群は、すべての要素に逆を対応させる関数が追加されたモノイドだ。後者の関数の型は$m \to m$だ。例を挙げると、整数は群を形成し、二項演算として加算、単位元として0、否定として正負反転を持つ。群を定義するには次の3つの関数から始める：

$$
\begin{aligned}
m\times{}m \to m \\
m \to m \\
1 \to m
\end{aligned}
$$

前と同様に、このトリプルを結合して1つの関数集合にできる：

$$m\times{}m + m + 1 \to m$$

1つの二項演算子（加算）、1つの単項演算子（否定）、1つの零項演算子（恒等射――ここでは0）から始めた。そして、それらを1つの関数に結合した。このシグネチャーを持つすべての関数は、潜在的な群を定義する。

同様のことを続けられる。たとえば、環を定義するにはもう1つの二項演算子と1つの零項演算子を追加する、などだ。毎回、左辺が冪（0乗――終対象――を含んでよい）の和で、右辺が集合自体である関数型を得られる。

きっと一般化に夢中になってしまうだろう。まず、集合を対象に、関数を射に置き換えられる。n項演算子は、n項積からの射として定義できる。これは有限積 (finite product) をサポートする圏が必要であることを意味する。零項演算子については、終対象が存在する必要がある。したがって、カルテシアン圏が必要だ。これらの演算子を結合するには冪が必要なので、必要なのはカルテシアン閉圏だ。最終的に、この代数的な悪ふざけを完成させるには、余積が必要になる。

あるいは、式の導出方法を忘れて、最終的な積に集中してもよい。射の左側にある積の和は自己関手を定義している。代わりに任意の自己関手$F$を選択するとどうなるだろう？
その場合は圏に制約を課す必要はない。こうして得られたものはF-代数と呼ばれる。

F-代数は、1つの自己関手$F$、1つの対象$a$と、次の1つの射からなるトリプルだ：

$$F\ a \to a$$

ここでの対象はしばしば台 (carrier, underlying object) と呼ばれ、プログラミングの文脈ではキャリア*型*と呼ばれる。また、射は評価射 (evaluation function, structure map) と呼ばれることが多い。関手`F`が式を形成し、それを射が評価すると考えてほしい。

HaskellによるF-代数の定義を示す：

```haskell
type Algebra f a = f a -> a
```

代数はその評価関数で識別される。

モノイドの例では、問題の関手は次のようになる：

```haskell
data MonF a = MEmpty | MAppend a a
```

これはHaskellで$1 + a \times{}a$を表したものだ（[代数的データ構造](#simple-algebraic-data-types)を思い出してほしい）。

環は次の関手を使って定義される：

```haskell
data RingF a = RZero
        | ROne
        | RAdd a a
        | RMul a a
        | RNeg a
```

これはHaskellで$1 + 1 + a \times{}a + a \times{}a + a$を表したものだ。

環の例としては整数の集合が挙げられる。`Integer`をキャリア型に選ぶと、評価関数を次のように定義できる：

```haskell
evalZ :: Algebra RingF Integer
evalZ RZero      = 0
evalZ ROne       = 1
evalZ (RAdd m n) = m + n
evalZ (RMul m n) = m * n
evalZ (RNeg n)   = -n
```

同じ関手`RingF`に基づくF-代数は他にもたくさんある。たとえば、多項式も正方行列も環を形成する。

ご覧のとおり、関手の役割は、評価に代数の評価子を使えるような式を生成することだ。ここまでは、非常に単純な式しか見てこなかった。しかし、より複雑な、再帰を使って定義できる式に関心があることも多い。

## 再帰

任意の式木を生成する方法の1つは、関手の定義内の変数`a`を再帰で置き換えることだ。たとえば、環における任意の式は、木に似たデータ構造によって生成される：

```haskell
data Expr = RZero
          | ROne
          | RAdd Expr Expr
          | RMul Expr Expr
          | RNeg Expr
```

もとの環の評価子は再帰版に置き換えられる：

```haskell
evalZ :: Expr -> Integer
evalZ RZero        = 0
evalZ ROne         = 1
evalZ (RAdd e1 e2) = evalZ e1 + evalZ e2
evalZ (RMul e1 e2) = evalZ e1 * evalZ e2
evalZ (RNeg e)     = -(evalZ e)
```

これはまだあまり実用的ではない。すべての整数を1の和で表現するのが強制されているからだ。もっとも、緊急用には役立つ。

それにしても、F-代数の言葉で式木を記述するにはどうすればよいだろうか？
関手の定義において、自由型変数を再帰的に置換の結果で置き換えるプロセスを、何らかの形で形式化する必要がある。これを段階的に行うことを想像してほしい。まず、深さ1の木を次のように定義する：

```haskell
type RingF1 a = RingF (RingF a)
```

`RingF`の定義の穴を、`RingF a`によって生成された深さ0の木で埋めている。深さ2の木も同様にして得られる：

```haskell
type RingF2 a = RingF (RingF (RingF a))
```

これは次のようにも書ける：

```haskell
type RingF2 a = RingF (RingF1 a)
```

このプロセスを繰り返すことで、シンボリックな方程式を書ける：

<div class="sourceCode"><pre
class="sourceCode haskell"><code class="sourceCode haskell">type RingF<sub>n+1</sub> a = RingF (RingF<sub>n</sub> a)</code></pre></div>

概念としては、このプロセスを無限に何度も繰り返すことで`Expr`が得られる。`Expr`は`a`に依存しないことに注意してほしい。旅の出発点によらず、いつも同じ場所に辿り着く。これは任意の圏の任意の自己関手に常に当てはまるわけではないが、$\Set$圏では適切だ。

当然、これは身振り手振りでの議論なので、後でより厳密に説明する。

自己関手を無限回適用すると、[不動点]{.keyword #fixed_point} (fixed point) という対象が生成される。この対象は次のように定義される：

$$\mathit{Fix}\ f = f\ (\mathit{Fix}\ f)$$

この定義の背景には、$\mathit{Fix}\ f$を得るために$f$を無限回適用しているので、さらにもう1回適用しても何も変わらない、という直観がある。Haskellでは、不動点の定義は次のようになる：

```haskell
newtype Fix f = Fix (f (Fix f))
```

おそらく、次のように、定義されている型の名前がコンストラクターの名前と別ならば、もっと読みやすくなる：

```haskell
newtype Fix f = In (f (Fix f))
```

しかし、ここでは広く使われている表記に従うことにする。コンストラクター`Fix`（あるいは好みによっては`In`）は関数と見なせる：

```haskell
Fix :: f (Fix f) -> Fix f
```

また、関手適用を1層だけ剥がす関数もある：

```haskell
unFix :: Fix f -> f (Fix f)
unFix (Fix x) = x
```

2つの関数は互いに逆だ。これらの関数は後で使うことになる。

## F-代数の圏

この本の中で最も古いトリックについて述べよう：何か新しい対象を構築する方法を思いついたら、それらが圏を形成するかどうかを常に確認すべし。驚くまでもなく、任意の自己関手$F$における代数は圏を形成する。その圏の対象は代数だ――台対象$a$と射$F\ a \to a$からなるペアで、どちらももとの圏$\cat{C}$からのものだ。

この描像を完全なものにするには、F-代数の圏における射を定義する必要がある。射は、ある代数 $(a, f)$ を別の代数 $(b, g)$ に写さなければならない。これを、台を写す射$m$――もとの圏で$a$から$b$へ向かう射――と定義する。どの射でもいいわけではなく、2つの評価子と互換性がなければならない。（構造を保存するそのような射を[準同型]{.keyword #homomorphism}と呼ぶ。）
F-代数の準同型を定義する方法は次のとおりだ。まず、$m$を次の写像にリフトできることに注目してほしい：

$$F\ m \Colon F\ a \to F\ b$$

それから$g$を辿ると$b$に行き着く。同様に、$f$を使って$F\ a$から$a$に移ってから$m$を辿ってもよい。2つの経路が等しくなるようにしたい：

$$g \circ F\ m = m \circ f$$

[![alg](images/alg.png)](images/alg.png){width=30%}

これが本当に圏であることは簡単に納得できる（ヒント：$\cat{C}$の恒等射については問題なく、また、準同型の合成は準同型だ）。

F-代数の圏に始対象が存在するなら、それは[始代数]{.keyword #initial_algebra} (initial algebra) と呼ばれる。この始代数の台を$i$と呼び、その評価子を$j \Colon F\ i \to i$と呼ぼう。始代数の評価子である$j$は同型射だと分かる。この結果はLambekの定理として知られている。その証明は始対象の定義に依存している。定義では、始対象は他のF-代数への一意な準同型$m$の存在を必要とする。$m$が準同型なので、次の図式は可換でなければならない：

[![alg2](images/alg2.png)](images/alg2.png){width=30%}

次に、台が$F\ i$である代数を構築しよう。そのような代数の評価子は$F\ (F i)$ から$F\ i$への射でなければならない。このような評価子は$j$をリフトするだけで簡単に構成できる。

$$F\ j \Colon F\ (F\ i) \to F\ i$$

$(i, j)$ は始代数なので、そこから $(F i, F j)$ への一意な準同型$m$が必要だ。次の図式は可換でなければならない：

[![alg3a](images/alg3a.png)](images/alg3a.png){width=30%}

しかし、次のような自明な可換図式もある（どちらの経路も同じだ！）：

[![alg3](images/alg3.png)](images/alg3.png){width=30%}

これは、$j$が代数の準同型であり、$(F\ i, F\ j)$ を $(i, j)$ に写すことを示していると解釈できる。これら2つの図式を繋ぐと次のようになる：

[![alg4](images/alg4.png)](images/alg4.png){width=60%}

この図式は、同様に、$j \circ m$が代数の準同型であることを示していると解釈できる。この場合のみ2つの代数は同じだ。さらに、$(i, j)$ は初期代数なので、それ自身から自身への準同型は1つしか存在できず、それはどの代数でも準同型だと分かっている恒等射$id_i$だ。したがって、$j \circ m = id_i$となる。この事実と左側の図式の可換特性を用いて$m\circ j=id_{Fi}$であることを証明できる。これは$m$が$j$の逆であることを示しているため、$j$は$F\ i$と$i$の間の同型射だ。

$$F\ i \cong i$$

しかし、これは$i$が$F$の不動点だと言っているにすぎない。以上が最初の身振り手振りでの議論の背景にある形式的証明だ。

Haskellに戻ろう：$i$は`Fix f`で、$j$はコンストラクター`Fix`、その逆は`unFix`だと理解している。Lambekの定理における同型からは、始代数を得るには関手$f$を取って引数$a$を`Fix f`に置き換えればよいことが分かる。また、不動点が$a$によらない理由も分かる。

## 自然数

自然数もF-代数として定義できる。出発点は次のような射のペアだ：

$$
\begin{aligned}
zero &\Colon 1 \to N \\
succ &\Colon N \to N
\end{aligned}
$$

1番目は0を選択し、2番目はすべての数をその次の数に写す。前と同じように、この2つは1つにまとめられる：

$$1 + N \to N$$

左辺は関手を定義し、Haskellでは次のように書ける：

```haskell
data NatF a = ZeroF | SuccF a
```

この関手の不動点（生成される始代数）は、Haskellでは次のようにコード化できる：

```haskell
data Nat = Zero | Succ Nat
```

自然数は、0か、ある数の次の数かのどちらかだ。これは自然数のPeano表現として知られている。

## カタモルフィズム

Haskellの表記で初期条件を書き直してみよう。始代数は`Fix f`と呼ばれる。その評価子はコンストラクター`Fix`だ。始代数から他の任意の代数への一意な射`m`が同じ関手上に存在する。台が`a`であり評価子が`alg`である代数を選択しよう。

[![alg5](images/alg5.png)](images/alg5.png){width=40%}

ところで、`m`が何なのかに注目してほしい：これは不動点の評価子であり、再帰的な式木全体の評価子だ。これを実装するための汎用的な方法を探そう。

Lambekの定理はコンストラクター`Fix`が同型射であることを示している。その逆は`unFix`と呼ばれる。したがって、この図式で矢印の1つを反転して次のようにできる：

[![alg6](images/alg6.png)](images/alg6.png){width=40%}

この図式の可換条件を書こう：

```haskell
m = alg . fmap m . unFix
```

この方程式は`m`の再帰的な定義として解釈できる。再帰は関手`f`を使って作成されたすべての有限木に対して停止する。そのことは、`fmap m`が関手`f`の最上層の下で動作することに着目すれば分かる。言い換えると、それはもとの木の子に対して機能する。子は常にもとの木より1レベル浅くなる。

`Fix f`を使って構築された木に`m`を適用するとどうなるか述べよう。まず、`unFix`の作用によってコンストラクターが剥がれ、木の最上位があらわになる。次に、最上位ノードのすべての子に`m`を適用する。それにより、型`a`の結果が生成される。最後に、非再帰的評価子`alg`を適用することで、これらの結果を結合する。重要な点は、評価子`alg`が単純な非再帰関数であることだ。

これは任意の代数`alg`に対して行えるので、代数をパラメーターに取って`m`と呼ばれる関数を返す高階関数を定義することは意味がある。この高階関数はカタモルフィズム (catamorphism) と呼ばれる。

```haskell
cata :: Functor f => (f a -> a) -> Fix f -> a
cata alg = alg . fmap (cata alg) . unFix
```

例を見てみよう。自然数を定義する関手を考える：

```haskell
data NatF a = ZeroF | SuccF a
```

台の型として`(Int, Int)`を選択し、代数を次のように定義する：

```haskell
fib :: NatF (Int, Int) -> (Int, Int)
fib ZeroF = (1, 1)
fib (SuccF (m, n)) = (n, m + n)
```

この代数のカタモルフィズム`cata fib`でフィボナッチ数が計算されることは容易に納得できる。

一般に、`NatF`の代数は漸化式を定義する。つまり、現在の要素の値を前の要素によって表す。そして、カタモルフィズムは一連の要素のうちn番目のものを評価する。

## 畳み込み

`e`のリストは次の関手の始代数だ：

```haskell
data ListF e a = NilF | ConsF e a
```

実際、`List e`と呼ばれる再帰の結果で変数`a`を置き換えると、次のようになる：

```haskell
data List e = Nil | Cons e (List e)
```

list関手の代数は、台の特定の型を選択し、2つのコンストラクターについてパターンマッチングを行う関数を定義する。`NilF`に対するその値は、空リストを評価する方法を表す。また、`ConsF`に対する値は、現在の要素をそれ以前の累積値と組み合わせる方法を表す。

例として、リストの長さを計算するために使える代数を次に示す（台の型は`Int`だ）：

```haskell
lenAlg :: ListF e Int -> Int
lenAlg (ConsF e n) = n + 1
lenAlg NilF = 0
```

実際、結果として得られるカタモルフィズム`cata lenAlg`によってリストの長さが計算される。評価関数は、(1) リスト要素とアキュムレータを受け取って新しいアキュムレータを返す関数と、(2) 開始値（ここでは0）とを組み合わせたものであることに注目してほしい。値の型とアキュムレータの型は台の型によって与えられる。

これを従来のHaskellの定義と比較してみよう：

```haskell
length = foldr (\e n -> n + 1) 0
```

`foldr`の2つの引数は代数の2つの成分そのものだ。

別の例を見てみよう：

```haskell
sumAlg :: ListF Double Double -> Double
sumAlg (ConsF e s) = e + s
sumAlg NilF = 0.0
```

再び、これを次のものと比較する：

```haskell
sum = foldr (\e s -> e + s) 0.0
```

ご覧のとおり、`foldr`はリストに対してカタモルフィズムを特殊化して便利にしたものにすぎない。

## 余代数

いつものように、F-余代数という双対構成があり、射が逆向きになっている：

$$a \to F\ a$$

任意の関手についての余代数も、その余代数的構造を保存する準同型を伴った圏を形成する。その圏の終対象 $(t, u)$ は終余代数 (terminal / final coalgebra) と呼ばれる。他のすべての代数 $(a, f)$ では、次の図式を可換にする一意な準同型`m`が存在する：

[![alg7](images/alg7.png)](images/alg7.png){width=40%}

終余代数はその関手の不動点であり、射$u \Colon t \to F\ t$が同型射である（余代数に関するLambekの定理）ことを意味する：

$$F\ t \cong t$$

終余代数は通常、プログラミングでは、（無限であってもよい）データ構造または遷移系を生成するためのレシピとして解釈される。

始代数の評価にカタモルフィズムが使えるのと同様に、終余代数の余評価にはアナモルフィズム (anamorphism) が使える：

```haskell
ana :: Functor f => (a -> f a) -> a -> Fix f
ana coalg = Fix . fmap (ana coalg) . coalg
```

余代数の正準的な例は、不動点が型`e`の要素の無限ストリームであるような関手に基づいている。これがその関手だ：

```haskell
data StreamF e a = StreamF e a
  deriving Functor
```

そして、これがその不動点だ：

```haskell
data Stream e = Stream e (Stream e)
```

`StreamF e`の余代数は、型`a`のシードを取り、ある要素と次のシードからなるペアを生成する関数だ（ペアを`StreamF`というファンシーな名前で呼んでいる）。

無限数列を生成する余代数の簡単な例はすぐ生成できる。たとえば、自乗や逆数などのリストだ。

もっと興味深い例としては、素数のリストを生成する余代数がある。無限リストを台として使うのが秘訣だ。最初のシードはリスト`[2..]`になる。次のシードはこのリストの尾部から2の倍数をすべて除いたものになる。これは奇数のリストであり、3から始まる。次の段階では、このリストの尾部を取って3の倍数をすべて除く。エラトステネスの篩を作っているのに気付いただろう。この余代数は次の関数で実装される：

```haskell
era :: [Int] -> StreamF Int [Int]
era (p : ns) = StreamF p (filter (notdiv p) ns)
    where notdiv p n = n `mod` p /= 0
```

この余代数のアナモルフィズムは素数のリストを生成する：

```haskell
primes = ana era [2..]
```

ストリームは無限リストなので、Haskellのリストに変換できるはずだ。そのためには、同じ関手`StreamF`を使って代数を形成したうえでカタモルフィズムを実行すればよい。たとえば、次のカタモルフィズムはストリームをリストに変換する：

```haskell
toListC :: Fix (StreamF e) -> [e]
toListC = cata al
   where al :: StreamF e [e] -> [e]
         al (StreamF e a) = e : a
```

ここでは、同じ不動点が同じ自己関手の始代数でもあり終余代数でもある。これは任意の圏でいつも成り立つわけでない。一般に、自己関手には不動点が多数ありうる（不動点がないこともある）。始代数はいわゆる最小不動点で、終余代数は最大不動点だ。ただし、Haskellでは両方とも同じ式で定義され、一致する。

リストのアナモルフィズムはunfoldと呼ばれる。有限リストを作成するには、関手を改変して`Maybe`のペアを作成する：

```haskell
unfoldr :: (b -> Maybe (a, b)) -> b -> [a]
```

`Nothing`の値でリストの生成が終了する。

余代数の面白い例として、レンズに関するものが挙げられる。レンズはゲッターとセッターのペアとして表せる：

```haskell
set :: a -> s -> a
get :: a -> s
```

ここで、通常、`a`は型`s`のフィールドを持つ直積データ型だ。ゲッターはそのフィールドの値を取得し、セッターはそのフィールドを新しい値に置き換える。これら2つの関数は1つにまとめられる：

```haskell
a -> (s, s -> a)
```

この関数はさらに次のように書き直せる：

```haskell
a -> Store s a
```

ここで次のような関手を定義した：

```haskell
data Store s a = Store (s -> a) s
```

これは積の和から構成される単純な代数的関手ではないことに注意してほしい。これには冪$a^s$が含まれる。

レンズはこの関手の余代数であり、台の型は`a`だ。`Store s`がコモナドでもあることは以前にも見た。行儀の良い (well-behaved) レンズは、コモナドの構造と適合する余代数に対応することが分かる。これについては次章で説明する。

## 課題

1. 1変数多項式の環について評価関数を実装せよ。多項式は$x$の冪の係数のリストとして表せる。たとえば、$4x^2-1$は、（0乗項から始めて）`[-1, 0, 4]`と表される。
2. 前の構成を、$x^2y-3y^3z$のような、独立変数を多数含む多項式に一般化せよ。
3. $2\times{}2$行列の環について代数を実装せよ。
4. 自然数の自乗のリストを生成するようなアナモルフィズムを持つ余代数を定義せよ。
5. `unfoldr`を使って、最初の$n$個の素数のリストを生成せよ。
