# 関数型 {#function-types}

ここまでは、関数型の意味について言い繕ってきた。関数型は他の型とは異なる。

たとえば`Integer`は単なる整数の集合だ。`Bool`は2要素の集合だ。しかし、関数型$a \to b$はそれ以上のもので、対象$a$と$b$の間の射の集合だ。任意の圏における2つの対象間の射の集合はhom集合と呼ばれる。たまたま$\Set$圏では、すべてのhom集合はそれ自体が同じ圏の対象だ。結局それは*集合*だからだ。

![Set圏のなかのhom集合は単なる集合だ。](images/set-hom-set.jpg){width=35%}

同じことは、hom集合が圏の外にあるような他の圏には当てはまらない。それらは*外部* (external) hom集合と呼ばれることもある。

![この圏Cのhom集合は外部集合だ。](images/hom-set.jpg){width=35%}

$\Set$圏の自己参照的な性質によって、関数型は特殊なものになっている。しかし、少なくともいくつかの圏では、hom集合を表す対象を構築する方法がある。このような対象は[内部]{.keyword #internal} (internal) hom集合と呼ばれる。

## 普遍的構成

関数型が集合であることを一旦忘れて、ゼロから関数型を、より一般的には内部hom集合を構築してみよう。いつものように、ここでは$\Set$圏からヒントを得る。ただし、集合の性質に一切頼らないように気を付けて、他の圏でも構成が自動的に使えるようにする。

関数型は、引数の型と結果の型の関係から、複合型と見なせる。対象間の関係を含む複合型の構築についてはすでに説明した。[積と余積の型](#products-and-coproducts)を定義するのに普遍的構成を使った。同じトリックを使って関数型を定義できる。作成する関数型、引数の型、結果の型という3つの対象を含むパターンが必要になる。

これら3つの型を結びつける自明なパターンは[関数適用]{.keyword #function_application} (function application) あるいは[評価]{.keyword #evaluation} (evaluation) と呼ばれる。ある関数型の候補を$z$と呼ぼう（$\Set$圏でない場合、これは他の対象と同様の単なる対象であることに注意）。また、引数の型を$a$（対象）と呼ぼう。関数適用はこれらのペアを結果の型$b$（対象）に写す。対象が3つあり、そのうち（引数の型と結果の型を表す）2つが決まった。

関数適用は写像だ。どうすればその写像をパターンに組み込めるだろう？
対象の内部を見ることが許されていたなら、関数$f$（$z$の要素）と引数$x$（$a$の要素）をペアにして$f x$（$f$を$x$へ適用したものであり、$b$の要素）に写せただろう。

![Setでは、関数の集合zから関数fを選び、集合（型）aから引数xを選べる。その結果、集合（型）bの中の要素f xが得られる。](images/functionset.jpg){width=35%}

個々のペア $(f, x)$ を扱う代わりに、関数型$z$と引数の型$a$の*積*全体について述べることもできる。積$z\times{}a$は対象であり、関数適用の射として、そこから$b$への矢$g$を選択できる。$\Set$では、$g$はすべてのペア $(f, x)$ を$f x$に写す関数となるだろう。

したがって、2つの対象$z$と$a$の積が別の対象$b$に射$g$で接続されている、というパターンになる。

![普遍的構成の出発点である対象と射のパターン](images/functionpattern.jpg){width=40%}

このパターンは、普遍的構成を使って関数型を特定するのに十分特化しているだろうか？
すべての圏でそうだとは言えない。しかし、我々が関心を持っている圏ではそうだ。さらに別の疑問もある：積を先に定義することなく関数対象を定義できるだろうか？
積が全く存在しない圏や、対象のすべてのペアに対しては存在しない圏もある。したがって、答えはノーだ。直積型がなければ関数型はない。これについては、後で冪乗について述べるときに再び説明する。

普遍的構成をおさらいしよう。まず対象と射のパターンから始める。これは粗い検索であり、通常はヒットするものが多すぎる。特に、$\Set$では、ほとんどすべてのものがすべてに接続されている。任意の対象$z$を選んで$a$との積を作れば、そこから$b$への関数を作れる（$b$が空集合の場合を除く）。

その時に秘密兵器を使う。順位付けだ。これは通常、候補となる対象の間に一意な写像（構成を分解する写像）が存在することを条件として行われる。ここでは、$z \times a$から$b$への射$g$を伴う$z$は、それ自身の関数適用$g'$を伴う別の$z'$よりも*優れて*いるとして選別するが、$g'$の適用が$g$の適用を通じて分解するような、$z'$から$z$への一意な写像$h$が存在する場合に限る（ヒント：この文は図を見ながら読むこと）。

![関数対象の候補間の順位付けの確立](images/functionranking.jpg){width=40%}

ここで厄介な点がある。それが、この特定の普遍的構成の説明を今まで延期した主な理由だ。射$h \Colon z'\to z$が存在するとき、$z'$と$z$をそれぞれ$a$と掛けた図式を閉じたい。$z'$から$z$への写像$h$が存在するとき、本当に必要なのは、$z'\times a$から$z\times a$への写像だ。そして今、[積の関手性](#functoriality)についてすでに議論したので、そのやり方は分かっている。積自体が関手（正確には自己双関手）なので射のペアをリフトできる。すなわち、対象の積だけでなく、射の積も定義できる。

積$z' \times a$の2番目の要素には触れていないので、射のペア $(h, \id)$ をリフトする。ここで、$\id$は$a$の恒等射だ。

そして、ある関数適用$g$で別の関数適用$g'$を分解するとこうなる：

$$g' = g \circ (h \times \id)$$

ここで鍵となるのは、射に対する積の作用だ。

普遍的構成の第3部は、普遍的に最も優れた対象を選ぶことだ。その対象を$a \Rightarrow b$と呼ぶことにしよう（Haskellの対象制約と混同しないための、1つの型クラスに対するシンボル名と考えてほしい。後で別の命名方法について議論する）。この対象は独自の関数適用を伴う。それは $(a \Rightarrow b) \times a$から$b$への射であり、$\mathit{eval}$と呼ばれる。対象$a \Rightarrow b$は、関数対象の他の候補すべてを、その関数適用の射$g$が$\mathit{eval}$を通じて分解するようなかたちで一意に写せる場合、最も優れていると言える。我々の順位付けでは、この対象は他のどの対象よりも優れている。

![普遍的な関数対象の定義。これは上記と同じ図式だが、対象$a \Rightarrow b$は*普遍*だ。](images/universalfunctionobject.jpg){width=40%}

形式的には：

> $a$から$b$への*関数対象*は、対象$a \Rightarrow b$に射
> $$\mathit{eval} \Colon ((a \Rightarrow b) \times a) \to b$$
> を伴ったものであり、他の任意の対象$z$に射
> $$g\Colon z \times a \to b$$

> を伴ったものに対して、一意な射
> $$h \Colon z \to (a \Rightarrow b)$$
> が存在して$g$を$\mathit{eval}$を通じて分解する:
> $$g=\mathit{eval} \circ (h \times \id)$$

当然、このような対象$a \Rightarrow b$が、ある圏内の任意の対象$a$と$b$のペアについて存在する保証はない。しかし、$\Set$では常に存在する。さらに、$\Set$では、この対象はhom集合$\Set(a, b)$ と同型だ。

そのため、Haskellでは、関数の型`a -> b`を圏論的関数対象$a \Rightarrow b$として解釈する。

## カリー化

関数対象の全候補を見てみよう。ただし今回は、射の$g$を2つの変数$z$と$a$の関数として考えてみよう。

$$g \Colon z \times a \to b$$

積からの射であることと2変数関数であることは同じくらい近い。特に、$\Set$では$g$は値のペアを取る関数であり、そのペアの片方は集合$z$から、もう片方は集合$a$からの値だ。

一方、普遍性 (universal property) は、このような$g$ごとに、$z$を関数対象$a \Rightarrow b$に写す一意な射$h$が存在することを示している：

$$h \Colon z \to (a \Rightarrow b)$$

$\Set$において、これは単に関数$h$が型$z$の変数を1つ受け取って$a$から$b$への関数を返すことを意味する。これによって$h$は高階関数になる。したがって、普遍的構成は、2変数関数と、関数を返す1変数関数との間に1対1の対応を確立する。この対応は[カリー化]{.keyword #currying} (currying) と呼ばれ、$h$は$g$をカリー化した (curried) バージョンと呼ばれる。

これは1対1の対応だ。任意の$g$に対し一意な$h$が存在し、任意の$h$に対して次の式を使って引数2つの関数$g$を常に再生成できるからだ：

$$g = \mathit{eval} \circ (h \times \id)$$

関数$g$は$h$の*非カリー化*バージョンと呼べる。

カリー化は本質的にHaskellの構文に組み込まれている。関数を返す関数：

```haskell
a -> (b -> c)
```

は2変数関数と見なされることが多い。括弧を外したシグネチャーはそのように読まれる：

```haskell
a -> b -> c
```

この解釈は、複数の引数を取る関数を定義する方法ではっきり分かる。例として：

```haskell
catstr :: String -> String -> String
catstr s s' = s ++ s'
```

と同じ関数を、関数を返す1引数関数、すなわちラムダとして記述できる：

```haskell
catstr' s = \s' -> s ++ s'
```

これら2つの定義は等価であり、どちらも1つの引数だけに部分適用でき、次のような1引数関数を生成する：

```haskell
greet :: String -> String
greet = catstr “Hello “
```

厳密に言えば、2変数関数はペア（直積型）を取る関数だ。

```haskell
(a, b) -> c
```

2つの表現の間の変換は自明であり、それを行う2つの（高階）関数は、もちろん、`curry`と`uncurry`と呼ばれる。

```haskell
curry :: ((a, b)->c) -> (a->b->c)
curry f a b = f (a, b)
```

および

```haskell
uncurry :: (a->b->c) -> ((a, b)->c)
uncurry f (a, b) = f a b
```

`curry`は、関数対象の普遍的構成の*factorizer*であることに注目してほしい。これは、次の形式で書き直した場合に特に顕著だ：

```haskell
factorizer :: ((a, b)->c) -> (a->(b->c))
factorizer g = \a -> (\b -> g (a, b))
```

（備忘録：factorizerは候補から分解関数を生成する。）


C++のような非関数型言語でもカリー化は可能だが、簡単ではない。C++の複数引数関数は、Haskellでのタプルを取る関数に対応すると見なせる（ただし、さらに混乱を招くことに、C++では明示的な`std::tuple`を取る関数、可変長引数関数、初期化子リストを取る関数も定義できる）。

標準テンプレートライブラリの`std::bind`を使えばC++関数を部分適用できる。たとえば、文字列2つを取る関数があるとする：

```cpp
std::string catstr(std::string s1, std::string s2) {
    return s1 + s2;
}
```

文字列1つを取る関数は次のように定義できる：

```cpp
using namespace std::placeholders;

auto greet = std::bind(catstr, "Hello ", _1);
std::cout << greet("Haskell Curry");
```

ScalaはC++やJavaよりも関数型寄りの、中間的立場に立っている。定義したい関数が部分適用されると予想されるときは、複数引数のリストを使って定義する：

```scala
def catstr(s1: String)(s2: String) = s1 + s2
```

当然、これにはある程度の先見の明や予測がライブラリの作者に求められる。

## 冪乗

数学の文献では、関数対象、すなわち2つの対象$a$と$b$の間の内部hom対象を[冪乗]{.keyword #exponential} (exponential) と呼んで$b^{a}$と記すことが多い。引数の型が指数に含まれていることに注目してほしい。この記法は一見奇妙に思えるかもしれないが、関数と積の関係を考えると完全に理にかなっている。内部hom対象の普遍的構成で積を使わなければならないことはすでに見たが、つながりはそれよりも深い。

これは、`Bool`、`Char`、あるいは`Int`や`Double`など、有限個の値を持つ型の間の関数を考えるときに最もよく見られる。そのような関数は、少なくとも原理的には、完全にメモ化したり、データ構造に変換してルックアップしたりできる。そしてこれが、射である関数と、対象である関数型との同値性の本質だ。

たとえば、`Bool`を取る（純粋）関数は、`False`に対応する値と`True`に対応する値のペアによって完全に決まる。`Bool`から`Int`へのすべての可能な関数の集合は、`Int`のすべての組み合わせの集合だ。これは積`Int` × `Int`と同じか、記法を少し創意工夫するなら、`Int`<sup>2</sup>と同じだ。

別の例として、256種類の値を含むC++の型`char`を見てみよう（Haskellの`Char`はユニコードを使っているのでもっと多い）。C++標準ライブラリの一部には、実装に通常はルックアップが使われる関数がいくつかある。`isupper`や`isspace`のような関数はテーブルを使って実装される。テーブルは256個のブール値のタプルと等価だ。タプルは直積型であるため、256個のブーリアンの積`bool × bool × bool × ... × bool`を扱っていることになる。積を繰り返したものが冪乗なのは知ってのとおりだ。`bool`を256（または`char`）回「掛ける」と、`bool`の`char`乗、つまり
`bool`<sup>char</sup>になる。

`bool`の256要素のタプルとして定義される型は何通りの値を取りうるだろう？
ちょうど$2^{256}$通りだ。これはまた、`char`から`bool`への関数の種類の数でもあり、各関数は一意な256要素のタプルに対応する。同様に、`bool`から`char`への関数の数は$256^{2}$と計算できる。以下同様だ。このような場合には、関数型の冪乗記法が完全に理にかなっている。

`int`や`double`を取る関数を完全にメモ化したいとは思わないだろう。しかし、関数とデータ型の間には、常に実用的だとは限らないにしても、同値性がある。型はリスト・文字列・木など無数に存在する。これらの型を取る関数をせっせとメモ化したら、無限のストレージが必要になるだろう。しかし、Haskellは遅延評価言語であるため、遅延評価された（無限の）データ構造と関数の境界は曖昧だ。この関数とデータの双対性は、Haskellの関数型と圏論の冪乗対象との区別を説明している。冪乗対象の方が*データ*という概念によく対応している。

## カルテシアン閉圏

型や関数のモデルとしては集合の圏を今後も使うが、圏にはより大きな族があって、同じ目的のモデルとして使えることは言及する価値がある。それらの圏は[カルテシアン閉]{.keyword #Cartesian_closed} (Cartesian closed) と呼ばれ、$\Set$はそのような圏の一例だ。

カルテシアン閉圏は、次のものを含まなければならない：

1. 終対象
2. 任意の対象のペアの直積
3. 対象の任意のペアの冪乗

冪乗を積の反復（無限に繰り返される可能性がある）と見なすなら、カルテシアン閉圏は、任意のアリティの1つのサポートプロダクトと見なせる。特に、終対象は0個の対象の積、つまり対象の0乗と見なせる。

コンピューター科学の観点から見てカルテシアン閉圏で興味深いのは、単純に型付けされたラムダ計算のモデルを提供しており、すべての型付けされたプログラミング言語の基礎を形成していることだ。

終対象と積には、始対象と余積という双対がある。それら2つも含むカルテシアン閉圏で、余積に対し積を分配できるもの

```haskell
a × (b + c) = a × b + a × c
(b + c) × a = b × a + c × a
```

は[双カルテシアン閉]{.keyword #bicartesian_closed} (bicartesian closed) 圏と呼ばれる。次の節では、$\Set$に代表される双カルテシアン閉圏の興味深い特性について説明する。

## 冪乗と代数的データ型

関数型を冪乗として解釈すると、代数的データ型のスキームに非常によく適合する。高校で習う代数における数0と1、和、積、冪乗に関係する基本的な恒等式はどれも、双カルテシアン閉圏においてそれぞれ始対象と終対象、余積、積、冪乗を考えれば、ほとんど変わらないことがわかった。それらを証明する手段（随伴や米田の補題など）はまだ得ていないが、価値ある直観の源としてここに挙げておこう。

### 0乗

$$a^{0} = 1$$

圏論の解釈では、0を始対象に、1を終対象に、等価性を同型に置き換える。冪乗は内部hom対象だ。ここに示した冪乗は、始対象から任意の対象$a$への射の集合を表している。始対象の定義によれば、そのような射は1つだけ存在するので、hom集合$\cat{C}(0, a)$ は単集合となる。単集合は$\Set$内の終対象なので、この恒等式は$\Set$内で自明に成り立つ。つまり、どんな双カルテシアン閉圏でも成り立つということだ。

Haskellでは、0を`Void`で、1をunit型`()`で、冪乗を関数型でそれぞれ置き換える。それは、`Void`から任意の型`a`への関数の集合が、単集合であるunit型と等価だと主張する。言い換えると、関数`Void -> a`は1つしかない。この関数は以前にも見たことがある：`absurd`と呼ばれる関数だ。

2つの理由から、これは少しトリッキーだ。1つは、Haskellは実際には無人型 (uninhabited type) を持たないことだ――すべての型には「終わりのない計算の結果」、つまりボトムが含まれている。第2の理由は、`absurd`のすべての実装は等価であるということだ。なぜなら、それらが何をしようと、誰も実行できないからだ。`absurd`に渡せる値はない。（そして、終わりのない計算を渡せたとしても、決して値は返らない！）

### 1の冪乗

$$1^{a} = 1$$

この恒等射は、$\Set$で解釈するとき、終対象の定義を「どの対象にも終対象への一意な射がある」と言い換える。一般に、$a$から終対象への内部hom対象は、終対象そのものと同型だ。

Haskellでは、型`a`からunit型への関数は1つしかない。この関数は以前にも見たことがある。`unit`と呼ばれる関数だ。`()`に部分適用された`const`関数とも見なせる。

### 1乗

$$a^{1} = a$$

これは、終対象からの射が対象`a`の「要素」を選ぶのに利用できるという観察を言い換えたものだ。このような射の集合は対象そのものと同型だ。$\Set$とHaskellでは、集合`a`の要素と、それらの要素を選択する関数`() -> a`との間に同型が成り立つ。

### 和による冪乗

$$a^{b+c} = a^{b} \times a^{c}$$

これは、圏論では2つの対象の余積による冪乗が2つの冪乗の積と同型だと明確に示している。Haskellでは、この代数的恒等射には非常に実用的な解釈がある。これは、2つの型の直和型を取る関数が、個別の型を取る関数のペアと等価だと示している。直和型を取る関数を定義するときに使うケース分析そのものだ。`case`式で1つの関数定義を記述する代わりに、通常はそれを2つ（またはそれ以上）の関数に分割して、それぞれの型コンストラクターを別々に処理する。たとえば、直和型 `(Either Int Double)` を取る関数を考える：

```haskell
f :: Either Int Double -> String
```

これは、それぞれ`Int`と`Double`を取る2つの関数のペアとして定義できる。

```haskell
f (Left n)  = if n < 0 then "Negative int" else "Positive int"
f (Right x) = if x < 0.0 then "Negative double" else "Positive double"
```

ここで、`n`は`Int`で、`x`は`Double`だ。

### 冪乗の冪乗

$$(a^{b})^{c} = a^{b \times c}$$

これは単にカリー化を純粋に冪乗対象の観点で表現したものだ。関数を返す関数は、積を引数に取る関数（2引数の関数）と等価だ。

### 積の冪乗

$$(a \times b)^{c} = a^{c} \times b^{c}$$

Haskellでは、ペアを返す関数は、それぞれがペアの1つの要素を生成する関数のペアと等価だ。

これらの高校数学の単純な代数的恒等式が、このように圏論にリフトされ、関数型プログラミングに実際に応用されているのは、実に驚くべきことだ。

## カリー・ハワード同型

論理と代数的データ型の対応についてはすでに述べた。`Void`型とunit型`()`は、偽と真に対応する。直積型と直和型は、論理積$\wedge$ (AND) と論理和$\vee$ (OR) に対応する。このスキームでは、先ほど定義した関数型が論理包含$\Rightarrow$に対応している。つまり、型`a -> b`は「aならばb」と読める。

カリー・ハワード同型によれば、すべての型は命題として解釈できる。命題とは、真または偽に定まる言明や判断だ。そのような命題は、型が居住 (inhabited) ならば真とされ、そうでなければ偽とされる。特に、論理包含は対応する関数型が居住ならば真であり、その型の関数が存在することを意味する。したがって、関数の実装は定理の証明になる。プログラムを書くのは定理を証明するのと等価だ。いくつか例を見てみよう。

関数対象の定義で導入した関数`eval`を取り上げよう。シグネチャーは次のとおりだ：

```haskell
eval :: ((a -> b), a) -> b
```

これは関数とその引数のペアを取り、適切な型の結果を生成する。つまり、次の射をHaskellで実装したものだ：

$$\mathit{eval} \Colon (a \Rightarrow b) \times a \to b$$

この射は関数型$a \Rightarrow b$（すなわち冪乗対象$b^{a}$）を定義する。このシグネチャーを、カリー・ハワード同型を使って論理の述語に変換しよう。

$$((a \Rightarrow b) \wedge a) \Rightarrow b$$

この命題の読み方はこうだ：$a$ならば$b$が真であり、かつ$a$が真ならば、$b$は必ず真である。これは完全に直観に適っていて、古代から[modus ponens]{.keyword #modus_ponen}として知られていた。次の関数を実装することで、この定理を証明できる：

```haskell
eval :: ((a -> b), a) -> b
eval (f, x) = f x
```

`a`を取り`b`を返す関数`f`と、型`a`の具体的な値`x`とのペアがあれば、`f`を`x`に適用するだけで型`b`の具体的な値を得られる。この関数を実装することで、型`((a -> b), a) -> b`が居住であることを示した。modus ponensは我々の論理では真だ。

では、あからさまに間違っている述語ではどうだろうか？
例：$a$または$b$が真ならば$a$は真でなければならない。

```haskell
a ⋁ b ⇒ a
```

これは明らかに間違っている。なぜなら、$a$が偽で$b$が真の場合が反例となるからだ。

この述語をCurry-Howard同型を使って関数シグネチャーに写すと、次のようになる：

```haskell
Either a b -> a
```

いくらやってみても、この関数は実装できない――`Right`の値で呼び出された場合、型`a`の値は生成できない。（ここでは*純粋*関数について説明していることを思い出してほしい。）

最終的に、`absurd`関数の意味にたどり着く：

```haskell
absurd :: Void -> a
```

`Void`が偽に変換されることを考えると、次のようになる：

$$\mathit{false} \Rightarrow a$$

虚偽からは何でも導ける (_ex falso quodlibet_)。Haskellにおけるこの命題（関数）の証明（実装）として可能なものを以下に1つ示す。

```haskell
absurd (Void a) = absurd a
```

ここで、`Void`は次のように定義される：

```haskell
newtype Void = Void Void
```

いつものように、型`Void`はトリッキーだ。この定義では、値を構築するには値を提供する必要があるため、値を構築できない。したがって、この関数`absurd`は決して呼び出せない。

いずれも興味深い例だが、Curry-Howard同型に実用面はあるのだろうか？
おそらく日々のプログラミングではないだろう。しかし、AgdaやCoqのようなプログラミング言語では、定理を証明するためにCurry-Howard同型が利用されている。

コンピューターは数学者の仕事を助けているだけでなく、数学の基礎そのものに革命をもたらしている。この分野の注目の最新研究テーマはホモトピー型理論と呼ばれ、型理論の副産物だ。ブーリアン、整数、積と余積、関数型などでいっぱいだ。そして、疑念を払拭するかのように、理論はCoqとAgdaで定式化されている。コンピューターは世界にさまざまな形で革命を起こしている。

## 参考文献

1. Ralph Hinze, Daniel W. H. James, [Reason Isomorphically!](http://www.cs.ox.ac.uk/ralf.hinze/publications/WGP10.pdf). この論文には、この章で述べた圏論におけるすべての高校数学の代数的恒等式の証明が含まれている。
