# 極限と余極限 {#limits-and-colimits}

圏論では、すべてがすべてに関係していて、すべてをいろいろな角度から見られるようだ。たとえば、[積](#products-and-coproducts)の普遍構成を考えてみよう。[関手](#functors)と[自然変換](#natural-transformations)について理解が深まった今、それらを単純化し、できれば一般化できないだろうか。やってみよう。

![](images/productpattern.jpg){width=30%}

積の構成は、積を構築したい2つの対象$a$と$b$を選択することから始まる。しかし、*対象を選択する*とは何を意味するのだろう？
もっと圏論らしい言葉で言い換えられないだろうか？
2つの対象は1つのパターンを形成する――ごく単純なパターンだ。このパターンは圏に抽象化できる。ごく単純な圏だが、それでも紛れもなく圏だ。これは、$\cat{2}$と呼ばれる圏だ。$1$と$2$の2つの対象だけを含み、2つの必須の恒等射以外の射は含まない。ここで、$\cat{C}$内の2つの対象の選択を、関手$D$を圏$\cat{2}$から$\cat{C}$に定義する動作と言い換えられる。関手は対象を対象に写すため、その像はちょうど2つの対象になる (関手が対象を潰す場合は1つの対象になることもある)。また、射も写される――この場合は、単に恒等射を恒等射に写しているだけだ。

このアプローチの素晴らしいところは、圏論的な概念に基づいて構築されていることであり、「対象を選ぶ」といった不正確な、まるで我々の祖先の狩猟採集民の語彙から直接取ったような記述を避けていることだ。また、付随的に、簡単に一般化することもできる。パターンを定義するために$\cat{2}$より複雑な圏を使うのを止めるものはないからだ。

![](images/two.jpg){width=35%}

しかし、続けよう。積を定義する次のステップは、候補となる対象$c$の選択だ。ここでも、単元圏からの関手という観点で選択を言い換えられる。そして実際、Kan拡張を使っていたら、そうするのが正しい。しかし、まだKan拡張が用意できていないので、別のトリックを使おう。同じ圏$\cat{2}$から$\cat{C}$への定関手$\Delta$だ。$\cat{C}$で$c$を選択するには$\Delta_c$を使う。$\Delta_c$はすべての対象を$c$に写し、すべての射を$\idarrow[c]$に写すことを思い出してほしい。

![](images/twodelta.jpg){width=35%}

これで、$\Delta_c$と$D$の2つの関手が$\cat{2}$と$\cat{C}$の間にあるため、これらの関手の間の自然変換について質問するのは自然だ。$\cat{2}$には2つの対象しかないため、自然変換には2つのコンポーネントがある。$\cat{2}$内の対象$1$は、$\Delta_c$によって$c$に、$D$によって$a$に写される。したがって、$1$の$\Delta_c$と$D$の間の自然変換のコンポーネントは、$c$から$a$への射だ。それは$p$と呼べる。同様に、2番目のコンポーネントは、$c$から$b$までの射$q$だ。これは、$\cat{2}$内の対象$2$の、$D$における像だ。しかし、これらは元々の積の定義で用いた2つの射影と全く同じだ。したがって、対象や射影の選択について議論する代わりに、ただ関手や自然変換の選択について議論すればよい。ここでの単純なケースでは、変換の自然条件が満たされるのは自明だ。なぜなら、$\cat{2}$には射が (恒等射しか) 存在しないからだ。

![](images/productcone.jpg){width=35%}

この構成を$\cat{2}$以外の圏 (たとえば、自明でない射を含む圏) に一般化すると、$\Delta_c$と$D$の間の変換に自然条件が課される。このような変換を*錐*  (cone) と呼ぶ。なぜなら、自然変換のコンポーネントによって側面が形成される錐・ピラミッドを考えると、$\Delta$の像がその頂点だからだ。$D$の像は錐の底面を形成する。

一般に、錐を構築するには、パターンを定義する圏$\cat{I}$から始める。小さな、有限なことが多い圏だ。$\cat{I}$から$\cat{C}$への関手$D$を選択し、それ (またはその像) を*図式*と呼ぶ。錐の頂点として、$\cat{C}$中の$c$を選択する。それを使って$\cat{I}$から$\cat{C}$への定関手$\Delta_c$を定義する。$\Delta_c$から$D$までの自然変換が、我々の錐となる。有限の$\cat{I}$に対しては、それは$c$と図式を接続するただの射の集まりで、$D$における$\cat{I}$の像だ。

![](images/cone.jpg){width=35%}

自然性は、この図式のすべての三角形 (ピラミッドの側面) が可換であることを必要とする。実際に、$\cat{I}$内の任意の射$f$を取ったとする。関手$D$は、それを$\cat{C}$内の射$D f$に、すなわち、ある三角形の底辺となる射に写す。定関手$\Delta_c$は、$f$を$c$における恒等射に写す。$\Delta$は射の両端を1つの対象にまとめ、自然性の正方図式は可換な三角形になる。この三角形の2本の腕は自然変換のコンポーネントだ。

![](images/conenaturality.jpg){width=35%}

したがって、これは1つの錐だ。関心があるのは*普遍的な錐*だ――積の定義のために普遍的な対象を選んだのと同じだ。

それを目指す方法はいろいろある。たとえば、任意の関手$D$に基づいて*錐の圏*を定義できる。その圏における対象は錐だ。ただし、$\cat{C}$のすべての対象$c$が錐の頂点になるわけではない。なぜなら、$\Delta_c$と$D$の間に自然変換が存在しない可能性があるからだ。

圏にするためには、錐の間の射も定義しなければならない。それらは頂点間の射によって完全に決定される。しかし、どんな射でも良いわけではない。我々の積の構成では、候補となる対象 (頂点) の間の射は射影の共通因子でなければならない、という条件を課したのを思い出してほしい。例として:

```haskell
p' = p . m
q' = q . m
```

![](images/productranking.jpg){width=35%}

この条件は、一般的な場合には、一辺が分解射である三角形はすべて可換である、という条件に変換される。

![分解射$h$を持つ2つの錐を結ぶ可換な三角形 (ここでは、下側の錐は普遍的な三角形で、その頂点は$\Lim[D]$である)。](images/conecommutativity.jpg){width=35%}

これらの分解射を錐の圏における射と見なそう。これらの射が実際に合成することと、恒等射が分解射であることを確認するのは簡単だ。したがって、錐は圏を形成する。

これで、普遍錐 (universal cone) を錐の圏の*終対象*として定義できるようになった。終対象の定義では、任意の対象からその対象への一意な射があるとされている。我々の場合、それは他の任意の錐の頂点から普遍射の頂点への一意な分解射があることを意味する。この普遍錐を、図式$D$と$\Lim[D]$の*極限* (limit) と呼ぶ ($\Lim$記号の下に$I$に向かう左矢印を記すことが多い)。簡単に、この錐の頂点を極限 (あるいは極限対象) と呼ぶこともよくある。

直観的には、極限は図式全体の特性を単一の対象で具現化している。たとえば、2対象の図式の極限は、その2つの対象の積だ。積 (および2つの射影) には、両方の対象に関する情報が含まれている。そして、普遍であるということは外来のゴミがないことを意味する。

## 自然同型としての極限

この極限の定義にはどこか不満が残る。どういうことかと言うと、使えなくはないが、任意の2つの錐を結ぶ三角形には依然としてこの可換性条件が課されている。もしそれを何らかの自然条件に置き換えられれば、はるかにエレガントになるだろう。だが、どうやって？


ここで扱っているのは、もはや1つの錐ではなく、錐の集まり (実際には圏) 全体だ。極限が存在するなら (そして――明言しておくと――存在する保証はない)、それらの錐の1つは普遍錐だ。その他のすべての錐について、その頂点を普遍錐の頂点$\Lim[D]$に写す一意な分解射$c$が存在する。(実際には「その他の」という言葉を省いても構わない。なぜなら、恒等射は普遍錐をそれ自身に写し、それ自身を分解するのは自明だからだ。)重要な部分を繰り返そう: 任意の錐について、特別な種類の一意な射が存在する。錐から特別な射への写像があり、それは1対1の写像だ。

この特別な射は、hom集合$\cat{C}(c, \Lim[D])$ のメンバーだ。このhom集合の他のメンバーは、2つの錐の写像を分解しないという意味で、あまり幸運ではない。ここで必要なのは、$c$ごとに、$\cat{C}(c, \Lim[D])$ から1つの射――特定の可換性条件を満たす射――を選択できるようにすることだ。まるで自然変換を定義しているかのように聞こえるだろうか？
そのとおりだ！


しかし、この変換に関係する関手は何だろうか？


関手の1つは、$c$から集合$\cat{C}(c, \Lim[D])$ への写像だ。これは$\cat{C}$から$\Set$への関手で、対象を集合に写す。実際は反変関手だ。射に関する作用を定義する方法は次のとおりだ: $c'$から$c$への射$f$を例に取ろう:

$$f \Colon c' \to c$$

我々の関手は$c'$を集合$\cat{C}(c', \Lim[D])$ に写す。この関手の$f$に対する作用を定義する (言い換えると、$f$をリフトする) には、$\cat{C}(c, \Lim[D])$ と$\cat{C}(c', \Lim[D])$ の間の対応する写像を定義する必要がある。したがって、$\cat{C}(c, \Lim[D])$ の要素$u$を1つ選択し、それを$\cat{C}(c', \Lim[D])$ の要素に写せるか確認する。hom集合の要素は射なので、次のようになる:

$$u \Colon c \to \Lim[D]$$

$u$を$f$に前置合成 (precompose) すると、次のようになる:

$$u . f \Colon c' \to \Lim[D]$$

そして、これは$\cat{C}(c', \Lim[D])$ の要素だ――つまり、実際に射の写像を見つけた:

```haskell
contramap :: (c' -> c) -> (c -> Lim D) -> (c' -> Lim D)
contramap f u = u . f
```

*反変*関手の特徴である、$c$と$c'$の順序の反転に注目してほしい。

![](images/homsetmapping.jpg){width=35%}

自然変換を定義するには、同じく$\cat{C}$から$\Set$への写像である別の関手が必要だ。しかし、今回は錐の集合を考えてみよう。錐は単なる自然変換なので、我々は自然変換の集合$Nat(\Delta_c, D)$ を見ている。`c`からこの自然変換の特定の集合への写像は (反変) 関手だ。どうやってそれを示そう？
ここでも、射の作用を定義してみよう:

$$f \Colon c' \to c$$

$f$のリフトは、$\cat{I}$から$\cat{C}$へ向かう関手2つの間の自然変換の写像でなければならない。

$$Nat(\Delta_c, D) \to Nat(\Delta_{c'}, D)$$

自然変換をどのように写せば良いだろう？
自然変換は射――そのコンポーネント――の選択であり、$\cat{I}$の要素ごとに射を1つ選択する。$a$ ($\cat{I}$内の対象) における、ある$\alpha$ ($Nat(\Delta_c, D)$ のメンバー) のコンポーネントは、次の射だ:

$$\alpha_a \Colon \Delta_c a \to D a$$

または、定関手$\Delta$の定義を使うと、

$$\alpha_a \Colon c \to D a$$

となる。$f$と$\alpha$が与えられた場合、$Nat(\Delta_{c'}, D)$ のメンバーである$\beta$を構築する必要がある。$a$におけるそのコンポーネントは次の射となる:

$$\beta_a \Colon c' \to D a$$

前者 ($\alpha_a$) から後者 ($\beta_a$) を得るのは、$f$を前置合成すれば簡単だ:

$$\beta_a = \alpha_a . f$$

これらのコンポーネントが実際に自然変換になることを示すのは比較的簡単だ。

![](images/natmapping.jpg){width=40%}

以上より、我々の射$f$を仮定して、2つの自然変換の間の写像をコンポーネントごとに構築した。この写像は関手の`contramap`を定義する:

$$c \to Nat(\Delta_c, D)$$

先ほど見せたのは、$\cat{C}$から$\Set$への2つの (反変) 関手があるということだ。何の仮定もしなかった――これらの関手は常に存在する。

ちなみに、これらの関手のうち第1のものは圏論で重要な役割を果たしている。米田の補題について話すときに再び見ることになるだろう。任意の圏$\cat{C}$から$\Set$への反変関手は「前層」(presheaf) と呼ばれる。ここでのものは[表現可能前層]{.keyword #representable_presheaf} (representable presheaf) と呼ばれている。第2の関手も前層だ。

2つの関手が出てきたので、その間の自然変換について話せる。そこで、これ以上何も言わず、結論を述べよう: $\cat{I}$から$\cat{C}$への関手$D$は極限$\Lim[D]$を持つ。ただし、先ほど定義した2つの関手間に自然同型がある場合に限る。

$$\cat{C}(c, \Lim[D]) \simeq Nat(\Delta_c, D)$$

自然同型とは何かを思い出してほしい。すべてのコンポーネントが同型、つまり可逆な射である自然変換だ。

この命題の証明をなぞるつもりはない。その手順は、退屈とまでは言わないにしても非常に単純だ。自然変換を扱うときには、通常は、射であるコンポーネントに注目する。この場合、両方の関手の終域が$\Set$なので、自然同型のコンポーネントは関数になる。それらは高階関数だ。なぜなら、それらは、hom集合から自然変換の集合へと向かうからだ。再び、関数が引数に対して何を行うかを調べることで関数を分析できる。ここで、引数は射――$\cat{C}(c, \Lim[D])$ のメンバーで、結果は自然変換――$Nat(\Delta_c, D)$ のメンバー、または錐と呼ばれるものになる。この自然変換には独自のコンポーネントがあり、それは射だ。したがって、どこまでも射なので、それらを追跡できれば、先ほどの命題を証明できる。

最も重要な結果は、この同型に対する自然条件がまさしく錐の写像についての可換性条件だということだ。

今後のアトラクションの予告として、集合$Nat(\Delta_c, D)$ を関手圏におけるhom集合と見なせることに触れておきたい。つまり、ここでの自然同型は2つのhom集合を関連づけ、随伴と呼ばれるさらに一般的な関係を指し示している。

## 極限の例

圏論的な積は、$\cat{2}$と呼ばれる単純な圏によって生成される図式の極限であることを見てきた。

極限のさらに単純な例として、終対象がある。初期衝動としては単元圏が終対象につながると考えそうだが、真実はさらに赤裸々だ: 終対象は空圏によって生み出された極限だ。空圏からの関手は対象を選択しないので、錐は頂点だけに縮む。普遍錐はその唯一の頂点で、他の頂点からの一意な射を持つ。これが終対象の定義だと気付くだろう。

次に興味深い極限は*イコライザ*と呼ばれる。これは2つの要素からなる圏によって生成された極限であり、その間の2つの平行な射を伴う (そして、いつものように、それらは恒等射だ)。この圏は、2つの対象$a$と$b$と、2つの射とで構成される$\cat{C}$の図式を選択する。

```haskell
f :: a -> b
g :: a -> b
```

この図式の上に錐を構築するには、頂点$c$と2つの射影を追加する必要がある。

```haskell
p :: c -> a
q :: c -> b
```

![](images/equalizercone.jpg){width=35%}

可換でなければならない三角形が2つある:

```haskell
q = f . p
q = g . p
```

これは、$q$がこれらの方程式の1つによって一意に決定されることを示している。たとえば`q = f . p`で、図から省略できる。残りの条件は1つだけだ:

```haskell
f . p = g . p
```

つまり、$\Set$だけに注目すると、関数$p$の像は$a$の部分集合を選択している。この部分集合に限定したとき、関数$f$と$g$は等しくなる。

たとえば、$a$を座標$x$と$y$でパラメータ化された2次元平面とする。$b$を実数の直線とし、次のようにする:

```haskell
f (x, y) = 2 * y + x
g (x, y) = y - x
```

これら2つの関数のイコライザは、実数の集合 (頂点$c$) と次の関数だ:

```haskell
p t = (t, (-2) * t)
```

$(p~t)$ は2次元平面内の直線を定義することに注意してほしい。この線に沿って、2つの関数は等しい。

当然、等値性につながる可能性のある他の集合$c'$と関数$p'$もある:

```haskell
f . p' = g . p'
```

ただし、それらはすべて$p$を通じて一意に分解する。たとえば、単集合$\cat{()}$を$c'$として受け取り、次の関数を使える:

```haskell
p'() = (0, 0)
```

$f(0, 0) = g(0, 0)$ だから、これは良い錐だ。しかし、これは普遍ではない。なぜなら、$h$による一意な分解:

```haskell
p' = p . h
```

with

```haskell
h () = 0
```

![](images/equilizerlimit.jpg){width=35%}

したがって、イコライザは型$f~x = g~x$の方程式を解くのに使える。ただし、もっと汎用的に使える。なぜなら、代数的な定義ではなく、対象や射の観点で定義されているからだ。

方程式を解くという考え方をさらに一般化したものが、別の極限で具体化されている――引き戻し (pullback) だ。ここに、等価にしたい2つの射がまたあるが、今回はそれらの始域が異なる。まず$1\rightarrow2\leftarrow3$という形の3対象の圏を作成する。この圏に対応する図式は、3つの対象 ($a$, $b$, $c$) と2つの射で構成されている:

```haskell
f :: a -> b
g :: c -> b
```

この図式は*余スパン* (cospan) と呼ばれることが多い。

この図式の上に構築された錐は、頂点$d$と3つの射から構成されている:

```haskell
p :: d -> a
q :: d -> c
r :: d -> b
```

![](images/pullbackcone.jpg){width=35%}

可換性条件から、$r$は他の射によって完全に決定されることがわかり、図から省略できる。したがって、次の条件のみが残る:

```haskell
g . q = f . p
```

引き戻しは、この形の普遍錐だ。

![](images/pullbacklimit.jpg){width=35%}

再び、注目する範囲を集合だけに絞ると、対象$d$は、$a$と$c$からの要素のペアで構成されていると見なせる。ここで、$f$が最初のコンポーネントに作用したものは、$g$が2番目のコンポーネントに作用したものと等しい。これがまだ一般的すぎるなら、$g$が定数関数である特別な場合を考えてみてほしい。たとえば、 ($b$が実数の集合だと仮定して) $g~\_ = 1.23$とする。そうすれば、実際に方程式を解いていることになる:

```haskell
f x = 1.23
```

この場合、$c$として何を選択しても (空集合でない限り) 関係ないので、単集合として構わない。たとえば、集合$a$は3次元ベクトルの集合であり、$f$はベクトル長だ。そうすれば、引き戻しはペア $(v, ())$ の集合だ。ここで、$v$は長さ1.23のベクトル (方程式$\sqrt{(x^{2}+y^{2}+z^{2})} = 1.23$の解) であり、$()$ は単集合のダミー要素だ。

しかし、引き戻しにはより一般的な用途があり、プログラミングで用いることもできる。たとえば、C++のクラスを圏と考えてみよう。その射はサブクラスとスーパークラスを結ぶ矢だ。継承を推移的なプロパティと見なそう。つまり、`C`が`B`を継承し、`B`が`A`を継承しているなら、`C`は`A`を継承していると言える (要するに、`A`へのポインタを期待する箇所に`C`へのポインタを渡せる)。また、`C`は`C`から継承すると見なそう。つまり、すべてのクラスに恒等射がある。このようにすればサブクラス化はサブタイプ化と整合が取れる。C++は多重継承もサポートしているので、`A`を継承する2つのクラス`B`と`C`、および`B`と`C`とを多重継承する4番目のクラス`D`を含むダイヤモンド継承の図式を構築できる。通常、`D`は`A`のコピーを2つ取得する。ほとんどの場合、これは望ましくない。しかし、仮想継承を使えば`D`内の`A`のコピーを1つだけにできる。

`D`がこの図式の引き戻しになるとはどういうことだろう？
それは、`B`と`C`を多重継承するすべてのクラス`E`が、`D`のサブクラスでもあることを意味する。これは直接C++で表現できない。C++は公称型 (nominal subtyping) を採用しているからだ (C++コンパイラはこの種のクラス関係を推測しない――それには「ダック・タイピング」が必要となる)。しかし、サブタイプ関係の外に出て、代わりに`E`から`D`へのキャストが安全かどうか尋ねられる。このキャストが安全なのは、`D`が`B`と`C`の必要最小限の組み合わせで、追加データやメソッドのオーバーライドがない場合だ。そしてもちろん、`B`と`C`のメソッドに名前の衝突がある場合、引き戻しはない。

![](images/classes.jpg){width=25%}

さらに、型推論では引き戻しがより高度な使い方をされる。多くの場合、2つの表現の型を*unify*する必要がある。たとえば、コンパイラが関数の型を推測しようとしているとする:

```haskell
twice f x = f (f x)
```

すべての変数と部分式に予備的な型を割り当てる。具体的には、以下を割り当てる:

```haskell
f       :: t0
x       :: t1
f x     :: t2
f (f x) :: t3
```

そこから次のことが推測される:

```haskell
twice :: t0 -> t1 -> t3
```

また、関数の適用規則から生じる一連の制約も課される:

```haskell
t0 = t1 -> t2 -- fがxに適用されるため。
t0 = t2 -> t3 -- fが (f x) に適用されるため。
```

これらの制約を統一するために、ある型 (または型変数) の集合を見つけて、その型で両方の式の未知の型に代入し、同じ型を生成させる必要がある。そのような置換の1つは次のとおりだ:

```haskell
t1 = t2 = t3 = Int
twice :: (Int -> Int) -> Int -> Int
```

しかし、明らかに、これは最も一般的ではない。最も一般的な置換を得るには引き戻しを用いる。詳細については、本書の範囲外であるため、ここでは説明しないが、次のような結果になることを確信できる:

```haskell
twice :: (t -> t) -> t -> t
```

ここで`t`は自由な型変数だ。

## 余極限

圏論のすべての構造と同じように、極限には双対となる像が反対圏にある。錐内のすべての射の方向を反転させると、余錐 (co-cone) が1つでき、それらのうち普遍なものが余極限 (colimit) と呼ばれる。反転が分解射にも影響することに注意してほしい。分解射は普遍な余錐から他の余錐へと流れることになる。

![2つの頂点を結ぶ分解射`h`を持つ余錐。](images/colimit.jpg){width=35%}

余極限の典型的な例は余積だ。これは、積の定義で使った$\cat{2}$圏によって生成される図式に対応する。

![](images/coproductranking.jpg){width=35%}

積も余積も、別々の方法で対象のペアの本質を具現化している。

終対象が極限だったように、始対象は空圏に基づく図式に対応する余極限だ。

引き戻しの双対は*押し出し*と呼ばれる。これはスパンと呼ばれる図式に基づいており、圏$1\leftarrow2\rightarrow3$によって生成される。

## 連続性

すでに述べたように、関手は、既存の接続 (射) を決して壊さないという意味で、圏の連続的な写像の概念に近い。圏$\cat{C}$から$\cat{C'}$への\emph{連続関手}$F$の実際の定義には、関手が極限を保存するという要件が含まれている。$\cat{C}$内のすべての図式$D$は、単に2つの関手を合成すれば、$\cat{C'}$内の図式$F\circ D$に写せる。$F$の連続性条件は、図式$D$が極限$\Lim[D]$を有するなら、図式$F \circ D$も極限を有し、それは$F (\Lim[D])$ に等しいことを示している。

![](images/continuity.jpg){width=60%}

関手は射を射に、合成を合成に写すので、錐の像は常に錐であることに注意してほしい。合成の三角形は常に合成の三角形に写される (関手は合成を保存する)。分解射についても同じことが言える: 分解射の像も分解射だ。したがって、すべての関手は*ほぼ*連続している。問題になる可能性があるのは、一意性条件だ。$\cat{C'}$の分解射は一意でないことがある。$\cat{C'}$には、$\cat{C}$にはなかった「より優れた錐」が他にあるかもしれない。

Hom関手は連続関手の一例だ。Hom関手、$\cat{C}(a, b)$ は、最初の変数について反変であり、2番目の変数について共変であることを思い出してほしい。言い換えれば、次の関手だ:

<pre>
C<sup>op</sup> × C -> Set
</pre>

第2引数が固定されると、hom集合関手 (表現可能前層になる) は$\cat{C}$の余極限を$\Set$の極限に写す。第1引数が固定されると、極限を極限に写す。

Haskellでのhom関手は、任意の2つの型から関数型への写像であるため、単なるパラメータ化された関数型だ。2番目のパラメータを、たとえば`String`に固定すると、次の反変関手が得られる:

```haskell
newtype ToString a = ToString (a -> String)
instance Contravariant ToString where
    contramap f (ToString g) = ToString (g . f)
```

連続性とは、`ToString`が余極限、たとえば余積`Either b c`に適用された場合、極限を生成することを意味する。この場合、2つの関数型の積は次のようになる:

```haskell
ToString (Either b c) ~ (b -> String, c -> String)
```

実際、`Either b c`の関数はどれも、関数のペアによって提供される型2つを含む型宣言として実装される。

同様に、hom集合の第1引数を固定すると、おなじみのreader関手が得られる。連続性とは、たとえば、積を返す関数すべてが関数の積と等価であることを意味する。具体的には:

```haskell
r -> (a, b) ~ (r -> a, r -> b)
```

読者がどう思っているかは分かっている: これらを理解するのに圏論は必要ない。その通りだ！
それでも、このような結果が、ビットやバイト、プロセッサアーキテクチャ、コンパイラ技術、さらにはラムダ計算に頼ることなく、第一原理から得られるのは驚くべきことだと思う。

「極限」と「連続性」という名前の由来が何なのか気になるなら、それらは微積分学において対応する概念を一般化したものだ。微積分学では、極限と連続性は開近傍に関連して定義される。開集合は、トポロジーを定義し、圏 (ポセット) を形成する。

## 課題

1. C++クラスの圏における押し出しを説明するにはどうするか？


2. 恒等関手$\mathbf{Id} \Colon \cat{C} \to \cat{C}$の極限が始対象であることを示せ。

3. 任意の集合の部分集合は圏を形成する。その圏の射は、1番目の集合が2番目の集合の部分集合である場合、それら2つの集合を接続する矢として定義される。そのような圏の集合2つの引き戻しは何か？
   押し出しは何か？
   始対象と終対象は何か？


4. 余イコライザとは何か予想できるか？


5. 終対象が存在する圏では、終対象に向かう引き戻しが積であることを示せ。

6. 同様に、始対象が存在するなら、始対象からの押し出しが余積であることを示せ。
